#include "Globals.dh"
#include "Menu.dh"
#include "Minigame.dh"
#include "RPG.dh"
#include "Shield.dh"
#include "Shop.dh"
#include "Utils.dh"

int ShopCategories = 8; // 8 vanilla ShopCategories, 8 reserved for compatibility
int ShopSpotID = 1000;

str[SHOP_CATEGORIES] ShopNames =
{
	"Weapons";
	"Ammo";
	"Health";
	"Armor";
	"Powerups";
	"Tokens";
	"Shield Parts";
	"Augmentations";
};

int[SHOP_CATEGORIES] ShopMax =
{
	8; 11; 9; 11; 24; 8; 0; 3;
};

ShopItem[SHOP_CATEGORIES][SHOP_ITEMS] Shop =
{
	// Name, Actor, Price, Minimum Rank
    
	// Weapons
	{
		{ "Chainsaw";				"Chainsaw";							300;		0;	};
		{ "Pistol"; 				"Pistol"; 							25; 		0;	};
		{ "Shotgun"; 				"Shotgun"; 							100; 		0;	};
		{ "SuperShotgun";			"Super Shotgun";					500;		1;	};
		{ "Chaingun";				"Chaingun";							750;		0;	};
		{ "RocketLauncher";			"Rocket Launcher";					1000;		1;	};
		{ "PlasmaRifle";			"Plasma Rifle";						2500;		1;	};
		{ "BFG9000";				"BFG9000";							5000;		1;	};
	};
	
	// Ammo
	{
		{ "Clip";					"Clip";								10;			0;	};
		{ "ClipBox";				"Box of Bullets";					50;			0;	};
		{ "Shell";					"Shells";							25;			0;	};
		{ "ShellBox";				"Box of Shells";					125;		0;	};
		{ "RocketAmmo";				"Rockets";							50;			1;	};
		{ "RocketBox";				"Box of Rockets";					250;		1;	};
		{ "Cell";					"Cells";							200;		1;	};
		{ "CellPack";				"Cell Pack";						1000;		1;	};
		{ "SmallBackpack";			"Small Backpack";					285;		1;	};
		{ "Backpack";				"Backpack";							1425;		1;	};
		{ "BigBackpack";			"Big Backpack";						6325;		1;	};
	};

    // Health
	{
		{ "HealthBonus"; 			"Health Bonus"; 					1; 			0;	};
		{ "Stimpack"; 				"Stimpack"; 						50; 		0; 	};
		{ "Medikit"; 				"Medikit"; 							100; 		0; 	};
		{ "LargeMedikit"; 			"Large Medikit"; 					250; 		1; 	};
		{ "XLMedikit"; 				"Extra-Large Medikit"; 				500; 		1; 	};
		{ "MedPack"; 				"Med Pack"; 						750; 		1; 	};
		{ "SurgeryKit"; 			"Surgery Kit"; 						1000; 		2; 	};
		{ "Soulsphere"; 			"Soulsphere"; 						2000; 		2; 	};
		{ "Continue"; 				"Continue"; 						50000; 		4; 	};
	};

    // Armor
	{
		{ "ArmorBonus"; 			"Armor Bonus";						1; 			0; 	};
		{ "GreenArmor"; 			"Green Armor"; 						100; 		0; 	};
		{ "UsedGreenArmor"; 		"Green Armor (Used)"; 				50; 		0; 	};
		{ "BlueArmor"; 				"Blue Armor"; 						500; 		0; 	};
		{ "UsedBlueArmor"; 			"Blue Armor (Used)"; 				250; 		0; 	};
		{ "YellowArmor"; 			"Yellow Armor"; 					1000; 		1; 	};
		{ "UsedYellowArmor"; 		"Yellow Armor (Used)"; 				500; 		1; 	};
		{ "RedArmor"; 				"Red Armor"; 						5000; 		2; 	};
		{ "UsedRedArmor"; 			"Red Armor (Used)"; 				2500; 		2; 	};
		{ "WhiteArmor"; 			"White Armor"; 						10000; 		4; 	};
		{ "UsedWhiteArmor"; 		"White Armor (Used)"; 				5000; 		4; 	};
	};
	
	// Powerups
	{
		{ "InvulnerabilitySphere";	"Invulnerability Sphere"; 			5000; 		0; 	};
		{ "InvulnerabilityCharge"; 	"Invulnerability Charge"; 			10000; 		2; 	};
		{ "BlurSphere"; 			"Invisibility Sphere"; 				1000;		0;	};
		{ "InvisibilityCharge"; 	"Invisibility Charge"; 				2000;		2;	};
		{ "TimeSphere"; 			"Time Sphere"; 						1000;		4;	};
		{ "RegenSphere"; 			"Regeneration Sphere"; 				1000;		2;	};
		{ "RadSuit"; 				"Radiation Suit"; 					250;		0;	};
		{ "Infrared"; 				"IR Goggles"; 						500;		0;	};
		{ "Berserk"; 				"Berserk Pack"; 					1000;		1;	};
		{ "AllMap"; 				"Computer Area Map"; 				1000;		1;	};
		{ "Wings"; 					"Wings"; 							1000;		2;	};
		{ "StimSmall"; 				"Small Stim"; 						2000;		1;	};
		{ "StimMedium"; 			"Medium Stim"; 						5000;		2;	};
		{ "StimLarge"; 				"Large Stim"; 						10000;		4;	};
		{ "Megasphere"; 			"Megasphere"; 						10000;		4;	};
		{ "StrengthRune"; 			"Strength Rune"; 					2500;		4;	};
		{ "DrainRune"; 				"Drain Rune"; 						2500;		4;	};
		{ "EndlessRune"; 			"Endless Rune"; 					5000;		4;	};
		{ "ResistanceRune"; 		"Resistance Rune"; 					2500;		4;	};
		{ "RegenerationRune"; 		"Regeneration Rune"; 				2500;		4;	};
		{ "FrightRune"; 			"Fright Rune"; 						1000;		4;	};
		{ "GhostRune"; 				"Ghost Rune"; 						2500;		4;	};
		{ "HighJumpRune"; 			"High Jump Rune"; 					1000;		4;	};
		{ "HasteRune"; 				"Haste Rune"; 						1000;		4;	};
	};

	// Tokens
	{
		{ "SmallStatToken"; 		"Stat Token"; 						25000;		0;	};
		{ "BigStatToken"; 			"Big Stat Token"; 					100000;		2;	};
		{ "SmallStatCapToken"; 		"Stat Cap Token"; 					25000;		0;	};
		{ "BigStatCapToken"; 		"Big Stat Cap Token"; 				100000;		2;	};
		{ "SmallSkillToken"; 		"Skill Token"; 						25000;		0;	};
		{ "BigSkillToken"; 			"Big Skill Token"; 					100000;		2;	};
		{ "MegaToken"; 				"Mega Token"; 						250000;		4;	};
		{ "BigMegaToken"; 			"Big Mega Token"; 					500000;		4;	};
	};
	
	// Shield Parts - Built Dynamically
	{};

	// Augmentations
	{
		{ "AugCanister";			"Augmentation Canister"; 			500000;		0;	};
		{ "AugUpgradeCanister";		"Augmentation Upgrade Canister"; 	500000;		4;	};
		{ "AugSlotUpgrade";			"Augmentation Slot Upgrade"; 		1000000;	8;	};
	};
};

// Open the Shop
acscript OpenShop(bool FromBase) net
{
	// If you're dead, return
	if (GetActorProperty(Player.TID, APROP_Health) <= 0) return;
	
	// If you're in any minigames, return
	if (Player.InMinigame) return;
	
	// Close the main menu if it's open
	Player.InMenu = false;
	
	if (Player.InShop)
	{
		// Sanity Check to prevent the shop closing itself after buying an item in the Outpost Shop
		if (!FromBase) return;
		
		ActivatorSound("menu/leave", 127);
		SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
		Player.InShop = false;
	}
	else
	{
		// Don't let you Shop Anywhere if it's disabled
		if (!InBase && !GetCVar("drpg_shoptype")) return;
		
		ActivatorSound("menu/shop", 127);
		SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
		Player.InShop = true;
	};
};

// Sell items in bulk
acscript SellAllPage(int SellPage, bool Silent)
{
	int Total;
	
	// Set font
	SetFont("BIGFONT");
	
	// Invalid Player.ShopPages
	if (SellPage < 0 || SellPage > SHOP_CATEGORIES)
	{
		Print("\cgInvalid Shop Page\n");
		ActivatorSound("menu/error", 127);
		return;
	};
	
	// Sell Items
	for (int i = 0; i < ShopMax[SellPage]; i++)
		if (CheckInventory(Shop[SellPage][i].Actor) > 0)
			Total += SellItem(Shop[SellPage][i].Actor, true)
		else continue;
	
	// Display Message
	if (!Silent)
	{
		HudMessage("Sold all %s items for %d Credits\n",
				   ShopNames[SellPage], Total,
				   HUDMSG_FADEOUT, 0, CR_GOLD, 0.5, 0.5, 5.0, 5.0);
		ActivatorSound("credits/payout", 127);
	};
};

function void ShopBuildPages()
{
	int Index;
	
	// Shield Page
	ShopMax[6] = 0;
	for (int i = 0; i < 4; i++)
	{
		str ShieldActorName = "";
		str ShieldName = "";
		int ShieldCost = 0;
		
		ShopMax[6] += ShieldPartsMax[i];
		
		switch (i)
		{
			case 0: ShieldActorName = "ShieldBody";			ShieldName = "Shield Body - ";		break;
			case 1:	ShieldActorName = "ShieldBattery";		ShieldName = "Shield Battery - ";	break;
			case 2:	ShieldActorName = "ShieldCapacitor";	ShieldName = "Shield Capacitor - ";	break;
			case 3:	ShieldActorName = "ShieldAccessory";	ShieldName = "Shield Accessory - ";	break;
		};
		
		for (int j = 0; j < ShieldPartsMax[i]; j++)
		{
			// Name
			Shop[6][Index].Actor = StrParam("%s%d\n", ShieldActorName, j + 1);
			Shop[6][Index].Name = StrParam("%s%s\c-\n", ShieldName, ShieldData[i][j][1]);
			
			// Price
			if (i < 3) // Bodies, Batteries, Capacitors
			{
				ShieldCost += ShieldStats[i][j][0] * 100;
				ShieldCost += ShieldStats[i][j][1] * 1000;
				ShieldCost += Abs((ShieldStats[i][j][2] * 20000.0));
			}
			else // Accessories
			{
				if (j == 0) ShieldCost = 50000;
				if (j == 1) ShieldCost = 25000;
				if (j == 2) ShieldCost = 10000;
				if (j == 3) ShieldCost = 20000;
				if (j == 4) ShieldCost = 50000;
				if (j == 5) ShieldCost = 25000;
				if (j == 6) ShieldCost = 10000;
			};
			
			Shop[6][Index].Price = ShieldCost;
			
			// Rank
			Shop[6][Index].Rank = j;
			
			Index++;
		};
	};
	
	// Compatibility handling - Extras
	if (GetCVar("drpg_ext_extras"))
	{
		// Weapons
		Shop[0][0].Actor = "Pistol1";
		Shop[0][1].Actor = "Shotgun1";
		Shop[0][2].Actor = "SSG";
		Shop[0][3].Actor = "Chaingun1";
		Shop[0][4].Actor = "RocketLauncher1";
		Shop[0][5].Actor = "Plasmagun";
		Shop[0][6].Actor = "BFG9001";
		Shop[0][7].Actor = "Chainsaw1";
	};
	
	// Compatibility handling - Brutal Doom
	if (GetCVar("drpg_ext_brutal"))
	{
		// Weapons
		ShopMax[0] = 14;
		
		Shop[0] =
		{
			{ "BrutalPistol"; 			"Pistol"; 					25; 	0; 	};
			{ "Rifle"; 					"Assault Rifle"; 			100; 	0; 	};
			{ "Shot_Gun"; 				"Shotgun"; 					250; 	0; 	};
			{ "SSG"; 					"Super Shotgun"; 			500; 	0; 	};
			{ "Mini_Gun"; 				"Minigun"; 					750; 	0; 	};
			{ "Grenade_Launcher"; 		"Grenade Launcher"; 		1000; 	0; 	};
			{ "Rocket_Launcher"; 		"Rocket Launcher"; 			2000; 	0; 	};
			{ "Plasma_Gun"; 			"Plasma Rifle"; 			3000; 	0; 	};
			{ "Rail_Gun"; 				"Railgun"; 					5000; 	0; 	};
			{ "BIG_FUCKING_GUN"; 		"BFG9000"; 					10000;	0; 	};
			{ "BFG10000"; 				"BFG10k"; 					25000; 	0; 	};
			{ "HellishMissileLauncher";	"Hellish Missile Launcher";	5000; 	0; 	};
			{ "FlameCannon";			"Flame Cannon"; 			5000; 	0; 	};
			{ "SecretWeapon_MP40"; 		"MP40"; 					1000; 	0; 	};
			{ "Chain_Saw"; 				"Chainsaw"; 				300; 	0; 	};
		};
	};
	
	// Compatibility Handling - DoomRL
	if (GetCVar("drpg_ext_doomrl"))
	{
		// Categories
		ShopCategories = 10;
		ShopNames[8] = "Mod Packs";
		ShopNames[9] = "Boots";
		
		// Maximums
		ShopMax[0] = 43; // Weapons
		ShopMax[3] = 42; // Armor
		ShopMax[4] = 26; // Powerups
		ShopMax[8] = 8;	 // Mod Packs
		ShopMax[9] = 23; // Boots
		
		// Weapons
		Shop[0] =
		{
			// Common Weapons
			{ "RLChainsaw"; 							"Chainsaw"; 								100;	0; 	};
			{ "RLPistol"; 								"Pistol"; 									25;		0; 	};
			{ "RLShotgun"; 								"Shotgun"; 									100; 	0; 	};
			{ "RLCombatShotgun"; 						"Combat Shotgun"; 							250; 	0; 	};
			{ "RLDoubleShotgun"; 						"Double Shotgun"; 							500; 	1; 	};
			{ "RLChaingun"; 							"Chaingun"; 								750; 	0; 	};
			{ "RLRocketLauncher"; 						"Rocket Launcher"; 							1000; 	1; 	};
			{ "RLPlasmaRifle"; 							"Plasma Rifle"; 							2500; 	1; 	};
			{ "RLBFG9000"; 								"BFG9000"; 									5000; 	2;	};
			
			// Exotic Weapons
			{ "RLBlaster"; 								"Blaster \ct[Exotic]\c-"; 					1000; 	4; 	};
			{ "RLCombatPistol"; 						"Combat Pistol \ct[Exotic]\c-"; 			1500; 	4; 	};
			{ "RLDesertEagle"; 							"Handcannon \ct[Exotic]\c-"; 				2000; 	4; 	};
			{ "RLSuperShotgun"; 						"Super Shotgun \ct[Exotic]\c-"; 			2500; 	4; 	};
			{ "RLAssaultShotgun"; 						"Assault Shotgun \ct[Exotic]\c-"; 			3000; 	4; 	};
			{ "RLUzi"; 									"Uzi \ct[Exotic]\c-"; 						3000; 	4; 	};
			{ "RLBattleRifle"; 							"Battle Rifle \ct[Exotic]\c-"; 				4000; 	4; 	};
			{ "RLMinigun"; 								"Minigun \ct[Exotic]\c-"; 					5000; 	4; 	};
			{ "RLMissileLauncher"; 						"Missile Launcher \ct[Exotic]\c-"; 			5000; 	4; 	};
			{ "RLNapalmLauncher"; 						"Napalm Launcher \ct[Exotic]\c-"; 			7500; 	4; 	};
			{ "RLLaserRifle"; 							"Laser Rifle \ct[Exotic]\c-"; 				7500; 	4; 	};
			{ "RLPlasmaShotgun"; 						"Plasma Shotgun \ct[Exotic]\c-"; 			10000; 	4; 	};
			{ "RLTristarBlaster"; 						"Tristar Blaster \ct[Exotic]\c-"; 			20000; 	4; 	};
			{ "RLNuclearPlasmaRifle"; 					"Nuclear Plasma Rifle \ct[Exotic]\c-";		25000; 	4; 	};
			{ "RLNuclearBFG9000"; 						"Nuclear BFG9000 \ct[Exotic]\c-"; 			50000; 	4; 	};
			{ "RLCombatTranslocator"; 					"Combat Translocator \ct[Exotic]\c-"; 		10000; 	4; 	};
			
			// Unique Weapons
			{ "RLJackhammer"; 							"Jackhammer \cd[Unique]\c-"; 				10000;	-1; };
			{ "RLRailgun"; 								"Railgun \cd[Unique]\c-"; 					20000; 	-1; };
			{ "RLMysteriousMagnum"; 					"Mysterious Magnum \cd[Unique]\c-"; 		25000; 	-1; };
			{ "RLBFG10k"; 								"BFG10k \cd[Unique]\c-"; 					50000; 	-1; };
			{ "RLUnknownHerald"; 						"Unknown Herald \cd[Unique]\c-"; 			25000; 	-1; };
			{ "RLFragShotgun"; 							"Frag Shotgun \cd[Unique]\c-"; 				10000; 	-1; };
			{ "RLQuadShotgun"; 							"Quad Shotgun \cd[Unique]\c-"; 				25000; 	-1; };
			{ "RLLightweaver"; 							"Lightweaver \cd[Unique]\c-"; 				25000; 	-1; };
			{ "RLTrigun"; 								"Trigun \cd[Unique]\c-"; 					10000; 	-1; };
			{ "RLGrammatonClericBeretta"; 				"Grammaton Cleric Beretta \cd[Unique]\c-";	10000; 	-1; };
			{ "RLAntiFreakJackel"; 						"Anti-Freak Jackel \cd[Unique]\c-"; 		25000; 	-1; };
			{ "RLTantrumCannon"; 						"Quantum Tantrum Cannon \cd[Unique]\c-"; 	25000; 	-1; };
			{ "RLRevenantsLauncher"; 					"Revenant's Launcher \cd[Unique]\c-"; 		10000; 	-1; };
			{ "RLNullPointer"; 							"Charch's Null Pointer \cd[Unique]\c-"; 	10000; 	-1; };
			{ "RLParticleBeamCannon"; 					"Particle Beam Cannon \cd[Unique]\c-"; 		25000; 	-1; };
			
			// Legendary Weapons
			{ "RLJudgeOfTheDead"; 						"Judge Of The Dead \cf[Legendary]\c-"; 		50000; 	-1; };
			{ "RLHurricaneCannon"; 						"Hurricane Cannon \cf[Legendary]\c-"; 		50000; 	-1; };
			{ "RLNeuralStunner"; 						"Neural Stunner \cf[Legendary]\c-"; 		50000; 	-1; };
		};
		
		// Armors
		Shop[3] =
		{
			{ "RLArmorBonusPickup"; 					"Armor Bonus"; 								100; 	0; 	};
			{ "RLGreenArmorPickup"; 					"Security Armor"; 							250; 	0; 	};
			{ "RLBlueArmorPickup";						"Combat Armor"; 							500; 	0; 	};
			{ "RLRedArmorPickup"; 						"Commando Armor"; 							1000; 	0; 	};
			{ "RLBallisticGreenArmorPickup"; 			"Ballistic Security Armor"; 				1250; 	1; 	};
			{ "RLBallisticBlueArmorPickup"; 			"Ballistic Combat Armor"; 					1500; 	1; 	};
			{ "RLBallisticRedArmorPickup"; 				"Ballistic Commando Armor"; 				2000; 	1; 	};
			{ "RLCyberNanoGreenArmorPickup"; 			"CyberNano Security Armor"; 				5000; 	2; 	};
			{ "RLCyberNanoBlueArmorPickup"; 			"CyberNano Combat Armor"; 					7500; 	2; 	};
			{ "RLCyberNanoRedArmorPickup"; 				"CyberNano Commando Armor~"; 				10000; 	2; 	};
			{ "RLFireproofGreenArmorPickup"; 			"Fireproof Security Armor"; 				2000; 	2; 	};
			{ "RLFireproofBlueArmorPickup"; 			"Fireproof Combat Armor"; 					2500; 	2; 	};
			{ "RLFireproofRedArmorPickup"; 				"Fireproof Commando Armor"; 				5000; 	2; 	};
			{ "RLNanofiberGreenArmorPickup"; 			"Nanofiber Security Armor"; 				2000; 	2; 	};
			{ "RLNanofiberBlueArmorPickup"; 			"Nanofiber Combat Armor"; 					2500; 	2; 	};
			{ "RLNanofiberRedArmorPickup"; 				"Nanofiber Commando Armor"; 				3000; 	2; 	};
			{ "RLNanofiberSkinGreenArmorPickup"; 		"Nanofiber Skin Security Armor"; 			5000; 	2; 	};
			{ "RLNanofiberSkinBlueArmorPickup"; 		"Nanofiber Skin Combat Armor"; 				7500; 	2; 	};
			{ "RLNanofiberSkinRedArmorPickup"; 			"Nanofiber Skin Commando Armor"; 			10000; 	2; 	};
			{ "RLPowerGreenArmorPickup"; 				"Powered Security Armor"; 					2500; 	4; 	};
			{ "RLPowerBlueArmorPickup"; 				"Powered Combat Armor"; 					5000; 	4; 	};
			{ "RLPowerRedArmorPickup"; 					"Powered Commando Armor"; 					10000; 	4; 	};
			{ "RLCerberusArmorPickup"; 					"Cerberus Armor"; 							2500; 	8; 	};
			{ "RLFireShieldArmorPickup"; 				"Fire Shield"; 								10000; 	8; 	};
			{ "RLTacticalArmorPickup"; 					"Tactical Armor"; 							1000; 	8; 	};
			{ "RLBallisticShieldArmorPickup"; 			"Ballistic Shield"; 						10000; 	8; 	};
			{ "RLBallisticVestArmorPickup"; 			"Ballistic Vest"; 							5000; 	8; 	};
			{ "RLBulletproofVestArmorPickup"; 			"Bulletproof Vest"; 						7500; 	8; 	};
			{ "RLDuelistArmorPickup"; 					"Duelist Armor"; 							7500; 	8; 	};
			{ "RLEnergyShieldedVestArmorPickup"; 		"Energy-Shielded Vest"; 					7500; 	8; 	};
			{ "RLEnergyShieldArmorPickup"; 				"Energy Shield"; 							10000; 	8; 	};
			{ "RLGothicArmorPickup"; 					"Gothic Armor"; 							10000; 	8; 	};
			{ "RLMedicalArmorPickup"; 					"Medical Armor"; 							5000; 	8; 	};
			{ "RLPhaseshiftArmorPickup"; 				"Phaseshift Armor"; 						7500; 	8; 	};
			{ "RLPlasmaShieldArmorPickup"; 				"Plasma Shield"; 							7500; 	8; 	};
			{ "RLOnyxArmorPickup"; 						"Onyx Armor"; 								5000; 	8; 	};
			{ "RLCyberneticArmorPickup"; 				"Cybernetic Armor"; 						10000; 	8; 	};
			{ "RLLavaArmorPickup"; 						"Lava Armor"; 								7500; 	8; 	};
			{ "RLMaleksArmorPickup"; 					"Malek's Armor"; 							5000; 	8; 	};
			{ "RLMedicalPowerArmorPickup"; 				"Medical Power Armor"; 						7500; 	8; 	};
			{ "RLNecroArmorPickup"; 					"Necro Armor"; 								7500; 	8; 	};
			{ "RLShieldedArmorPickup"; 					"Shielded Armor"; 							10000; 	8; 	};
		};
		
		// Powerups
		Shop[4][0].Actor = "InvulnerabilitySphere2";
		Shop[4][6].Actor = "RadSuit2";
		Shop[4][7].Actor = "Infrared2";
		Shop[4][8].Actor = "Berserk2";
		Shop[4][ShopMax[4] - 2] = { "RLTrackingMap"; "Tracking Map"; 2000; 4; };
		Shop[4][ShopMax[4] - 1] = { "RLSupplyCrate"; "Supply Crate"; 25000; 8; };
		
		// Mod Packs
		Shop[8] =
		{
			{ "RLPowerModItem"; 						"Power Modpack"; 							2500; 	0; 	};
			{ "RLBulkModItem"; 							"Bulk Modpack"; 							2500; 	0; 	};
			{ "RLAgilityModItem"; 						"Agility Modpack"; 							2500; 	0; 	};
			{ "RLTechnicalModItem"; 					"Technical Modpack"; 						2500; 	0; 	};
			{ "RLSniperModItem"; 						"Sniper Modpack"; 							10000; 	2; 	};
			{ "RLFirestormModItem"; 					"Firestorm Modpack"; 						10000; 	2; 	};
			{ "RLNanoModItem"; 							"Nano Modpack"; 							10000; 	2; 	};
			{ "RLArmorModItem"; 						"Armor Modpack"; 							5000;	2; 	};
		};
		
		// Boots
		Shop[9] =
		{
			{ "RLSteelBootsPickup"; 					"Steel Boots";								250; 	0; };
			{ "RLProtectiveBootsPickup"; 				"Protective Boots"; 						500; 	0; };
			{ "RLPlasteelBootsPickup"; 					"Plasteel Boots"; 							750; 	0; };
			{ "RLAntigravSteelBootsPickup"; 			"Anti-Grav Steel Boots"; 					1000; 	1; };
			{ "RLAntigravProtectiveBootsPickup"; 		"Anti-Grav Protective Boots"; 				1500; 	1; };
			{ "RLAntigravPlasteelBootsPickup"; 			"Anti-Grav Plasteel Boots"; 				2000; 	1; };
			{ "RLEnviromentalSteelBootsPickup"; 		"Environmental Steel Boots"; 				1000; 	1; };
			{ "RLEnviromentalProtectiveBootsPickup";	"Environmental Protective Boots"; 			1500; 	1; };
			{ "RLEnviromentalPlasteelBootsPickup"; 		"Environmental Plasteel Boots"; 			2000; 	1; };
			{ "RLFireproofSteelBootsPickup"; 			"Fireproof Steel Boots"; 					1000; 	1; };
			{ "RLFireproofProtectiveBootsPickup"; 		"Fireproof Protective Boots"; 				1500; 	1; };
			{ "RLFireproofPlasteelBootsPickup"; 		"Fireproof Plasteel Boots"; 				2000; 	1; };
			{ "RLGrapplingSteelBootsPickup"; 			"Grappling Steel Boots"; 					1000; 	1; };
			{ "RLGrapplingProtectiveBootsPickup"; 		"Grappling Protective Boots"; 				1500; 	1; };
			{ "RLGrapplingPlasteelBootsPickup"; 		"Grappling Plasteel Boots"; 				2000; 	1; };
			{ "RLCerberusBootsPickup"; 					"Cerberus Boots"; 							1000; 	4; };
			{ "RLTacticalBootsPickup"; 					"Tactical Boots"; 							1500; 	4; };
			{ "RLLavaBootsPickup"; 						"Lava Boots"; 								2500; 	4; };
			{ "RLAcidProofBootsPickup"; 				"Acid-Proof Boots"; 						2000; 	4; };
			{ "RLGothicBootsPickup"; 					"Gothic Boots"; 							2500; 	4; };
			{ "RLPhaseshiftBootsPickup"; 				"Phaseshift Boots"; 						2500; 	4; };
			{ "RLEnviroBootsPickup"; 					"Enviro Boots"; 							1000; 	4; };
			{ "RLNyarlaptotepsBootsPickup"; 			"Nyarlaptotep's Boots"; 					5000; 	4; };
		};
	};
};

function void ShopLoop()
{
	int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
	int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
	fixed X = 24.1;
	fixed Y = 50.0;
	int Discount = (int)(Player.RankLevel * 2.1);
	int Cost = Shop[Player.ShopPage][Player.ShopIndex].Price - Shop[Player.ShopPage][Player.ShopIndex].Price * Discount / 100;
	int SellPrice;
	int Color;
	int Available;
	str Name;
	str Item;
	int Held;
	int Rank;
	
	// Certain pages are built dynamically or require modifications depending on extension CVARs
	ShopBuildPages();

#if DEBUG
	// Remove rank limitations in Debug Mode
	for (int i = 0; i < ShopCategories; i++)
		for (int j = 0; j < ShopMax[i]; j++)
			Shop[i][j].Rank = 0;
#endif
	
	// Calculate the held amounts for the shop items
	for (int i = 0; i < ShopCategories; i++)
		for (int j = 0; j < ShopMax[i]; j++)
			Shop[i][j].Held = CheckInventory(Shop[i][j].Actor);
			
	// Calculate a total of how many of the items are actually available for buying or selling depending on your Rank
	for (int i = 0; i < ShopMax[Player.ShopPage]; i++)
		if (Shop[Player.ShopPage][i].Rank <= Player.RankLevel) Available++;
	
	// Get the sell price
	if (Player.ShopPage == 1) // Ammo
		SellPrice = GetSellPrice(GetAmmoAmount(Shop[Player.ShopPage][Player.ShopIndex].Actor))
	else
		SellPrice = GetSellPrice(1);
	
	// Freeze the game if the option is active
	if (GetCVar("drpg_menufreeze"))
		GiveInventory("MenuFreezer", 1);
	
	// Set the HUD Size
	SetHudSize(GetCVar("drpg_menu_width"), GetCVar("drpg_menu_height"), true);

	// Draw the background
	if (GetCVar("drpg_menudim"))
		FadeRange(0, 0, 0, 0.5, 0, 0, 0, 0.0, 0.25);
	
	// Draw Shop
	SetFont("BIGFONT");
	HudMessage("Shop - %s (%d/%d)\n",
			   ShopNames[Player.ShopPage], Player.ShopPage + 1, ShopCategories,
			   HUDMSG_PLAIN, 3000, CR_GREEN,
			   24.1, 24.0, 0.05);
	
	// Draw Price
	if (Player.ShopPage == 5) // Tokens
		HudMessage("\cj%d C \ck(Discount: %d%%)\n",
				   Cost, Discount,
				   HUDMSG_PLAIN, 3000 + SHOP_ITEMS, CR_GOLD,
				   24.1, 38.0, 0.05)
	else
		HudMessage("\cj%d C \ci(%d C) \ck(Discount: %d%%)\n",
				   Cost, SellPrice, Discount,
				   HUDMSG_PLAIN, 3000 + SHOP_ITEMS, CR_GOLD,
				   24.1, 38.0, 0.05);
	
	// Determine Offset
	if (Player.ShopIndex > SHOP_SCROLL && Available > SHOP_SCROLL)
		Player.ShopOffset = Player.ShopIndex - SHOP_SCROLL
	else
		Player.ShopOffset = 0;
	
	// Draw Item List
	for (int i = Player.ShopOffset; i < Player.ShopOffset + (SHOP_SCROLL * 2) + 1; i++)
	{
		Name = Shop[Player.ShopPage][i].Name;
		Cost = Shop[Player.ShopPage][i].Price;
		Held = Shop[Player.ShopPage][i].Held;
		Rank = Shop[Player.ShopPage][i].Rank;
		
		// Stop if we're at the end of the list
		if (i > ShopMax[Player.ShopPage] - 1) continue;
		
		// Continue past this item if we don't have the rank to buy/sell it
		if ((Rank > 0 && Player.RankLevel < Rank) || Rank == -1) continue;
		
		SetFont("SMALLFONT");
		
		// Set the Color
		if (i == Player.ShopIndex)
			Color = MenuCursorColor
		else if (CheckInventory("Credits") >= Cost - Cost * Discount / 100)
			Color = CR_WHITE
		else
			Color = CR_RED;
		
		// Set the Item String
		if (Held > 0)
			Item = StrParam("%s \cd[%d]\n", Name, Held)
		else
			Item = StrParam("%s\n", Name);
		
		// Display the item string
		HudMessage("%s\n", Item, HUDMSG_PLAIN, 3001 + i, Color, X, Y, 0.05);
		
		// Move down Y
		Y += 8.0;
	};
		
	// Check cursor movement for repeating
	if (Buttons == BT_FORWARD)
	{
		Player.DelayTimer++;
		if (Player.DelayTimer > 35)
			MoveCursor(DIR_UP);
	};
	if (Buttons == BT_BACK)
	{
		Player.DelayTimer++;
		if (Player.DelayTimer > 35)
			MoveCursor(DIR_DOWN);
	};
	
	// Check Input
	if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD)
		MoveCursor(DIR_UP);
	if (Buttons == BT_BACK && OldButtons != BT_BACK)
		MoveCursor(DIR_DOWN);
	if ((Buttons == BT_LEFT && OldButtons != BT_LEFT) ||
		(Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT))
		MoveCursor(DIR_LEFT);
	if ((Buttons == BT_RIGHT && OldButtons != BT_RIGHT) ||
		(Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT))
		MoveCursor(DIR_RIGHT);
	if (Buttons == BT_USE && OldButtons != BT_USE && !Player.MenuBlock)
	{
		if (CheckInventory("Credits") >= Shop[Player.ShopPage][Player.ShopIndex].Price)
			BuyItem(Shop[Player.ShopPage][Player.ShopIndex].Actor)
		else
			ActivatorSound("menu/error", 127);
	};
	if (Buttons == BT_SPEED && OldButtons != BT_SPEED)
		if (Shop[Player.ShopPage][Player.ShopIndex].Held > 0)
			SellItem(Shop[Player.ShopPage][Player.ShopIndex].Actor, false)
		else
			ActivatorSound("menu/error", 127);
	if (Buttons & BT_SPEED)
	{
		Player.DelayTimer++;
		
		if (Player.DelayTimer > 35 * 2 && Shop[Player.ShopPage][Player.ShopIndex].Held > 0)
		{
			SellItem(Shop[Player.ShopPage][Player.ShopIndex].Actor, true);
			Player.DelayTimer = 0;
		};
	};
	
	// Reset the Delay Timer if no buttons are pressed
	if (Buttons == 0 && OldButtons == 0)
		Player.DelayTimer = 0;
	
	// Reset the menu block
	Player.MenuBlock = false;	
};

function void MoveCursor(int Direction)
{
	if (Direction == DIR_UP)
	{
		if (Player.ShopIndex <= 0) Player.ShopIndex = ShopMax[Player.ShopPage];
		ActivatorSound("menu/move", 127);
		Player.ShopIndex--;
		while (Shop[Player.ShopPage][Player.ShopIndex].Rank > Player.RankLevel || Shop[Player.ShopPage][Player.ShopIndex].Rank == -1)
			if (Player.ShopIndex <= 0)
				Player.ShopIndex = ShopMax[Player.ShopPage]
			else
				Player.ShopIndex--;
	}
	else if (Direction == DIR_DOWN)
	{
		if (Player.ShopIndex >= ShopMax[Player.ShopPage] - 1) Player.ShopIndex = -1;
		ActivatorSound("menu/move", 127);
		Player.ShopIndex++;
		while (Shop[Player.ShopPage][Player.ShopIndex].Rank > Player.RankLevel || Shop[Player.ShopPage][Player.ShopIndex].Rank == -1)
			if (Player.ShopIndex >= ShopMax[Player.ShopPage] - 1)
				Player.ShopIndex = 0
			else
				Player.ShopIndex++;
	}
	else if (Direction == DIR_LEFT)
	{
		if (Player.ShopPage <= 0) Player.ShopPage = ShopCategories;
		ActivatorSound("menu/move", 127);
		Player.ShopIndex = 0;
		Player.ShopPage--;
	}
	else if (Direction == DIR_RIGHT)
	{
		if (Player.ShopPage >= ShopCategories - 1) Player.ShopPage = -1;
		ActivatorSound("menu/move", 127);
		Player.ShopIndex = 0;
		Player.ShopPage++;
	};
};

function int GetAmmoAmount(str Item)
{
	int Amount = 1;
	
	// If it's ammo, we need to get the real amounts to give if you're using the Give buy mode
	if (Item == "Clip")			Amount = 10;
	if (Item == "ClipBox")		Amount = 50;
	if (Item == "Shell")		Amount = 4;
	if (Item == "ShellBox")		Amount = 20;
	if (Item == "RocketAmmo")	Amount = 1;
	if (Item == "RocketBox")	Amount = 5;
	if (Item == "Cell")			Amount = 20;
	if (Item == "CellPack")		Amount = 100;
	
	// If it's ammo and it's the large version, just change it down to the base type
	if (Item == "ClipBox")		Item = "Clip";
	if (Item == "ShellBox")		Item = "Shell";
	if (Item == "RocketBox")	Item = "RocketAmmo";
	if (Item == "CellPack")		Item = "Cell";
	
	// Now apply difficulty-based multipliers to the amount if it's ammo
	if (Item == "Clip" || Item == "Shell" || Item == "RocketAmmo" || Item == "Cell")
		if (GameSkill() == 1 || GameSkill() >= 5)
			Amount *= 2;
	
	return Amount;
};

function void BuyItem(str Item)
{
	int Cost = Shop[Player.ShopPage][Player.ShopIndex].Price;
	
	// If you aren't the required rank, fail
	if (Shop[Player.ShopPage][Player.ShopIndex].Rank > Player.RankLevel) return;
	
	// If it has no price, fail
	if (Cost == 0)
	{
		ActivatorSound("menu/error", 127);
		return;
	};
	
	// DoomRL Compatibility
	if (GetCVar("drpg_ext_doomrl"))
		if (Player.ShopPage == 0) // Weapons
			Item = StrParam("%sPickup\n", Item);
	
	if (InBase && !GetCVar("drpg_shoptype"))
		SpawnSpotForced(Item, ShopSpotID, 0, 0)
	else
	{
		SpawnForced(Item, GetActorX(0), GetActorY(0), GetActorZ(0), 0, 0);
		SetActorVelocity(Player.TID, 0.01, 0.01, 0, true, false);
	};
	
	ActivatorSound("menu/buy", 127);
	TakeInventory("Credits", Cost);
};

function int GetSellPrice(int Amount)
{
	int SellCost = Shop[Player.ShopPage][Player.ShopIndex].Price;
	
	// Some pages need modified sell behavior
	if (Player.ShopPage == 1) // Ammo
	{
		Amount = GetAmmoAmount(Shop[Player.ShopPage][Player.ShopIndex].Actor);
		if (Player.ShopIndex == 4) // GROSS HACK for Rockets
			SellCost /= Amount * 5
		else
			SellCost /= Amount;
	};
	
	SellCost /= 10;
	
	// Make sure you always get at least 1 Credit from a sale
	if (SellCost == 0) SellCost = 1;
	
	return SellCost;
};

function int SellItem(str Item, bool SellAll)
{
	int Amount = 1;
	int SellCost;

	// You must be at least Rank 1 or in the Outpost to sell items
	if (Player.RankLevel == 0 && !InBase)
	{
		SetFont("BIGFONT");
		Print("\cgYou cannot sell items outside the Outpost until you reach the first rank\n");
		ActivatorSound("menu/error", 127);
		return 0;
	};
	
	// Page blacklist
	if (Player.ShopPage == 5) // Tokens
	{
		ActivatorSound("menu/error", 127);
		return 0;
	};
	
	if (SellAll)
		Amount = CheckInventory(Item);
	
	if (Player.ShopPage == 1) // Ammo
		Amount = GetAmmoAmount(Item);
	
	// Get Sell Price
	SellCost = GetSellPrice(Amount);

	// DoomRL Compatibility
	if (GetCVar("drpg_ext_doomrl"))
		for (int i = 1; i <= Amount; i++)
		{
			if (Player.ShopPage == 0) // Weapons
				TakeInventory("RLWeaponLimit", 1);
			if (Player.ShopPage == 3) // Armor
				TakeInventory("RLArmorInInventory", 1);
			if (Player.ShopPage == 8) // Mod Packs
				if (Player.ShopIndex == 8) // Special handling case for Armor Modpack
					TakeInventory("RLArmorModItemInInventory", 1)
				else
					if (PlayerClass(PlayerNumber()) == 2) // Special handling case for Technician
						TakeInventory("RLScavengerModLimit", 1)
					else
						TakeInventory("RLModLimit", 1);
			if (Player.ShopPage == 9) // Boots
				TakeInventory("RLBootsInInventory", 1);
		};
	
	// Sell the Item
	if (CheckInventory(Item) >= Amount)
	{
		ActivatorSound("credits/payout", 127);
		TakeInventory(Item, Amount);
		GiveInventory("Credits", SellCost * Amount);
	}
	else
		ActivatorSound("menu/error", 127);
	
	return SellCost;
};
