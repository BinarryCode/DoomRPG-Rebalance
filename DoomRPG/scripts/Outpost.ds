#include "Arena.dh"
#include "Globals.dh"
#include "Outpost.dh"
#include "RPG.dh"
#include "Stats.dh"
#include "Utils.dh"

/* --------------------------------------------------

SPOT ID'S
1000 - Shop Drop
1001 - Marine Spawn Spot
1002 - Arena Monsters
1003 - Arena Start Spot
1100-1130 - Arena Spawn Points
1200 - Arena Item Spawner
1201 - Hostile Marines

// -------------------------------------------------- */

int LevelChoice = 1;
int SkillChoice = 0;
int WaveChoice = 1;

// Invasion
bool Invasion = false;

script EnterOutpost()
{
    InBase = true;
    
    Sector_SetPlaneReflection(6, 128, 0);
};

script RegenArea(int ID, int Paid)
{
	Start:
    // Input
    int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
    int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
    int Interval;
    
	if (Buttons & BT_SPEED) Interval = 2 else Interval = 10;
    
    // XP
    if (ID == 1)
    {
        if (Paid && Credits < Player.Level * 100)
            return;
        if (Player.Level == 0)
            Player.XP += 100
        else
            Player.XP += Player.Level * 100;
        
        FadeRange(255, 255, 255, 0.1, 255, 255, 255, 0.0, 0.1);
        AmbientSound("regen/xp", 127);
        
        if (Paid)
            TakeInventory("Credits", Player.Level * 100);
        
        Delay(Interval);
        goto Start;
    };
	
    // Rank
    if (ID == 2)
    {
        if (Paid && Credits < Player.RankLevel * 1001)
            return
        if (Player.RankLevel == 0)
            Player.Rank += 1000
        else
            Player.Rank += Player.RankLevel * 1000;
        
        FadeRange(255, 255, 0, 0.1, 255, 255, 0, 0.0, 0.1);
        AmbientSound("regen/rank", 127);
        
        if (Paid)
            TakeInventory("Credits", Player.RankLevel * 1000);
        
        Delay(Interval);
        goto Start;
    };
	
    // Health
    if (ID == 3)
    {
        if (Paid && Credits < 5)
            return;
        if (GetActorProperty(PlayerTID, APROP_Health) >= GetActorProperty(PlayerTID, APROP_SpawnHealth))
            return;
        
        HealThing(5);

        FadeRange(255, 0, 0, 0.1, 255, 0, 0, 0.0, 0.1);
        
        AmbientSound("regen/health", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
	
    // Armor
    if (ID == 4)
    {
        if (Paid && Credits < 5)
            return;
        if (CheckInventory("Armor") >= ArmorMax)
            return;
        
        GiveInventory("ArmorBonus", 5);
        
        FadeRange(0, 255, 0, 0.1, 0, 255, 0, 0.0, 0.1);
        AmbientSound("regen/armor", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
	
    // EP
    if (ID == 5)
    {
        if (Paid && Credits < 5)
            return;
        if (Player.EP < Player.EPMax)
            Player.EP += 5
        else
        if (Player.EP >= Player.EPMax)
            return;
        
        FadeRange(0, 255, 255, 0.1, 0, 255, 255, 0.0, 0.1);
        AmbientSound("regen/ep", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
	
    // Credits
    if (ID == 6)
    {
        if (Paid) return;
        
        GiveInventory("Credits", 100);
        
        FadeRange(128, 128, 0, 0.1, 128, 128, 0, 0.0, 0.1);
        AmbientSound("regen/credits", 127);
        
        Delay(Interval);
        goto Start;
    };
};

script LevelTransport()
{
    while (1)
    {
        SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
        
        // Stop Underflow
        if (LevelChoice < 1)
            LevelChoice = 1;
        
        // Text
        SetFont("BIGFONT");
        HudMessage("Choose Level\n", HUDMSG_PLAIN, 1, CR_GOLD, 0.5, 0.2, 0.05);
        
        if (Player.MapsVisited[LevelChoice])
            HudMessage("%d\n", LevelChoice, HUDMSG_PLAIN, 2, CR_GREEN, 0.5, 0.3, 0.05)
        else
            HudMessage("%d\n", LevelChoice, HUDMSG_PLAIN, 2, CR_RED, 0.5, 0.3, 0.05);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && LevelChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice--;
        };
        if (Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT && LevelChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice -= 10;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice++;
        };
        if (Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice += 10;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            if (Player.MapsVisited[LevelChoice])
            {
                FadeRange(255, 255, 255, 0.0, 255, 255, 255, 1.0, 2.0);
                Delay(105);
                SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
                InBase = false;
                Transported = true;
                Teleport_NewMap(LevelChoice, 0);
                break;
            }
            else
            {
                HudMessage("You haven't reached this level yet\n", HUDMSG_FADEOUT, 1, CR_RED, 0.5, 0.5, 3.0, 1.0);
                ActivatorSound("menu/error", 127);
                FadeRange(255, 255, 255, 1.0, 255, 255, 255, 0.0, 2.0);
                SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
                InBase = true;
                return;
            };
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

script SkillComputer()
{
    AmbientSound("misc/edgar", 127);
    Delay(1);
    
    while (1)
    {
        SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
        
        // Text
        SetFont("BIGFONT");
        HudMessage("Skill Level: \cg%d (%s)\n", SkillChoice + 1, SkillLevels[SkillChoice],
                   HUDMSG_PLAIN, 1, CR_GOLD, 0.5, 0.2, 0.05);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && SkillChoice > 0)
        {
            ActivatorSound("menu/move", 127);
            SkillChoice--;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK && SkillChoice < 4)
        {
            ActivatorSound("menu/move", 127);
            SkillChoice++;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            SetSkill(SkillChoice);
            SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
            return;
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

script ToggleArena()
{
    ArenaActive = !ArenaActive;
    ActivatorSound("misc/secret", 127);
    
    SetFont("BIGFONT");
    
    if (ArenaActive)
        HudMessage("Arena Active\n", HUDMSG_FADEOUT, 1, CR_GREEN, 0.5, 0.5, 2.0, 1.0)
    else
        HudMessage("Arena Inactive\n", HUDMSG_FADEOUT, 1, CR_RED, 0.5, 0.5, 2.0, 1.0);
};

script PassArenaLine()
{
    if (ArenaActive)
    {
        Ceiling_LowerToFloor(99, 64);
        ArenaLoop();
    };
};

script SelectArenaWave()
{
    ActivatorSound("menu/move", 127);

    if (ArenaMaxWave == 0) ArenaMaxWave = 1;
    WaveChoice = ArenaMaxWave;
    
    Delay(1);

    while (1)
    {
        SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
        
        // Text
        SetFont("BIGFONT");
        HudMessage("Wave: \cd%d/%d\n", WaveChoice, ArenaMaxWave,
                   HUDMSG_FADEOUT, 1, CR_WHITE, 0.5, 0.5, 0.05, 1.0);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && WaveChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            WaveChoice--;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK && WaveChoice < ArenaMaxWave)
        {
            ActivatorSound("menu/move", 127);
            WaveChoice++;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            if (WaveChoice > 1)
                ArenaWave = WaveChoice - 1
            else
                ArenaWave = 1;
            
            ActivatorSound("menu/move", 127);
            SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
            return;
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

script PissOffMarines()
{
    SetMusic("Outpost2", 3);
    Thing_Activate(1);
    SetActorProperty(1, APROP_Friendly, 0);
    Thing_Hate(1, 0, 6);
    Thing_ChangeTID(1, 1201);
    
    while (1)
    {
        // You are dead, NO BIG SURPRISE
        if (GetActorProperty(PlayerTID, APROP_Health) <= 0)
        {
            SetMusic("Outpost2", 1);
            return;
        };
        
        // Teleport in new Marines
        if ((Timer() % (35 * 10)) == 1)
        {
            SpawnSpot("TeleportFog", 1001, 0, 0);
            SpawnSpotFacing("MarineRandomizer", 1001, 1201);
        };

        Delay(1);
    };
};

script TokenConverter()
{
	int StatWorth = 1;
	int StatCapWorth = 10;
	int SkillWorth = 5;
	int InAmount = 1;
	int Amount1;
	int Amount2;
	int MaxAmount;
	int Type;
	str Text;
	
	// Rank Check
	if (Player.RankLevel == 0)
	{
		SetFont("BIGFONT");
		Print("\cgYou must be at least Rank 1 to use the Token Converter\n");
		ActivatorSound("menu/error", 127);
		return;
	};
	
	SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
	ActivatorSound("menu/move", 127);

	while (1)
	{
		int StatTokens = CheckInventory("StatToken");
		int StatCapTokens = CheckInventory("StatCapToken");
		int SkillTokens = CheckInventory("SkillToken");
		int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
		int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
		
		switch (Type)
		{
			case 0: // Stat -> Skill
				Amount1 = InAmount * SkillWorth;
				Amount2 = InAmount;
				MaxAmount = StatTokens / SkillWorth;
				Text = StrParam("Convert \cg%d Stat Tokens \cj-> \cd%d Skill Tokens\n", Amount1, Amount2);
				break;
			case 1: // Stat -> Stat Cap
				Amount1 = InAmount * StatCapWorth;
				Amount2 = InAmount;
				MaxAmount = StatTokens / StatCapWorth;
				Text = StrParam("Convert \cg%d Stat Tokens \cj-> \ck%d Stat Cap Tokens\n", Amount1, Amount2);
				break;
			case 2: // Skill -> Stat
				Amount1 = InAmount * StatWorth;
				Amount2 = InAmount * SkillWorth;
				MaxAmount = SkillTokens;
				Text = StrParam("Convert \cd%d Skill Tokens \cj-> \cg%d Stat Tokens\n", Amount1, Amount2);
				break;
			case 3: // Skill -> Stat Cap
				Amount1 = InAmount * (StatCapWorth / SkillWorth);
				Amount2 = InAmount;
				MaxAmount = SkillTokens / 2;
				Text = StrParam("Convert \cd%d Skill Tokens \cj-> \ck%d Stat Cap Tokens\n", Amount1, Amount2);
				break;
			case 4: // Stat Cap -> Stat
				Amount1 = InAmount;
				Amount2 = InAmount * StatCapWorth;
				MaxAmount = StatCapTokens;
				Text = StrParam("Convert \ck%d Stat Cap Tokens \cj-> \cg%d Stat Tokens\n", Amount1, Amount2);
				break;
			case 5: // Stat Cap -> Skill
				Amount1 = InAmount;
				Amount2 = InAmount * (StatCapWorth / SkillWorth);
				MaxAmount = StatCapTokens;
				Text = StrParam("Convert \ck%d Stat Cap Tokens \cj-> \cd%d Skill Tokens\n", Amount1, Amount2);
				break;
		};

		// Input
		if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && Type > 0)
		{
			ActivatorSound("menu/move", 127);
			Type--;
		};
		if (Buttons == BT_BACK && OldButtons != BT_BACK && Type < 5)
		{
			ActivatorSound("menu/move", 127);
			Type++;
		};
		if ((Buttons == BT_LEFT && OldButtons != BT_LEFT) ||
			(Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT)
			&& InAmount > 1)
		{
			ActivatorSound("menu/move", 127);
			InAmount--;
		};
		if ((Buttons == BT_RIGHT && OldButtons != BT_RIGHT) ||
			(Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT)
			&& InAmount <= MaxAmount - 1)
		{
			ActivatorSound("menu/move", 127);
			InAmount++;
		};
		if (Buttons == BT_USE && OldButtons != BT_USE)
		{
			str InItem;
			str OutItem;
			
			switch (Type)
			{
				case 0: InItem = "StatToken"; 		OutItem = "SkillToken";		break;
				case 1: InItem = "StatToken"; 		OutItem = "StatCapToken";	break;
				case 2: InItem = "SkillToken"; 		OutItem = "StatToken";		break;
				case 3: InItem = "SkillToken"; 		OutItem = "StatCapToken";	break;
				case 4: InItem = "StatCapToken"; 	OutItem = "StatToken";		break;
				case 5: InItem = "StatCapToken"; 	OutItem = "SkillToken";		break;
			};
			
			if ((Type == 0 || Type == 1) && StatTokens >= Amount1)
			{
				InAmount = 1;
				GiveInventory(OutItem, Amount2);
				TakeInventory(InItem, Amount1);
				ActivatorSound("misc/tokenconvert", 127);
			}
			else if ((Type == 2 || Type == 3) && SkillTokens >= Amount1)
			{
				InAmount = 1;
				GiveInventory(OutItem, Amount2);
				TakeInventory(InItem, Amount1);
				ActivatorSound("misc/tokenconvert", 127);
			}
			else if ((Type == 4 || Type == 5) && StatCapTokens >= Amount1)
			{
				InAmount = 1;
				GiveInventory(OutItem, Amount2);
				TakeInventory(InItem, Amount1);
				ActivatorSound("misc/tokenconvert", 127);
			}
			else
				ActivatorSound("menu/error", 127);
		};
		if (Buttons == BT_SPEED && OldButtons != BT_SPEED)
		{
			ActivatorSound("menu/move", 127);
			SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
			return;
		};
		
		// Drawing
		SetHudSize(GetCVar("drpg_menu_width"), GetCVar("drpg_menu_height"), true);
		PrintSprite("TOKAD0", 0, 16.1, 188.1, 0.05);
		PrintSprite("TOKBD0", 0, 16.1, 208.1, 0.05);
		PrintSprite("TOKCD0", 0, 16.1, 228.1, 0.05);
		SetFont("BIGFONT");
		HudMessage("%d\n", StatTokens, 		HUDMSG_PLAIN, 0, CR_RED,  			40.1, 178.0, 0.05);
		HudMessage("%d\n", StatCapTokens, 	HUDMSG_PLAIN, 0, CR_YELLOW,  		40.1, 198.0, 0.05);
		HudMessage("%d\n", SkillTokens, 	HUDMSG_PLAIN, 0, CR_DARKGREEN,		40.1, 218.0, 0.05);
		SetFont("SMALLFONT");
		HudMessage("%s\n", Text, HUDMSG_PLAIN, 0, CR_WHITE, 140.1, 178.0, 0.05);

		Delay(1);
	};
};
