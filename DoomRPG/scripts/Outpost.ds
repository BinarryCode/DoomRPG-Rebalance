#include "Arena.dh"
#include "Globals.dh"
#include "Outpost.dh"
#include "RPG.dh"
#include "Shop.dh"
#include "Stats.dh"
#include "Utils.dh"

// Spawn Spots and TIDs
int ShopSpecialTID = 1001;
int MarineSpotTID = 1300;
int MarineTID = 1301;

// Choices
int LevelChoice = 1;
int SkillChoice = 0;
int WaveChoice = 1;

// Invasion
bool Invasion = false;

function void CheckShopSpecial()
{
	// Reset the item
	if (ShopSpecialTimer <= 0)
	{
		int Category;
		int Index;
		bool ValidItem;

#if DEBUG
		if (Arbitrator) Log("\cdDEBUG: Shop Special expired! Switching...\n");
#endif
		
		// Item Blacklist
		int[][2] ItemBlacklist =
		{
			// Category, Index
			
			// Ammo
			{ 1;	0; 	};	// Clip
			{ 1;	1; 	};	// Box of Bullets
			{ 1;	2; 	};	// Shells
			{ 1;	3; 	};	// Box of Shells
			{ 1;	4; 	};	// Rockets
			{ 1;	5; 	};	// Box of Rockets
			{ 1;	6; 	};	// Cell
			{ 1;	7; 	};	// Cell Pack
			{ 1;	8; 	};	// Small Backpack
			{ 1;	9; 	};	// Backpack
			
			// Health
			{ 2;	0;	};	// Health Bonus
			
			// Armor
			{ 3;	0;	};	// Armor Bonus
			
			// Powerups
			{ 4;	5;	};	// Time Sphere
			{ 4;	11;	};	// Wings
			{ 4;	15;	};	// Strength Rune
			{ 4;	16;	};	// Drain Rune
			{ 4;	17;	};	// Endless Rune
			{ 4;	18;	};	// Resistance Rune
			{ 4;	19;	};	// Regeneration Rune
			{ 4;	20;	};	// Fright Rune
			{ 4;	21;	};	// Ghost Rune
			{ 4;	22;	};	// High Jump Rune
			{ 4;	23;	};	// Haste Rune
			
			// Sentinel (Indicates end of list)
			{ -1;   -1; };
		};
		
		while (!ValidItem)
		{
			// Pick an item
			ShopBuildPages();
			Category = Random(0, ShopCategories - 1);
			Index = Random(0, ShopMax[Category] - 1);
			
			// Check the item against the blacklist
			for (int i = 0; ItemBlacklist[i][0] != -1; ++i)
			{
				ValidItem = true;
				if (Category == ItemBlacklist[i][0] && Index == ItemBlacklist[i][1])
				{
					ValidItem = false;
					break;
				};
			};
		};
		
		// Assign the item to the Shop Special item
		ShopSpecialItem = Shop[Category][Index];
		
		// DoomRL Compatibility
		if (GetCVar("drpg_ext_doomrl"))
			if (Category == 0) // Weapons
				ShopSpecialItem.Actor = StrParam("%sPickup\n", ShopSpecialItem.Actor);
		
		// Spawn the item if you're in the Outpost
		SpawnShopSpecialItem();
		
		// Reset the timer and bought status
		ShopSpecialTimer = SHOP_SPECIAL_TIMER;
		ShopSpecialBought = false;
	};
	
	// Remove the item if it was bought
	if (InBase && ShopSpecialBought)
		Thing_Remove(ShopSpecialTID + 1);
	
	// Decrease the timer
	ShopSpecialTimer--;
};

function void SpawnShopSpecialItem()
{
	if (InBase)
	{
		Thing_Remove(ShopSpecialTID + 1);
		SpawnSpotForced("TeleportFog", ShopSpecialTID, 0, 0);
		SpawnSpotForced(ShopSpecialItem.Actor, ShopSpecialTID, ShopSpecialTID + 1, 0);
		SetActorProperty(ShopSpecialTID + 1, APROP_Invulnerable, true);
	};
};

acscript EnterOutpost()
{
    InBase = true;
    
	// Add the reflections to the hallway floors
    Sector_SetPlaneReflection(6, 128, 0);
	
	// Spawn the Shop Special item
	SpawnShopSpecialItem();
};

acscript RegenArea(int ID, int Paid)
{
	// If you're dead, terminate
	if (GetActorProperty(Player.TID, APROP_Health) <= 0) return;
	
	Start:
    // Input
    int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
    int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
    int Interval;
    
	if (Buttons & BT_SPEED) Interval = 2 else Interval = 10;
    
    // Health
    if (ID == 1)
    {
        if (Paid && CheckInventory("Credits") < 5)
            return;
        if (GetActorProperty(Player.TID, APROP_Health) >= GetActorProperty(Player.TID, APROP_SpawnHealth))
            return;
        
        HealThing(5);

        FadeRange(255, 0, 0, 0.1, 255, 0, 0, 0.0, 0.1);
        
        AmbientSound("regen/health", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
	
    // Armor
    if (ID == 2)
    {
        if (Paid && CheckInventory("Credits") < 5)
            return;
        if (CheckInventory("Armor") >= Player.ArmorMax)
            return;
        
        GiveInventory("ArmorBonus", 5);
        
        FadeRange(0, 255, 0, 0.1, 0, 255, 0, 0.0, 0.1);
        AmbientSound("regen/armor", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
	
    // EP
    if (ID == 3)
    {
        if (Paid && CheckInventory("Credits") < 5)
            return;
        if (Player.EP < Player.EPMax)
            Player.EP += 5
        else
        if (Player.EP >= Player.EPMax)
            return;
        
        FadeRange(0, 255, 255, 0.1, 0, 255, 255, 0.0, 0.1);
        AmbientSound("regen/ep", 127);
        
        if (Paid)
            TakeInventory("Credits", 5);
        
        Delay(Interval);
        goto Start;
    };
};

acscript LevelTransport()
{
    while (1)
    {
        SetPlayerProperty(false, 1, PROP_TOTALLYFROZEN);
        
        // Stop Underflow
        if (LevelChoice < 1)
            LevelChoice = 1;
        
        // Text
        SetFont("BIGFONT");
        HudMessage("Choose Level\n", HUDMSG_PLAIN, 1, CR_GOLD, 0.5, 0.2, 0.05);
        
        if (Player.MapsVisited[LevelChoice])
            HudMessage("%d\n", LevelChoice, HUDMSG_PLAIN, 2, CR_GREEN, 0.5, 0.3, 0.05)
        else
            HudMessage("%d\n", LevelChoice, HUDMSG_PLAIN, 2, CR_RED, 0.5, 0.3, 0.05);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && LevelChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice--;
        };
        if (Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT && LevelChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice -= 10;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice++;
        };
        if (Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT)
        {
            ActivatorSound("menu/move", 127);
            LevelChoice += 10;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            if (Player.MapsVisited[LevelChoice])
            {
                FadeRange(255, 255, 255, 0.0, 255, 255, 255, 1.0, 2.0);
                Delay(105);
                SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
                InBase = false;
                Transported = true;
                Teleport_NewMap(LevelChoice, 0);
                break;
            }
            else
            {
                HudMessage("You haven't reached this level yet\n", HUDMSG_FADEOUT, 1, CR_RED, 0.5, 0.5, 3.0, 1.0);
                ActivatorSound("menu/error", 127);
                FadeRange(255, 255, 255, 1.0, 255, 255, 255, 0.0, 2.0);
                SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
                InBase = true;
                return;
            };
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

acscript SkillComputer()
{
	// Terminate if you aren't the Arbitrator
	if (InMultiplayer && !Arbitrator) return;
    
	// DoomRL Compatibility
	if (GetCVar("drpg_ext_doomrl"))
		SkillLevels[5] = "Armegeddon";
	
	ActivatorSound("misc/edgar", 127);
	
	SetPlayerProperty(false, 1, PROP_TOTALLYFROZEN);
    
    while (1)
    {
		// Prevent the menus from being opened
		Player.InMenu = false;
		Player.InShop = false;

        // Text
        SetFont("BIGFONT");
        HudMessage("Skill Level: \cg%d (%s)\n", SkillChoice + 1, SkillLevels[SkillChoice],
                   HUDMSG_PLAIN, 1, CR_GOLD, 0.5, 0.5, 0.05);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && SkillChoice > 0)
        {
            ActivatorSound("menu/move", 127);
            SkillChoice--;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK && SkillChoice < (GetCVar("drpg_ext_doomrl") ? 5 : 4))
        {
            ActivatorSound("menu/move", 127);
            SkillChoice++;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            SetSkill(SkillChoice);
            SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
            return;
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

acscript ToggleArena()
{
    ArenaActive = !ArenaActive;
    ActivatorSound("misc/secret", 127);
    
    SetFont("BIGFONT");
    
    if (ArenaActive)
        HudMessage("Arena Active\n", HUDMSG_FADEOUT, 1, CR_GREEN, 0.5, 0.5, 2.0, 1.0)
    else
        HudMessage("Arena Inactive\n", HUDMSG_FADEOUT, 1, CR_RED, 0.5, 0.5, 2.0, 1.0);
};

acscript PassArenaLine()
{
    if (ArenaActive)
    {
        Ceiling_LowerToFloor(99, 64);
        ArenaLoop();
    };
};

acscript SelectArenaWave()
{
    ActivatorSound("menu/move", 127);

    if (ArenaMaxWave == 0) ArenaMaxWave = 1;
    WaveChoice = ArenaMaxWave;
    
    Delay(1);

    while (1)
    {
        SetPlayerProperty(false, 1, PROP_TOTALLYFROZEN);
        
		// Prevent the menus from being opened
		Player.InMenu = false;
		Player.InShop = false;
        
		// Text
        SetFont("BIGFONT");
        HudMessage("Wave: \cd%d/%d\n", WaveChoice, ArenaMaxWave,
                   HUDMSG_FADEOUT, 1, CR_WHITE, 0.5, 0.5, 0.05, 1.0);
        
        // Input
        int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
        int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);

        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && WaveChoice > 1)
        {
            ActivatorSound("menu/move", 127);
            WaveChoice--;
        };
        if (Buttons == BT_BACK && OldButtons != BT_BACK && WaveChoice < ArenaMaxWave)
        {
            ActivatorSound("menu/move", 127);
            WaveChoice++;
        };
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
            if (WaveChoice > 1)
                ArenaWave = WaveChoice - 1
            else
                ArenaWave = 1;
            
            ActivatorSound("menu/move", 127);
            SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
            return;
        };
        if (Buttons == BT_SPEED)
        {
            SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
            return;
        };
        
        Delay(1);
    };
};

acscript PissOffMarines()
{
    SetMusic("Outpost2", 3);
    Thing_Activate(1);
    SetActorProperty(1, APROP_Friendly, 0);
    Thing_Hate(1, 0, 6);
    Thing_ChangeTID(1, 1201);
    
    while (1)
    {
        // You are dead, NO BIG SURPRISE
        if (GetActorProperty(Player.TID, APROP_Health) <= 0)
        {
            SetMusic("Outpost2", 1);
            return;
        };
        
        // Teleport in new Marines
        if ((Timer() % (35 * 10)) == 1)
        {
            SpawnSpot("TeleportFog", MarineSpotTID, 0, 0);
            SpawnSpotFacing("MarineRandomizer", MarineSpotTID, MarineTID);
        };

        Delay(1);
    };
};

acscript TokenConverter()
{
	// if you're already in a menu, terminate
	if (Player.InOutpostMenu) return;
	
	int StatWorth = 1;
	int StatCapWorth = 10;
	int SkillWorth = 5;
	int InAmount = 1;
	int Amount1;
	int Amount2;
	int MaxAmount;
	int Type;
	int Cost;
	str Text;
	
	// Rank Check
	if (Player.RankLevel == 0)
	{
		SetFont("BIGFONT");
		Print("\cgYou must be at least Rank 1 to use the Token Converter\n");
		ActivatorSound("menu/error", 127);
		return;
	};
	
	SetPlayerProperty(false, 1, PROP_TOTALLYFROZEN);
	ActivatorSound("menu/move", 127);
	Player.InOutpostMenu = true;

	while (1)
	{
		int StatTokens = CheckInventory("StatToken");
		int StatCapTokens = CheckInventory("StatCapToken");
		int SkillTokens = CheckInventory("SkillToken");
		int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
		int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
		
		// Prevent the menus from being opened
		Player.InMenu = false;
		Player.InShop = false;
		
		switch (Type)
		{
			case 0: // Stat -> Skill
				Amount1 = InAmount * SkillWorth;
				Amount2 = InAmount;
				MaxAmount = StatTokens / SkillWorth;
				Cost = 5000 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\cg%d Stat Tokens \cj-> \cd%d Skill Tokens\n", Cost, Amount1, Amount2);
				break;
			case 1: // Stat -> Stat Cap
				Amount1 = InAmount * StatCapWorth;
				Amount2 = InAmount;
				MaxAmount = StatTokens / StatCapWorth;
				Cost = 1000 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\cg%d Stat Tokens \cj-> \ck%d Stat Cap Tokens\n", Cost, Amount1, Amount2);
				break;
			case 2: // Skill -> Stat
				Amount1 = InAmount * StatWorth;
				Amount2 = InAmount * SkillWorth;
				MaxAmount = SkillTokens;
				Cost = 25000 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\cd%d Skill Tokens \cj-> \cg%d Stat Tokens\n", Cost, Amount1, Amount2);
				break;
			case 3: // Skill -> Stat Cap
				Amount1 = InAmount * (StatCapWorth / SkillWorth);
				Amount2 = InAmount;
				MaxAmount = SkillTokens / 2;
				Cost = 12500 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\cd%d Skill Tokens \cj-> \ck%d Stat Cap Tokens\n", Cost, Amount1, Amount2);
				break;
			case 4: // Stat Cap -> Stat
				Amount1 = InAmount;
				Amount2 = InAmount * StatCapWorth;
				MaxAmount = StatCapTokens;
				Cost = 25000 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\ck%d Stat Cap Tokens \cj-> \cg%d Stat Tokens\n", Cost, Amount1, Amount2);
				break;
			case 5: // Stat Cap -> Skill
				Amount1 = InAmount;
				Amount2 = InAmount * (StatCapWorth / SkillWorth);
				MaxAmount = StatCapTokens;
				Cost = 10000 * Amount1;
				Text = StrParam("Convert: \cf%d C\n\ck%d Stat Cap Tokens \cj-> \cd%d Skill Tokens\n", Cost, Amount1, Amount2);
				break;
		};

		// Input
		if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && Type > 0)
		{
			ActivatorSound("menu/move", 127);
			InAmount = 1;
			Type--;
		};
		if (Buttons == BT_BACK && OldButtons != BT_BACK && Type < 5)
		{
			ActivatorSound("menu/move", 127);
			InAmount = 1;
			Type++;
		};
		if ((Buttons == BT_LEFT && OldButtons != BT_LEFT) ||
			(Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT)
			&& InAmount > 1)
		{
			ActivatorSound("menu/move", 127);
			InAmount--;
		};
		if ((Buttons == BT_RIGHT && OldButtons != BT_RIGHT) ||
			(Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT)
			&& InAmount <= MaxAmount - 1)
		{
			ActivatorSound("menu/move", 127);
			InAmount++;
		};
		if (Buttons == BT_USE && OldButtons != BT_USE)
		{
			str InItem;
			str OutItem;
			
			switch (Type)
			{
				case 0: InItem = "StatToken"; 		OutItem = "SkillToken";		break;
				case 1: InItem = "StatToken"; 		OutItem = "StatCapToken";	break;
				case 2: InItem = "SkillToken"; 		OutItem = "StatToken";		break;
				case 3: InItem = "SkillToken"; 		OutItem = "StatCapToken";	break;
				case 4: InItem = "StatCapToken"; 	OutItem = "StatToken";		break;
				case 5: InItem = "StatCapToken"; 	OutItem = "SkillToken";		break;
			};
			
			if (CheckInventory("Credits") >= Cost)
			{
				if ((Type == 0 || Type == 1) && StatTokens >= Amount1)
				{
					InAmount = 1;
					GiveInventory(OutItem, Amount2);
					TakeInventory(InItem, Amount1);
					TakeInventory("Credits", Cost);
					ActivatorSound("misc/tokenconvert", 127);
					Player.InOutpostMenu = false;
				}
				else if ((Type == 2 || Type == 3) && SkillTokens >= Amount1)
				{
					InAmount = 1;
					GiveInventory(OutItem, Amount2);
					TakeInventory(InItem, Amount1);
					TakeInventory("Credits", Cost);
					ActivatorSound("misc/tokenconvert", 127);
					Player.InOutpostMenu = false;
				}
				else if ((Type == 4 || Type == 5) && StatCapTokens >= Amount1)
				{
					InAmount = 1;
					GiveInventory(OutItem, Amount2);
					TakeInventory(InItem, Amount1);
					TakeInventory("Credits", Cost);
					ActivatorSound("misc/tokenconvert", 127);
					Player.InOutpostMenu = false;
				}
				else
					ActivatorSound("menu/error", 127);
			}
			else
				ActivatorSound("menu/error", 127);
		};
		if (Buttons == BT_SPEED && OldButtons != BT_SPEED)
		{
			ActivatorSound("menu/move", 127);
			SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
			Player.InOutpostMenu = false;
			return;
		};
		
		// Drawing
		SetHudSize(GetCVar("drpg_menu_width"), GetCVar("drpg_menu_height"), true);
		PrintSprite("TOKAA0", 0, 16.1, 188.1, 0.05);
		PrintSprite("TOKBA0", 0, 16.1, 208.1, 0.05);
		PrintSprite("TOKCA0", 0, 16.1, 228.1, 0.05);
		SetFont("BIGFONT");
		HudMessage("%d\n", StatTokens, 		HUDMSG_PLAIN, 0, CR_RED,  			40.1, 178.0, 0.05);
		HudMessage("%d\n", StatCapTokens, 	HUDMSG_PLAIN, 0, CR_YELLOW,  		40.1, 198.0, 0.05);
		HudMessage("%d\n", SkillTokens, 	HUDMSG_PLAIN, 0, CR_DARKGREEN,		40.1, 218.0, 0.05);
		SetFont("SMALLFONT");
		HudMessage("%s\n", Text, HUDMSG_PLAIN, 0, CR_WHITE, 140.1, 178.0, 0.05);

		Delay(1);
	};
};

// Credit Room Script
acscript CreditRoom(int ID)
{
	if (ID == 1) // Enter
		LocalSetMusic("Credits");
	if (ID == 2) // Exit
		LocalSetMusic("Outpost");
	
	if (ID == 3) // Kyle873 - That's me!
	{
		SetFont("BIGFONT");
		HudMessage("Kyle873\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 3.0, 2.0);
		Delay(35);
		SetFont("SMALLFONT");
		HudMessage("What do you mean I can't nerf XP Gain to 1 per monster?!\n", HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.55, 3.0, 2.0);

		int RealCredits = CheckInventory("Credits");
		
		ActivatorSound("credits/payout", 127);
		Log("\ckYou have been paid -2147483648 by the UAC!\n");
		TakeInventory("Credits", RealCredits);
		Delay(35 * 10);
		GiveInventory("Credits", RealCredits);
	};
	
	if (ID == 4) // Lord Misfit
	{
		SetFont("BIGFONT");
		HudMessage("Lord Misfit\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 3.0, 2.0);
		Delay(35);
		SetFont("SMALLFONT");
		HudMessage("I swear it's a real bug this time!\n", HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.55, 3.0, 2.0);
		
		str VarString = "SetVar";
		
		Delay(35 * 3);
		for (int i = 0; i < 100; i++)
		{
			if (Random(1, 3) == 1) VarString = StrParam("%s%s\n", VarString, "Var");
			HudMessage("%s\n", VarString, HUDMSG_FADEOUT, 0, Random(1, 21), RandomFixed(0.0, 1.0), RandomFixed(0.0, 1.0), 3.0, 2.0);
			Delay(1);
		};
	};
	
	if (ID == 5) // Ryan Cordell
	{
		SetFont("BIGFONT");
		HudMessage("Ryan Cordell\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 7.0, 2.0);
		Delay(35);
		
		for (int i = 0; i < 10; i++)
		{
			ActivatorSound("weapons/rocklx", 127);
			Delay(Random(5, 10));
		};
		
		Delay(35 * 2);
		SetFont("SMALLFONT");
		HudMessage("Did I do that?\n", HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.55, 3.0, 2.0);
	};
	
	if (ID == 6) // marrub
	{
		SetFont("BIGFONT");
		HudMessage("marrub\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 3.0, 2.0);
		Delay(35);
		HudMessage("I HOPE YOU LIKE SHOTGUNS!\n", HUDMSG_FADEOUT, 0, CR_BRICK, 0.5, 0.55, 3.0, 2.0);
		Delay(35 * 2);
		
		for (int i = 0; i < 50; i++)
		{
			DropItem(0, "DumbShotgun", 1, 255);
			Delay(1);
		};
	};

	if (ID == 7) // Kate
	{
		SetFont("BIGFONT");
		HudMessage("Kate\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 3.0, 2.0);
		Delay(35);
		HudMessage("\cgR\ciA\ckI\cdN\chB\ctO\caW\cjS\n", HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.55, 3.0, 2.0);
		Delay(35);
		
		FadeTo(255, 0, 0, 0.5, 0.5);
		Delay(17);
		FadeTo(255, 128, 0, 0.5, 0.5);
		Delay(17);
		FadeTo(255, 255, 0, 0.5, 0.5);
		Delay(17);
		FadeTo(0, 255, 0, 0.5, 0.5);
		Delay(17);
		FadeTo(0, 0, 255, 0.5, 0.5);
		Delay(17);
		FadeTo(128, 0, 255, 0.5, 0.5);
		Delay(17);
		FadeTo(255, 0, 255, 0.5, 0.5);
		Delay(17);
		FadeTo(255, 255, 255, 0.5, 0.5);
		Delay(17);
		FadeTo(0, 0, 0, 0.0, 0.5);
	};
	
	if (ID == 8) // Yholl
	{
		SetFont("BIGFONT");
		HudMessage("Yholl\n", HUDMSG_FADEOUT, 0, CR_GREEN, 0.5, 0.5, 3.0, 2.0);
		Delay(35);
		HudMessage("\caYour suffering pleases me\n", HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.55, 3.0, 2.0);
		Delay(35);
		
		for (int i = 0; i < 50; i++)
		{
			DropItem(0, "DumbPistol", 1, 255);
			Delay(1);
		};
	};
};

acscript ShopSpecial()
{
	int Minutes = ShopSpecialTimer / 35 / 60;
	int Seconds = ShopSpecialTimer / 35 % 60;
	str TimerDisplay;
	
	// if you're already in a menu, terminate
	if (Player.InOutpostMenu) return;
	
	if (ShopSpecialBought)
	{
		// Setup the timer display
		if (Seconds < 10)
			TimerDisplay = StrParam("%d:0%d\n", Minutes, Seconds)
		else
			TimerDisplay = StrParam("%d:%d\n", Minutes, Seconds);

		SetFont("BIGFONT");
		HudMessage("Shop Special is currently out of stock.\n\nNext restock will be in %s.\n", 
				   TimerDisplay,
				   HUDMSG_FADEOUT, 5000, CR_RED, 0.5, 0.5, 2.0, 1.0);
		ActivatorSound("menu/error", 127);
		return;
	};
	
	ActivatorSound("menu/move", 127);
	SetPlayerProperty(false, 1, PROP_TOTALLYFROZEN);
	Player.InOutpostMenu = true;
	
	while (!ShopSpecialBought)
	{
		int Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
		int OldButtons = GetPlayerInput(PlayerNumber(), INPUT_OLDBUTTONS);
		str Name = ShopSpecialItem.Name;
		int Discount = (30 - (GameSkill() * 5));
		int Cost = ShopSpecialItem.Price - ShopSpecialItem.Price * Discount / 100;
		
		// If the item's already been bought, terminate
		if (ShopSpecialBought)
		{
			SetPlayerProperty(true, 0, PROP_TOTALLYFROZEN);
			return;
		};
		
		// Setup the timer display
		Minutes = ShopSpecialTimer / 35 / 60;
		Seconds = ShopSpecialTimer / 35 % 60;
		if (Seconds < 10)
			TimerDisplay = StrParam("%d:0%d\n", Minutes, Seconds)
		else
			TimerDisplay = StrParam("%d:%d\n", Minutes, Seconds);
		
		// The cost should always be at least 1 Credit
		if (Cost <= 0) Cost = 1;
		
		// Prevent the menus from being opened
		Player.InMenu = false;
		Player.InShop = false;

		// Input
        if (Buttons == BT_USE && OldButtons != BT_USE)
        {
			// Buy Item
			if (CheckInventory("Credits") >= Cost)
			{
				TakeInventory("Credits", Cost);
				SpawnForced(ShopSpecialItem.Actor, GetActorX(0), GetActorY(0), GetActorZ(0), 0, 0);
				SetActorVelocity(Player.TID, 0.01, 0.01, 0, true, false);
				ShopSpecialBought = true;
				
				ActivatorSound("credits/payout", 127);
				Delay(1);
				SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
				Player.InOutpostMenu = false;
				return;
			}
			else
				ActivatorSound("menu/error", 127);
        };
        if (Buttons == BT_SPEED)
        {
			ActivatorSound("menu/move", 127);
			SetPlayerProperty(false, 0, PROP_TOTALLYFROZEN);
			Player.InOutpostMenu = false;
            return;
        };
		
		// Drawing
		SetFont("BIGFONT");
		HudMessage("%s\n\ck%d C (Discount: %d%%)\n\cdTime Left: %s\n\n\cd%K\cj to buy\n\cd%K\cj to exit\n", 
				   Name, Cost, Discount, TimerDisplay, "+use", "+speed",
				   HUDMSG_FADEOUT, 5000, CR_WHITE, 0.5, 0.5, 0.05, 1.0);
		
		Delay(1);
	};
};
