#library "RPG"

#include "zcommon.acs"

#import "Stats.acs"
#import "Skills.acs"
#import "Augs.acs"
#import "Shop.acs"
#import "Menu.acs"
#import "Shield.acs"
#import "Utils.acs"
#import "XP.acs"

#libdefine MAX_NAMES		19

// Flags
global int 1 : FirstRun;

str Version = "v0.94 Beta";
str TimeStamp = "Saturday, January 11, 2014 at 6:01:07 PM";

int MapNumber;
int MapTime;
int ParBonus;

// Monster Names
str MonsterNames[MAX_NAMES][2] =
{
	{ "ZombieMan", "Zombie Man" },
	{ "ShotgunGuy", "Shotgun Guy" },
	{ "ChaingunGuy", "Chaingun Guy" },
	{ "Imp", "Imp" },
	{ "Demon", "Demon" },
	{ "Spectre", "Spectre" },
	{ "Cacodemon", "Cacodemon" },
	{ "HellKnight", "Hell Knight" },
	{ "BaronOfHell", "Baron of Hell" },
	{ "LostSoul", "Lost Soul" },
	{ "PainElemental", "Pain Elemental" },
	{ "Revenant", "Revenant" },
	{ "Fatso", "Mancubus" },
	{ "Arachnotron", "Arachnotron" },
	{ "ArchVile", "Arch-Vile" },
	{ "WolfensteinSS", "Wolfenstein SS" },
	{ "Cyberdemon", "Cyberdemon" },
	{ "SpiderMastermind", "Spider Mastermind" },
	{ "Marine", "Marine" }
};

// Init Script
script "Init" enter
{
	if (!FirstRun)
	{
		EP = 100;
		RankString = Ranks[0];
		Vitality = 10;
		Energy = 10;
		Capacity = 10;
		ComboTimer = -100;
		
		ShieldTimer = 175;
		ShieldBody = 1;
		ShieldBattery = 1;
		ShieldCapacitor = 1;
		ShieldAccessory = 0;
		
		int Tokens = 10 - FixedMul(GameSkill(), 0.75);
		GiveInventory("StatToken", Tokens);
		GiveInventory("SkillToken", Tokens);
		DefaultLoadout();
		
		// Starting Credits
		int StartCredits;
		switch (GameSkill())
		{
			case 1: StartCredits = 1000; 	break;
			case 2: StartCredits = 750; 	break;
			case 3: StartCredits = 500; 	break;
			case 4: StartCredits = 250; 	break;
			case 5: StartCredits = 200; 	break;
			case 6: StartCredits = 100; 	break;
			case 7: StartCredits = 0; 		break;
		}
		GiveInventory("Credits", StartCredits);
		
		// Version Info
		Log(s:"\cnDoom RPG ", s:Version, s:" (", s:TimeStamp, s:") ", s:"loaded!");
		
		// Set the default skill indices
		for (int i = 0; i < 5; i++)
		{
			SkillCatagory[i] = -1;
			SkillIndex[i] = -1;
		}
		
		// Damage Number Translations
		CreateTranslation(1, 0 : 255 = 176 : 176);	// Overkill
		CreateTranslation(2, 0 : 255 = 112 : 112);	// Healed
		CreateTranslation(3, 0 : 255 = 100 : 100);	// Scratch
		
		// Done first run
		FirstRun = true;
	}
	
	// Count all the monsters in the map and Setup the XP/Rank tables
	CountMonsters();
	SetupTables();
	
	// Execute Game Loops
	ACS_NamedExecuteAlways("Loop", 0);
	ACS_NamedExecuteAlways("Speed", 0);
	ACS_NamedExecuteAlways("DamageNumbers", 0);
	ACS_NamedExecuteAlways("HealthBars", 0);
	
	// Remove Aura if the keep Aura CVAR is off
	if (!GetCVAR("drpg_skill_keepauras"))
	{
		Aura = 0;
		AuraTimer = 0;
		AuraTimerHUD = 0;
	}
	
	// Transport from/to Outpost Screen Fading
	if (Transported)
	{
		FadeRange(255, 255, 255, 1.0, 255, 255, 255, 0.0, 1.0);
		Transported = false;
	}
	
	// Give the player the fake difficulty Monster Defense enhancing item
	MapNumber = GetLevelInfo(LEVELINFO_LEVELNUM);
	if 		(MapNumber == 999)	MapNumber = 1;
	else if (MapNumber > 30) 	MapNumber = 30;
	if (GetCVAR("drpg_monster_defense"))
		GiveInventory(StrParam(s:"MonsterDefense", d:MapNumber), 1);
	
	// IF you have the Nova Shield Accessory and move to a new level, make sure to check the Nova flag
	if (CheckInventory("Shield") && ShieldAccessory == 5)
		Nova = false;
	
	// Reset the menus if they were open in the last level
	InMenu = false;
	InShop = false;
}

// Nonster Init Script
script "MonsterInit" (void)
{
	// Start Damage Numbers Script
	ACS_NamedExecuteAlways("DamageNumbers", 0);
	
	// Start Monster Loop Script
	ACS_NamedExecuteAlways("MonsterLoop", 0);
}

// Monster Loop
script "MonsterLoop" (void)
{
	// If dead, terminate
	if (GetActorProperty(0, APROP_HEALTH) <= 0) terminate;
	
	// Init
	if (!CheckInventory("MonsterInit"))
	{
		// Monster Level System
		if (GetCVAR("drpg_monster_levels"))
		{
			int MonsterLevel;
			int MonsterStrength;
			int MonsterDefense;
			int MonsterRegen;
			
			// level
			MonsterLevel = Level + Random(-(GameSkill()), GameSkill());
			SetInventory("MonsterLevel", MonsterLevel);
			
			// Strength
			MonsterStrength = Level + Random(-(MonsterLevel - (10 - GameSkill())), MonsterLevel + GameSkill());
			SetInventory("MonsterStrength", MonsterStrength);
			
			// Defense
			MonsterDefense = Level + Random(-(MonsterLevel - (10 - GameSkill())), MonsterLevel + GameSkill());
			SetInventory("MonsterDefense", MonsterDefense);
			SetActorProperty(0, APROP_DAMAGEFACTOR, 1.0 - FixedMul(0.25, FixedDiv(MonsterDefense, 100)));
		}
		
		// Finished Init
		GiveInventory("MonsterInit", 1);
	}

	// Give Strength Item
	if (MonsterStrength > 0 && GetCVAR("drpg_monster_levels"))
		GiveInventory(StrParam(s:"Strength", d:(MonsterStrength * 2)), 1);
	
	Delay(1);
	restart;
}

// Loop Script
script "Loop" (void)
{
	// Update Functions
	CheckMonsters();
	CheckStats();
	CheckStatCaps();
	CheckLuck();
	CheckHealth();
	CheckArmorMax();
	CheckCombo();
	CheckLevel();
	CheckRank();
	CheckLevelInfo();
	CheckAugSlots();
	CheckAugs();
	CheckSkills();
	CheckSkillDescriptions();
	CheckBurnout();
	CheckShields();
	CheckShieldAccessory();
	CheckStatBounds();
	
	// Main Menu
	if (InMenu)
		MenuLoop();

	// Shop Menu
	if (InShop)
		ShopLoop();
		
	// Always Quick Heal if CVAR is set
	if (GetCVAR("drpg_auto_heal"))
		ACS_NamedExecuteAlways("QuickHeal", 0);

	// Shield Handling
	BeforeHealth = GetActorProperty(0, APROP_HEALTH);
	if (CheckInventory("Shield"))
		ACS_NamedExecuteAlways("Shield", 0);

	// Regeneration
	if (GetActorProperty(0, APROP_HEALTH) > 0)
		DoRegen();

	// Give the Strength boosting item
	if (Damage > 0 && Damage <= 16000)
		GiveInventory(StrParam(s:"Strength", d:Damage), 1);

	// Apply Stats
	SetActorProperty(0, APROP_SPAWNHEALTH, HealthMax);
	SetActorProperty(0, APROP_DAMAGEFACTOR, DamageFactor);
	SetActorProperty(0, APROP_SPEED, Speed);
	SetActorProperty(0, APROP_JUMPZ, JumpHeight);

	// For EP Bar on HUD
	TakeInventory("EP", 100);
	GiveInventory("EP", FixedMul(FixedDiv(EP, EPMax), 100));
	
	// Increase the monster difficulty depending on the map number if that CVAR is set
	if (GetCVAR("drpg_monster_attack"))
		SetActorProperty(0, APROP_DAMAGEFACTOR, DamageFactor + (MapNumber * 0.03125));
	
	// Make you invincible while in the Menus and if menu freezing is enabled
	if ((InMenu || InShop) && GetCVAR("drpg_menufreeze"))
		SetActorProperty(0, APROP_DAMAGEFACTOR, 0);
	
	// Increase the map timer
	MapTime++;
	
	// Loop
	Delay(1);
	
	// These need to be done in a seperate tic
	AfterHealth = GetActorProperty(0, APROP_HEALTH);
	AfterArmor = CheckInventory("BasicArmor");
	
	restart;
}

// Handles Weapon Firing Speed
script "Speed" (void)
{
	int Time;
	
	if (Agility <= 100)
		Time = Abs(Round(WeaponSpeed * 0.35 - 35.0));
	else
		Time = 0;
	
	if (GetCVAR("drpg_stat_weaponspeed"))
		GiveInventory("Speed", 1);
	
	Delay(Time + 1);
	restart;
}

// Quick Heal
script "QuickHeal" (int Quick)
{
	// If you're dead, terminate
	if (GetActorProperty(0, APROP_HEALTH) <= 0) terminate;
	
	int Health = GetActorProperty(0, APROP_HEALTH);
	int Percent = GetCVAR("drpg_auto_heal_percent");
	
	if ((Health < HealthMax / Percent) || Quick)
			 if (CheckInventory("StimPack")) 		UseInventory("StimPack");
		else if (CheckInventory("SmallMedikit")) 	UseInventory("SmallMedikit");
		else if (CheckInventory("Medikit")) 		UseInventory("Medikit");
		else if (CheckInventory("LargeMedikit")) 	UseInventory("LargeMedikit");
		else if (CheckInventory("XLMedikit")) 		UseInventory("XLMedikit");
		else if (CheckInventory("MedPack")) 		UseInventory("MedPack");
		else if (CheckInventory("SuperMedPack")) 	UseInventory("SuperMedPack");
		else if (CheckInventory("SurgeryKit")) 		UseInventory("SurgeryKit");
}

// Quick Armor
script "QuickArmor" (void)
{
		 if (CheckInventory("WhiteArmor")) 		UseInventory("WhiteArmor");
	else if (CheckInventory("WhiteArmorUsed")) 	UseInventory("WhiteArmorUsed");
	else if (CheckInventory("RedArmor")) 		UseInventory("RedArmor");
	else if (CheckInventory("RedArmorUsed")) 	UseInventory("RedArmorUsed");
	else if (CheckInventory("YellowArmor")) 	UseInventory("YellowArmor");
	else if (CheckInventory("YellowArmorUsed")) UseInventory("YellowArmorUsed");
	else if (CheckInventory("BlueArmor")) 		UseInventory("BlueArmor");
	else if (CheckInventory("BlueArmorUsed")) 	UseInventory("BlueArmorUsed");
	else if (CheckInventory("GreenArmor")) 		UseInventory("GreenArmor");
	else if (CheckInventory("GreenArmorUsed")) 	UseInventory("GreenArmorUsed");
}

// Buy Ammo for selected Weapon
script "QuickAmmo" (void)
{
	// Weapon, Ammo Type and Amount
	int Weapon;
	int AmmoType;
	int Amount;
	
	// Get the current Weapon to determine the ammo type
	if 		(CheckWeapon("Pistol") || CheckWeapon("ChainGun")) 		{ AmmoType = 0; Amount = 10; }
	else if (CheckWeapon("Shotgun") || CheckWeapon("SuperShotgun")) { AmmoType = 2; Amount = 4;  }
	else if (CheckWeapon("RocketLauncher"))							{ AmmoType = 4; Amount = 1;  }
	else if (CheckWeapon("PlasmaRifle") || CheckWeapon("BFG9000"))	{ AmmoType = 6; Amount = 20; }
	
	// ... And the alternate Extras versions too -_-
	else if (CheckWeapon("Pistol1") || CheckWeapon("ChainGun1")) 	{ AmmoType = 0; Amount = 10; }
	else if (CheckWeapon("Shotgun1") || CheckWeapon("SSG")) 		{ AmmoType = 2; Amount = 4;  }
	else if (CheckWeapon("RocketLauncher1"))						{ AmmoType = 4; Amount = 1;  }
	else if (CheckWeapon("PlasmaGun") || CheckWeapon("BFG9001"))	{ AmmoType = 6; Amount = 20; }
	
	// AND Brutal Doom -__-
	else if (CheckWeapon("BrutalPistol") || CheckWeapon("Rifle") || CheckWeapon("Mini_Gun")) 									{ AmmoType = 0; Amount = 10; }
	else if (CheckWeapon("Shot_Gun") || CheckWeapon("SSG")) 																	{ AmmoType = 2; Amount = 4;  }
	else if (CheckWeapon("Rocket_Launcher") || CheckWeapon("Grenade_Launcher"))													{ AmmoType = 4; Amount = 1;  }
	else if (CheckWeapon("Plasma_Gun") || CheckWeapon("Rail_Gun") || CheckWeapon("BIG_FUCKING_GUN") || CheckWeapon("BFG10000"))	{ AmmoType = 6; Amount = 20; }
	else
	{
		ActivatorSound("menu/error", 127);
		terminate;
	}
	
	// Discount and Cost
	int Discount = FixedMul(RankLevel, 2.1);
	int Cost = ShopCosts[1][AmmoType] - ShopCosts[1][AmmoType] * Discount / 100;

	// Give ammo and take Credits
	if (CheckInventory("Credits") >= Cost && CheckInventory(Shop[1][AmmoType][0]) < GetAmmoCapacity(Shop[1][AmmoType][0]))
	{
		ActivatorSound("credits/payout", 127);
		TakeInventory("Credits", Cost);
		GiveInventory(Shop[1][AmmoType][0], Amount);
	}
	else
		ActivatorSound("menu/error", 127);
}

// Death Script
script "Death" death
{
	// Taunt the Player
	if (GetCVAR("drpg_ext_brutal"))
		ActivatorSound("Taunt", 127);
	
	while (1)
	{
		// Wipe out your entire inventory
		ClearInventory();
		
		// Kill all the player's summons
		for (int i = 2100; i <= SummonID; i++)
			Thing_Damage(i, 1000000);
		
		// Reset a metric fuckton of variables to make death look cleaner
		InMenu = false;
		InShop = false;
		Menu = 0;
		MenuIndex = 0;
		ShopIndex = 0;
		Strength = 0;
		Defense = 0;
		Vitality = 0;
		Energy = 0;
		Regeneration = 0;
		Agility = 0;
		Capacity = 0;
		Luck = 0;
		Level = 0;
		XP = 0;
		RankLevel = 0;
		Rank = 0;
		EP = 0;
		Combo = 0;
		ComboTimer = -100;
		TotalGained = 0;
		XPGained = 0;
		RankGained = 0;
		Aura = 0;
		AuraTimer = 0;
		AuraTimerHUD = 0;
		PayTimer = 0;
		Shield = 0;
		ShieldBody = 0;
		ShieldBattery = 0;
		ShieldCapacitor = 0;
		ShieldAccessory = 0;
		
		Delay(1);
	}
}

// Handles Damage Numbers
script "DamageNumbers" (void)
{
	while (GetActorProperty(0, APROP_HEALTH) > 0 && GetUserCVAR(0, "drpg_damagenumbers"))
	{
		GiveInventory("MonsterHeightCheck", GetActorProperty(0, APROP_HEIGHT));
		SetInventory("MonsterCurrentHealth", GetActorProperty(0, APROP_HEALTH));
		SetInventory("MonsterHasTicked", 0);
		
		// Lag handling
		int DelayRate = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS) / 100;
		Delay(1 + DelayRate + GetUserCVAR(0, "drpg_damagenumbers_delay"));
		
		if (GetActorProperty(0, APROP_HEALTH) != CheckInventory("MonsterCurrentHealth"))
			if (CheckInventory("MonsterHasTicked") == 0)
			{
				int NewHealth = CheckInventory("MonsterCurrentHealth") - GetActorProperty(0, APROP_HEALTH);
				
				if (NewHealth > 9999)
					NewHealth = 9999;
				
				if (NewHealth != 0)
				{
					int Digit1 = GetDigit(Abs(NewHealth), 0);
					int Digit2 = GetDigit(Abs(NewHealth), 1);
					int Digit3 = GetDigit(Abs(NewHealth), 2);
					int Digit4 = GetDigit(Abs(NewHealth), 3);
					
					int X = GetActorX(0) + 1.0;
					int Y = GetActorY(0) + Random(-10.0, 10.0);
					int Z;
					int ActorHeight = CheckInventory("MonsterHeightCheck");
					int DigitID = Random(4000, 5000);
					bool Healed = (NewHealth < 0);
					
					if (ActorHeight > 0)
						Z = GetActorZ(0) + Random(ActorHeight, ActorHeight + 6);
					else
						Z = GetActorZ(0) + GetActorProperty(0, APROP_HEIGHT) + Random(0.0, 6.0);
					
					switch (Digit1)
					{
						case 0:	Spawn("Digit1Num0", X, Y, Z, DigitID, 0);	break;
						case 1:	Spawn("Digit1Num1", X, Y, Z, DigitID, 0);	break;
						case 2:	Spawn("Digit1Num2", X, Y, Z, DigitID, 0);	break;
						case 3:	Spawn("Digit1Num3", X, Y, Z, DigitID, 0);	break;
						case 4:	Spawn("Digit1Num4", X, Y, Z, DigitID, 0);	break;
						case 5:	Spawn("Digit1Num5", X, Y, Z, DigitID, 0);	break;
						case 6:	Spawn("Digit1Num6", X, Y, Z, DigitID, 0);	break;
						case 7:	Spawn("Digit1Num7", X, Y, Z, DigitID, 0);	break;
						case 8:	Spawn("Digit1Num8", X, Y, Z, DigitID, 0);	break;
						case 9:	Spawn("Digit1Num9", X, Y, Z, DigitID, 0);	break;
					}
					
					if (NewHealth >= 10 || NewHealth <= -10)
						switch (Digit2)
						{
							case 0:	Spawn("Digit2Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit2Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit2Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit2Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit2Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit2Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit2Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit2Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit2Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit2Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					if (NewHealth >= 100 || NewHealth <= -100)
						switch (Digit3)
						{
							case 0:	Spawn("Digit3Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit3Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit3Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit3Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit3Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit3Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit3Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit3Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit3Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit3Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					if (NewHealth >= 1000 || NewHealth <= -1000)
						switch (Digit4)
						{
							case 0:	Spawn("Digit4Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit4Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit4Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit4Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit4Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit4Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit4Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit4Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit4Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit4Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					// Overkill
					if (NewHealth >= GetActorProperty(0, APROP_SPAWNHEALTH))
						Thing_SetTranslation(DigitID, 1);

					// Healed
					if (Healed)
						Thing_SetTranslation(DigitID, 2);

					// Scratch
					if (NewHealth == 1)
						Thing_SetTranslation(DigitID, 3);
						
					SetActorVelocity(DigitID, 0, Random(-1.0, 1.0), 0.5, 0, 0);
					SetInventory("MonsterHasTicked", 1);
				}
			}
			
		if (!GetUserCVAR(0, "drpg_damagenumbers")) terminate;
	}
}

// Handles HUD Health Bars
script "HealthBars" (void)
{
	while (GetCVAR("drpg_healthbars"))
	{
		Thing_ChangeTID(0, 1000);
		int Activator = ActivatorTID();
		SetActivatorToTarget(1000);
		
		Delay(1);
		
		int X = GetUserCVAR(0, "drpg_healthbars_x");
		int Y = GetUserCVAR(0, "drpg_healthbars_y");
		str Name = "";
		int CurrentLevel = CheckInventory("MonsterLevel");
		int CurrentStrength = CheckInventory("MonsterStrength");
		int CurrentDefense = CheckInventory("MonsterDefense");
		int CurrentRegen = CheckInventory("MonsterRegeneration");
		int CurrentHealth = GetActorProperty(0, APROP_HEALTH);
		int MaxHealth = GetActorProperty(0, APROP_SPAWNHEALTH);
		bool Friendly = GetActorProperty(0, APROP_FRIENDLY);
		int HealthPercent;
		str FillType;
		int i;
		
		if (ActivatorTID() != Activator)
		{
			SetActivator(Activator);
			SetActivatorToTarget(1000);
			
			SetActivator(Activator);
			
			// Divide-by-zero check
			if (MaxHealth > 0)
				HealthPercent = (CurrentHealth * 100) / MaxHealth;
			
			/* Name handling
			for (i = 0; i < MAX_NAMES - 1; i++)
				if (StrCmp(GetActorClass(0), MonsterNames[i][0]) == 0)
					Name = MonsterNames[i][1];
			*/
			
			if (HealthPercent > 100)
				FillType = "Fill5";
			if (HealthPercent >= 75 && HealthPercent <= 100)
				FillType = "Fill1";
			if (HealthPercent >= 50 && HealthPercent <= 74)
				FillType = "Fill2";
			if (HealthPercent >= 25 && HealthPercent <= 49)
				FillType = "Fill3";
			if (HealthPercent <= 24)
				FillType = "Fill4";
			
			/* Multi-part lols
			if (i >= 75 && i <= 100)
				FillType = "Fill1";
			if (i >= 50 && i <= 74)
				FillType = "Fill2";
			if (i >= 25 && i <= 49)
				FillType = "Fill3";
			if (i <= 24)
				FillType = "Fill4"; */

			if (CurrentHealth > 0 && (!InMenu && !InShop))
			{
				SetHUDSize(800, 600, false);
				
				if (Friendly)
					PrintSprite("HPBar2", 4000, X, Y, 0.05);
				else
					PrintSprite("HPBar", 4000, X, Y, 0.05);

				SetFont("SMALLFONT");
				HudMessage(d:CurrentHealth, s:"/", d:MaxHealth; HUDMSG_PLAIN, 4001, CR_GREEN, X, Y, 0.05);
				
				// Stats
				if (GetCVAR("drpg_monster_levels"))
				{
					SetFont("BIGFONT");
					
					// HudMessage(s:Name; HUDMSG_PLAIN, 4002, CR_GOLD, X, Y - 22.0, 0.05);
					HudMessage(d:CurrentLevel; HUDMSG_PLAIN, 4003, CR_WHITE, X - 96.1, Y + 21.0, 0.05);
					HudMessage(d:CurrentStrength; HUDMSG_PLAIN, 4004, CR_RED, X - 2.1, Y + 21.0, 0.05);
					HudMessage(d:CurrentDefense; HUDMSG_PLAIN, 4005, CR_GREEN, X + 44.1, Y + 21.0, 0.05);
					// HudMessage(d:CurrentRegen; HUDMSG_PLAIN, 4006, CR_PURPLE, X + 92.1, Y + 21.0, 0.05);
					
					// Stat Icons
					PrintSprite("MonLevel", 0, X - 107.0, Y + 20.0, 0.05);
					PrintSprite("MonSTR", 0, X - 13.0, Y + 21.0, 0.05);
					PrintSprite("MonDEF", 0, X + 36.0, Y + 21.0, 0.05);
					// PrintSprite("MonREG", 0, X + 84.0, Y + 20.0, 0.05);
				}
				
				// Prevent bar overflow
				if (HealthPercent > 100)
					HealthPercent = 100;
				
				for (i = 0; i <= HealthPercent; i++)
					PrintSprite(FillType, 4100 + i, (X - 97.0) + (i * 2.0), Y, 0.05);
			}
		}
	}
	
	Delay(1);
	restart;
}

// Level exit script
// Can't use NamedExecute in xlat, lame
script 30000 (int Secret)
{
	int ParTime = GetLevelInfo(LEVELINFO_PAR_TIME);
	
	// If there's no par time, or you somehow already activated this script, terminate
	if (ParTime <= 0 || ParBonus) terminate;
	
	if ((MapTime / 35) <= ParTime)
	{
		int RankBonus = Random(10000 * RankLevel, 10000 * RankLevel * MapNumber);
		if (RankLevel == 0) RankBonus = 10000;
		
		FadeRange(255, 255, 0, 0.25, 255, 255, 0, 0.0, 1.0);
		ActivatorSound("misc/secret", 127);
		Combo++;
		RankGained += RankBonus;
		
		HudMessage(s:"Par Time Beaten!\n",
				   d:RankBonus, s:" Rank Bonus";
				   HUDMSG_FADEOUT, 0, CR_GOLD, 0.5, 0.5, 3.0, 2.0);
		
		ParBonus = true;
		Delay(35 * 8);
	}

	if (Secret)
		Exit_Secret(0);
	else
		Exit_Normal(0);
}

function void DefaultLoadout(void)
{
	// Fists/Melee
	if (GetCVAR("drpg_ext_brutal"))
		GiveInventory("Melee_Attacks", 1);
	else // Weapons and Ammo
	{
		GiveInventory("Fist", 1);
		GiveInventory("Clip", 30);
		GiveInventory("Pistol", 1);
		SetWeapon("Pistol");
		SetWeapon("Pistol1");
	}
	
	// Give Standard Shield Components
	GiveInventory("ShieldBody1", 1);
	GiveInventory("ShieldBattery1", 1);
	GiveInventory("ShieldCapacitor1", 1);
	
	// Brutal Doom Equipment
	if (GetCVAR("drpg_ext_brutal"))
	{
		GiveInventory("BrutalPistol", 1);
		TakeInventory("Clip", 1000000);
		GiveInventory("Clip", 100);
	}	
}
