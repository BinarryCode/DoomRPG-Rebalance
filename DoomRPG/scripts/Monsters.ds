#include "RPG.dh"

#include "Arena.dh"
#include "Mission.dh"
#include "Monsters.dh"
#include "Popoffs.dh"
#include "Shield.dh"
#include "Stats.dh"
#include "Utils.dh"

AddressSpace map_array MonsterSpace;

MonsterSpace MonsterInfo[MAX_MONSTERS] MonsterData =
{
    { "ZombieMan";                          "Former Human";             1;      };
    { "ShotgunGuy";                         "Former Sergeant";          2;      };
    { "ChaingunGuy";                        "Former Commando";          5;      };
    { "DoomImp";                            "Imp";                      4;      };
    { "Demon";                              "Demon";                    3;      };
    { "Spectre";                            "Spectre";                  3;      };
    { "Cacodemon";                          "Cacodemon";                10;     };
    { "HellKnight";                         "Hell Knight";              20;     };
    { "BaronOfHell";                        "Baron of Hell";            30;     };
    { "LostSoul";                           "Lost Soul";                3;      };
    { "PainElemental";                      "Pain Elemental";           40;     };
    { "Revenant";                           "Revenant";                 50;     };
    { "Fatso";                              "Mancubu";                  60;     };
    { "Arachnotron";                        "Arachnotron";              60;     };
    { "ArchVile";                           "Arch-Vile";                70;     };
    { "Cyberdemon";                         "Cyberdemon";               85;     };
    { "SpiderMastermind";                   "Spider Mastermind";        95;     };
};

MonsterSpace MonsterInfo[MAX_MONSTERS_DRLA] MonsterDataDRLA =
{
    // Normal
    { "RLFormerHuman";                      "Former Human";             1;      };
    { "RLFormerSergeant";                   "Former Sergeant";          2;      };
    { "RLFormerCommando";                   "Former Commando";          5;      };
    { "RLFormerCaptain";                    "Former Captain";           8;      };
    { "RLImp";                              "Imp";                      4;      };
    { "RLDemon";                            "Demon";                    3;      };
    { "RLSpectre";                          "Spectre";                  3;      };
    { "RLLostSoul";                         "Lost Soul";                3;      };
    { "RLCacodemon";                        "Cacodemon";                10;     };
    { "RLHellKnight";                       "Hell Knight";              20;     };
    { "RLBaronOfHell";                      "Baron of Hell";            30;     };
    { "RLArachnotron";                      "Arachnotron";              60;     };
    { "RLPainElemental";                    "Pain Elemental";           40;     };
    { "RLRevenant";                         "Revenant";                 50;     };
    { "RLMancubus";                         "Mancubu";                  60;     };
    { "RLArchVile";                         "Arch-Vile";                70;     };
    { "RLCyberdemon";                       "Cyberdemon";               87;     };
    { "RLSpiderMastermind";                 "Spider Mastermind";        95;     };
    { "RLAgonyElemental";                   "Agony Elemental";          100;    };
    
    // Nightmare
    { "RLEliteHuman";                       "Elite Human";              5;      1;  };
    { "RLEliteSergeant";                    "Elite Sergeant";           8;      1;  };
    { "RLEliteCommando";                    "Elite Commando";           12;     1;  };
    { "RLEliteCaptain";                     "Elite Captain";            15;     1;  };
    { "RLNightmareImp";                     "Nightmare Imp";            8;      1;  };
    { "RLNightmareDemon";                   "Nightmare Demon";          6;      1;  };
    { "RLNightmareSpectre";                 "Nightmare Spectre";        6;      1;  };
    { "RLNightmareLostSoul";                "Nightmare Soul";           6;      1;  };
    { "RLNightmareCacodemon";               "Nightmare Cacodemon";      14;     1;  };
    { "RLNightmareHellKnight";              "Nightmare Knight";         25;     1;  };
    { "RLNightmareBaronOfHell";             "Baron of Nightmares";      36;     1;  };
    { "RLNightmareArachnotron";             "Nightmare Arachnotron";    67;     1;  };
    { "RLNightmarePainElemental";           "Nightmare Elemental";      43;     1;  };
    { "RLNightmareRevenant";                "Nightmare Revenant";       52;     1;  };
    { "RLNightmareMancubus";                "Nightmare Mancubu";        64;     1;  };
    { "RLNightmareArchvile";                "Nightmare Vile";           85;     1;  };
    { "RLNightmareCyberdemon";              "Nightmare Cyberdemon";     100;    1;  };
    
    // Cybernetic
    { "RLCyberneticImp";                    "Cyber-Imp";                6;      1;  };
    { "RLCyberneticDemon";                  "Mech-Demon";               5;      1;  };
    { "RLCyberneticSpectre";                "Mechtre";                  5;      1;  };
    { "RLCyberneticLostSoul";               "Hellmine";                 1;      1;  };
    { "RLCyberneticHellKnight";             "Cybruiser";                35;     1;  };
    { "RLCyberneticBaronOfHell";            "Techno-Lord";              40;     1;  };
    { "RLCyberneticArachnotron";            "Arachnosentinel";          64;     1;  };
    { "RLCyberneticRevenant";               "Heavy Revenant";           57;     1;  };
    { "RLCyberneticMancubus";               "Volacubu";                 68;     1;  };
    { "RLCyberneticSpiderMastermind";       "Spider Overmind";          100;    1;  };
    
    // Armageddon
    // ...
};

MonsterSpace str[MAX_HUMANS][2] MonsterHumanData =
{
    // Former
    { "RLFormerHumanPistol";            "RLFormerHuman";    };
    { "RLFormerHumanBattleRifle";       "RLFormerHuman";    };
    { "RLFormerHumanBattleRifle2";      "RLFormerHuman";    };
    { "RLFormerHumanBattleRifle3";      "RLFormerHuman";    };
    { "RLFormerSergeantShotgun";        "RLFormerSergeant"; };
    { "RLFormerSergeantCombatShotgun";  "RLFormerSergeant"; };
    { "RLFormerCommandoChaingun";       "RLFormerCommando"; };
    { "RLFormerCommandoBattleRifle";    "RLFormerCommando"; };
    { "RLFormerCaptainPlasmaRifle";     "RLFormerCaptain";  };
    
    // Elite
    { "RLEliteHumanCombatPistol";       "RLEliteHuman";     };
    { "RLEliteHumanHandcannon";         "RLEliteHuman";     };
    { "RLEliteHumanBattleRifle";        "RLEliteHuman";     };
    { "RLEliteHumanBattleRifle2";       "RLEliteHuman";     };
    { "RLEliteHumanUzi";                "RLEliteHuman";     };
    { "RLEliteSergeantDoubleShotgun";   "RLEliteSergeant";  };
    { "RLEliteSergeantAssaultShotgun";  "RLEliteSergeant";  };
    { "RLEliteSergeantPlasmaShotgun";   "RLEliteSergeant";  };
    { "RLEliteCommandoMinigun";         "RLEliteCommando";  };
    { "RLEliteCaptainTristarBlaster";   "RLEliteCaptain";   };
};

// Monster Init Script
acscript void MonsterInit(int Flags)
{
    // If initialization has already occurred on this monster, terminate
    if (CheckInventory("DRPGMonsterInit")) return;
    
    // Set as initialized
    SetInventory("DRPGMonsterInit", 1);
    
    // Set the Height
    SetInventory("DRPGHeightCheck", (int)GetActorPropertyFixed(0, APROP_Height));
    
    // Start Damage Numbers Script
    DamageNumbers();
    
    // Give it a Health Bar
    if (!(Flags & MF_NOHEALTHBAR))
        SetInventory("DRPGHealthBar", 1);
    
    // Apply Monster Flags
    SetInventory("DRPGMonsterFlags", Flags);
    
    // Apply Stats
    if (!(Flags & MF_NOSTATS))
        MonsterStats(Flags, false);
    
    // Strength / Defense Handling
    MonsterStatsHandler();
    
    // Save the Render Type
    SetInventory("DRPGMonsterRenderStyle", GetActorProperty(0, APROP_RenderStyle));
    
    // Aura Spawner
    MonsterAuraDisplayHandler();
    
    // Regeneration Stat Handling
    MonsterRegenerationHandler();
    
    // Death Handler
    MonsterDeathCheck();
};

// Modify the targeted monster (mainly for debugging)
acscript void MonsterSet(int Level, int Aura, int Flags)
{
    // Move script to the targeted monster
    SetActivatorToTarget(Player.TID);
    
    // Set Stats/Flags
    if (Level > 0)
        SetInventory("DRPGMonsterLevel", Level);
    if (Aura > 0)
        SetInventory("DRPGMonsterAura", Aura);
    if (Flags > 0)
        SetInventory("DRPGMonsterFlags", Flags);
    
    // Re-run the Stats Script
    MonsterStats(Flags, true);
};

// Dump the targeted monster's stats to the console
acscript void MonsterDump()
{
    int PlayerNum = PlayerNumber();
    
    // Move script to the targeted monster
    SetActivatorToTarget(Player.TID);
    
    // If you have no target, terminate
    if (ActivatorTID() == Players(PlayerNum).TID)
    {
        ActivatorSound("menu/error", 127);
        return;
    };
    
    Log("\ca===== MONSTER INFORMATION =====\n");
    Log(" Actor: %s\n", GetActorClass(0));
    Log(" Tag: %s\n", GetActorPropertyString(0, APROP_Nametag));
    Log(" Height: %k\n", GetActorPropertyFixed(0, APROP_Height));
    Log(" Radius: %k\n", GetActorPropertyFixed(0, APROP_Radius));
    Log(" Speed: %k\n", GetActorPropertyFixed(0, APROP_Speed));
    Log(" TID: %d\n", ActivatorTID());
    
    Log("\cf===== MONSTER FLAGS =====\n");
    Log(" Init: %d\n", CheckInventory("DRPGMonsterInit"));
    Log(" Named: %d\n", CheckInventory("DRPGMonsterNamed"));
    Log(" HealthBar: %d\n", CheckInventory("DRPGHealthBar"));
    Log(" RenderStyle: %d\n", CheckInventory("DRPGMonsterRenderStyle"));
    Log(" Flags: %d\n", CheckInventory("DRPGMonsterFlags"));
    
    Log("\cd===== MONSTER STATS =====\n");
    Log(" Level: %d\n", CheckInventory("DRPGMonsterLevel"));
    Log(" Aura: %d\n", CheckInventory("DRPGMonsterAura"));
    Log(" Threat: %d\n", CheckInventory("DRPGMonsterThreat"));
    Log(" Spawn Health: %d\n", CheckInventory("DRPGMonsterSpawnHealth"));
    Log(" Health Max %d\n", CheckInventory("DRPGMonsterHealthMax"));
    Log(" Current Speed: %d\n", CheckInventory("DRPGMonsterSpeed"));
    Log(" \cgStrength: %d\n", CheckInventory("DRPGMonsterStrength"));
    Log(" \cdDefense: %d\n", CheckInventory("DRPGMonsterDefense"));
    Log(" \caVitality: %d\n", CheckInventory("DRPGMonsterVitality"));
    Log(" \cnEnergy: %d\n", CheckInventory("DRPGMonsterEnergy"));
    Log(" \ctRegeneration: %d\n", CheckInventory("DRPGMonsterRegeneration"));
    Log(" \ciAgility: %d\n", CheckInventory("DRPGMonsterAgility"));
    Log(" \chCapacity: %d\n", CheckInventory("DRPGMonsterCapacity"));
    Log(" \ckLuck: %d\n", CheckInventory("DRPGMonsterLuck"));
};

// Damage the target
acscript void MonsterDamage(int Amount, int Type)
{
    SetActivatorToTarget(Player.TID);
    
    if (ActivatorTID() == Player.TID)
        return;
    
    DamageThing(Amount, Type);
};

// Apply stats to monsters
script void MonsterStats(int Flags, int StatFlags)
{
    // Move Activator to whoever the player is targeting
    if (StatFlags & SF_PLAYERTARGET)
        SetActivatorToTarget(Player.TID);
    
    int MonsterLevel = CheckInventory("DRPGMonsterLevel");
    int MonsterAura = CheckInventory("DRPGMonsterAura");
    int MonsterThreat = CheckInventory("DRPGMonsterThreat");
    int MonsterSpeed = CheckInventory("DRPGMonsterSpeed");
    int MonsterStrength = CheckInventory("DRPGMonsterStrength");
    int MonsterDefense = CheckInventory("DRPGMonsterDefense");
    int MonsterVitality = CheckInventory("DRPGMonsterVitality");
    int MonsterEnergy = CheckInventory("DRPGMonsterEnergy");
    int MonsterRegeneration = CheckInventory("DRPGMonsterRegeneration");
    int MonsterAgility = CheckInventory("DRPGMonsterAgility");
    int MonsterCapacity = CheckInventory("DRPGMonsterCapacity");
    int MonsterLuck = CheckInventory("DRPGMonsterLuck");
    int NumPlayers;
    int PlayerAvgLevel;
    int AuraRandomizer;
    int[8] StatEffect;
    
    // Add up the levels of all the players in the game
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        if (!PlayerInGame(i)) continue;
        NumPlayers++;
        PlayerAvgLevel += Players(i).Level;
    };
    
    // Now calculate the average level between them
    PlayerAvgLevel /= NumPlayers;
    
    // Monster Stats System
    if (GetCVar("drpg_monster_levels") > 0)
    {
        // level
        if (!StatFlags)
        {
            int LevelType = GetCVar("drpg_monster_levels");
            fixed LevelWeight = GetCVarFixed("drpg_monster_level_weight");
            fixed MapWeight = GetCVarFixed("drpg_monster_map_weight");
            fixed RandomMinWeight = GetCVarFixed("drpg_monster_random_min_mult");
            fixed RandomMaxWeight = GetCVarFixed("drpg_monster_random_max_mult");
            int LevelNum = GetLevelInfo(LEVELINFO_LEVELNUM);
            
            // Cap Level Number to 100
            if (LevelNum > 100)
                LevelNum = 100;
            
            // If the Arena is active, base the Monster Levels Map Number portion on the current wave
            if (InBase && ArenaActive)
                LevelNum = ArenaWave;
            
            // Calculate Monster Level
            if (LevelType == 1 || LevelType == 3) // Player Level
                MonsterLevel += (int)((fixed)PlayerAvgLevel * LevelWeight);
            if (LevelType == 2 || LevelType == 3) // Map Number
                MonsterLevel += (int)((fixed)LevelNum * MapWeight);
            
            // Randomization Weight
            if (RandomMinWeight > RandomMaxWeight)
                Log("\cgERROR: \c-Monster Random Min Multiplier cannot be above Monster Random Max Multiplier!\n")
            else
                MonsterLevel = (int)(MonsterLevel * RandomFixed(RandomMinWeight, RandomMaxWeight));
            
            // If the monster is friendly, it has the average level of all players in the game
            if (GetActorProperty(0, APROP_Friendly))
                MonsterLevel = PlayerAvgLevel;
            
            // Special case for Powersuit Mk. II
            if (GetActorClass(0) == "DRPGSuperPowerSuit")
                MonsterLevel = 1000;
        };
        
        // Calculate the monster's cut and special stats
        if (GetCVar("drpg_monster_specialize"))
        {
            // Zero out the StatEffect array
            for (int i = 0; i < 8; i++)
                StatEffect[i] = 0;
            
            int DelayCounter = 0;
            int CutCount = 0;
            int SpecializedCount = 0;
            while (CutCount < 4 || SpecializedCount < 2)
            {
                for (int i = 0; i < 8; i++)
                {
                    if (StatEffect[i] == 0)
                    {
                        if (CutCount < 4 && Random(1, 2) == 1) // Cut Stat
                        {
                            StatEffect[i] = 1;
                            CutCount++;
                        }
                        else if (SpecializedCount < 2) // Specialized Stat
                        {
                            StatEffect[i] = 2;
                            SpecializedCount++;
                        };
                    };
                };
                
                DelayCounter++;
                if ((DelayCounter % 10) == 0)
                    Delay(1);
                
                // Give up if it takes too long
                if (DelayCounter > 100)
                {
                    if (GetUserCVar(Arbitrator, "drpg_debug"))
                        Log("\cgERROR: \c-Gave up on specializing monster!\n");
                    break;
                };
            };
        };
        
        // Calculate Monster Stats
        MonsterStrength = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterDefense = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterVitality = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterEnergy = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterRegeneration = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterAgility = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterCapacity = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        MonsterLuck = Random(MonsterLevel, MonsterLevel + Random(GameSkill(), GameSkill() * 2));
        
        if (GetCVar("drpg_monster_specialize"))
        {
            // Cut Stats
            if (StatEffect[0] == 1) MonsterStrength /= 2;
            if (StatEffect[1] == 1) MonsterDefense /= 2;
            if (StatEffect[2] == 1) MonsterVitality /= 2;
            if (StatEffect[3] == 1) MonsterEnergy /= 2;
            if (StatEffect[4] == 1) MonsterRegeneration /= 2;
            if (StatEffect[5] == 1) MonsterAgility /= 2;
            if (StatEffect[6] == 1) MonsterCapacity /= 2;
            if (StatEffect[7] == 1) MonsterLuck /= 2;
            
            // Specialized Stats
            if (StatEffect[0] == 2) MonsterStrength *= Random(2, 4);
            if (StatEffect[1] == 2) MonsterDefense *= Random(2, 4);
            if (StatEffect[2] == 2) MonsterVitality *= Random(2, 4);
            if (StatEffect[3] == 2) MonsterEnergy *= Random(2, 4);
            if (StatEffect[4] == 2) MonsterRegeneration *= Random(2, 4);
            if (StatEffect[5] == 2) MonsterAgility *= Random(2, 4);
            if (StatEffect[6] == 2) MonsterCapacity *= Random(2, 4);
            if (StatEffect[7] == 2) MonsterLuck *= Random(2, 4);
        };
    };
    
    // Apply Aura
    if (!GetActorProperty(0, APROP_Friendly) && !(Flags & MF_NOAURA) && !StatFlags)
    {
        int AuraCurve = GetCVar("drpg_aura_curve");
        int AuraMin = -1000 + MonsterEnergy + AuraCurve;
        if (AuraMin > 0) AuraMin = 0;
        AuraRandomizer = Random(AuraMin, 10);
        if (AuraRandomizer > 0)
            MonsterAura = AuraRandomizer;
        if (GetCVar("drpg_monster_shadows"))
            MonsterAura = 10;
    };
    
    // Apply the aura effects
    switch (MonsterAura)
    {
        case 1: // Red Aura - Strength
            MonsterStrength *= 2;
            GiveInventory("DRPGRedAuraGiver", 1);
            break;
        case 2: // Green Aura - Defense
            MonsterDefense *= 2;
            GiveInventory("DRPGGreenAuraGiver", 1);
            break;
        case 3: // White Aura - XP
            MonsterLevel *= 2;
            GiveInventory("DRPGWhiteAuraGiver", 1);
            break;
        case 4: // Pink Aura - Vitality
            MonsterVitality *= 2;
            GiveInventory("DRPGPinkAuraGiver", 1);
            break;
        case 5: // Blue Aura - Energy
            MonsterEnergy *= 2;
            GiveInventory("DRPGBlueAuraGiver", 1);
            MonsterLevelupHandler();
            MonsterEPDrainHandler();
            break;
        case 6: // Purple Aura - Regeneration
            MonsterRegeneration *= 2;
            break;
        case 7: // Orange Aura - Agility
            MonsterAgility *= 2;
            GiveInventory("DRPGOrangeAuraGiver", 1);
            break;
        case 8: // Dark Blue Aura - Capacity
            MonsterCapacity *= 2;
            GiveInventory("DRPGDarkBlueAuraGiver", 1);
            break;
        case 9: // Yellow Aura - Luck
            MonsterLuck *= 2;
            MonsterMoneyDrainHandler();
            break;
        case 10: // Black Aura - Miniboss
            MonsterLevel *= 2;
            MonsterStrength *= 2;
            MonsterDefense *= 2;
            MonsterVitality *= 2;
            MonsterEnergy *= 2;
            MonsterRegeneration *= 2;
            MonsterAgility *= 2;
            MonsterCapacity *= 2;
            MonsterLuck *= 2;
            GiveInventory("DRPGRedAuraGiver", 1);
            GiveInventory("DRPGGreenAuraGiver", 1);
            GiveInventory("DRPGWhiteAuraGiver", 1);
            GiveInventory("DRPGPinkAuraGiver", 1);
            GiveInventory("DRPGBlueAuraGiver", 1);
            GiveInventory("DRPGOrangeAuraGiver", 1);
            GiveInventory("DRPGDarkBlueAuraGiver", 1);
            MonsterLevelupHandler();
            MonsterEPDrainHandler();
            MonsterMoneyDrainHandler();
            break;
    };
    
    // Cap Level and Stats
    if (MonsterLevel <= 0)
        MonsterLevel = 1;
    if (MonsterLevel > 1000)
        MonsterLevel = 1000;
    if (MonsterStrength > 1000)
        MonsterStrength = 1000;
    if (MonsterDefense > 1000)
        MonsterDefense = 1000;
    if (MonsterVitality > 1000)
        MonsterVitality = 1000;
    if (MonsterEnergy > 1000)
        MonsterEnergy = 1000;
    if (MonsterRegeneration > 1000)
        MonsterRegeneration = 1000;
    if (MonsterAgility > 1000)
        MonsterAgility = 1000;
    if (MonsterCapacity > 1000)
        MonsterCapacity = 1000;
    if (MonsterLuck > 1000)
        MonsterLuck = 1000;
    
    // We need to make sure we only pass the speed to the monster if it's being initially created
    if (!StatFlags)
        MonsterSpeed = (int)GetActorPropertyFixed(0, APROP_Speed);
    
    // Apply the stats to the monster
    SetInventory("DRPGMonsterLevel", MonsterLevel);
    SetInventory("DRPGMonsterAura", MonsterAura);
    SetInventory("DRPGMonsterSpeed", MonsterSpeed);
    SetInventory("DRPGMonsterStrength", MonsterStrength);
    SetInventory("DRPGMonsterDefense", MonsterDefense);
    SetInventory("DRPGMonsterVitality", MonsterVitality);
    SetInventory("DRPGMonsterEnergy", MonsterEnergy);
    SetInventory("DRPGMonsterRegeneration", MonsterRegeneration);
    SetInventory("DRPGMonsterAgility", MonsterAgility);
    SetInventory("DRPGMonsterCapacity", MonsterCapacity);
    SetInventory("DRPGMonsterLuck", MonsterLuck);
    
    // Calculate Threat Level
    MonsterThreat = CalculateMonsterThreatLevel(0);
    SetInventory("DRPGMonsterThreat", MonsterThreat);
    
    // Calculate Health
    SetInventory("DRPGMonsterSpawnHealth", GetActorProperty(0, APROP_SpawnHealth));
    SetInventory("DRPGMonsterHealthMax", CalculateMonsterMaxHealth(0));
    SetActorProperty(0, APROP_Health, CalculateMonsterMaxHealth(0));
    
    // Calculate Speed
    SetActorPropertyFixed(0, APROP_Speed, CalculateMonsterSpeed(0));
};

script void MonsterStatsHandler()
{
    int MonsterStrength;
    int MonsterDefense;
    int OldStrength;
    int OldDefense;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    MonsterStrength = CheckInventory("DRPGMonsterStrength");
    MonsterDefense = CheckInventory("DRPGMonsterDefense");
    
    // Strength
    if (MonsterStrength != OldStrength)
    {
        if (OldStrength)
            TakeInventory(StrParam("DRPGStrength%d\n", OldStrength * GameSkill()), 1);
        SetInventory(StrParam("DRPGStrength%d\n", MonsterStrength * GameSkill()), 1);
        OldStrength = MonsterStrength;
    };
    
    // Defense
    if (MonsterDefense != OldDefense)
    {
        SetActorPropertyFixed(0, APROP_DamageFactor, 1.0 - (0.09 * (MonsterDefense / 100)));
        OldDefense = MonsterDefense;
    };
    
    Delay(4);
    
    goto Start;
};

script void MonsterAuraDisplayHandler()
{
    int MonsterRenderStyle = CheckInventory("DRPGMonsterRenderStyle");
    int AuraType;
    bool VisibleToPlayers;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    VisibleToPlayers = false;
    
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        if (!PlayerInGame(i))
            continue;
        
        if (CheckSight(Players(i).TID, 0, CSF_NOBLOCKALL))
        {
            VisibleToPlayers = true;
            break;
        };
    };
    
    if (!VisibleToPlayers)
    {
        Delay(10);
        goto Start;
    };
    
    AuraType = CheckInventory("DRPGMonsterAura");
    
    // Friendly Monster Aura
    if (GetActorProperty(0, APROP_Friendly) && GetActorPropertyString(0, APROP_Nametag) != "Marine" && GetActorPropertyString(0, APROP_Nametag) != "Heavy Marine" && !CheckInventory("DRPGMonsterNamed"))
        SpawnAura(0, 0, false);
    
    // Black Aura Effects
    if (AuraType == 10)
    {
        if (GetActorLightLevel(0) < 128)
            SetActorProperty(0, APROP_RenderStyle, STYLE_Fuzzy)
        else
            SetActorProperty(0, APROP_RenderStyle, MonsterRenderStyle);
    };
    
    // Spawn Aura
    if (AuraType)
        SpawnAura(0, AuraType, false);
    
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    Delay(1);
    goto Start;
};

script void MonsterLevelupHandler()
{
    int MonsterLevel;
    int MonsterFlags;
    int MonsterStrength;
    int MonsterDefense;
    int MonsterVitality;
    int MonsterEnergy;
    int MonsterRegeneration;
    int MonsterAgility;
    int MonsterCapacity;
    int MonsterLuck;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    if (CheckActorInventory(0, "PowerTimeFreezer"))
    {
        Delay(35);
        return;
    };
    
    Delay(35 * (60 - (MonsterEnergy / 20)));
    
    // Get Monster Stats
    MonsterLevel = CheckInventory("DRPGMonsterLevel");
    MonsterFlags = CheckInventory("DRPGMonsterFlags");
    MonsterStrength = CheckInventory("DRPGMonsterStrength");
    MonsterDefense = CheckInventory("DRPGMonsterDefense");
    MonsterVitality = CheckInventory("DRPGMonsterVitality");
    MonsterEnergy = CheckInventory("DRPGMonsterEnergy");
    MonsterRegeneration = CheckInventory("DRPGMonsterRegeneration");
    MonsterAgility = CheckInventory("DRPGMonsterAgility");
    MonsterCapacity = CheckInventory("DRPGMonsterCapacity");
    MonsterLuck = CheckInventory("DRPGMonsterLuck");
    
    // Apply the stats to the monster
    SetInventory("DRPGMonsterLevel", MonsterLevel + 1);
    SetInventory("DRPGMonsterStrength", MonsterStrength + Random(1, GameSkill()));
    SetInventory("DRPGMonsterDefense", MonsterDefense + Random(1, GameSkill()));
    SetInventory("DRPGMonsterVitality", MonsterVitality + Random(1, GameSkill()));
    SetInventory("DRPGMonsterEnergy", MonsterEnergy + Random(1, GameSkill()));
    SetInventory("DRPGMonsterRegeneration", MonsterRegeneration + Random(1, GameSkill()));
    SetInventory("DRPGMonsterAgility", MonsterAgility + Random(1, GameSkill()));
    SetInventory("DRPGMonsterCapacity", MonsterCapacity + Random(1, GameSkill()));
    SetInventory("DRPGMonsterLuck", MonsterLuck + Random(1, GameSkill()));
    
    // Re-calculate the monster's threat level
    SetInventory("DRPGMonsterThreat", CalculateMonsterThreatLevel(0));
    
    // Spawn Level-up Arrow
    SpawnForced("DRPGLevelUpArrow", GetActorX(0), GetActorY(0), GetActorZ(0) + GetActorPropertyFixed(0, APROP_Height), 0, 0);
    
    goto Start;
};

script void MonsterEPDrainHandler()
{
    int Energy;
    fixed Radius;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    if (CheckActorInventory(0, "PowerTimeFreezer"))
    {
        Delay(35);
        return;
    };
    
    Energy = CheckInventory("DRPGMonsterEnergy");
    Radius = GetActorPropertyFixed(0, APROP_Radius);
    
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        if (!PlayerInGame(i)) continue;
        
        if (Distance(0, Players(i).TID) < Radius * GameSkill() * 4 && CheckSight(0, Players(i).TID, 0) && Players(i).EP > 0)
        {
            int Previous = Players(i).EP;
            
            Players(i).EP -= Energy;
            
            if (Players(i).EP < 0)
                Players(i).EP = 0;
            
            if (Players(i).EP < Previous)
                Popoff(0, Previous - Players(i).EP, DNUM_EPGAIN);
        };
    };
    
    Delay(35);
    
    goto Start;
};

script void MonsterRegenerationHandler()
{
    int Regeneration;
    int AuraType;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    if (CheckActorInventory(0, "PowerTimeFreezer"))
    {
        Delay(35);
        return;
    };
    
    Regeneration = CheckInventory("DRPGMonsterRegeneration");
    AuraType = CheckInventory("DRPGMonsterAura");
    
    SetActorProperty(0, APROP_Health, GetActorProperty(0, APROP_Health) + (int)((fixed)CalculateMonsterMaxHealth(0) * ((fixed)Regeneration / 1000.0)));
    
    // Prevent going over 100%
    if (GetActorProperty(0, APROP_Health) > CalculateMonsterMaxHealth(0))
        SetActorProperty(0, APROP_Health, CalculateMonsterMaxHealth(0));
    
    Delay(35 * (30 - (AuraType == 6 ? 20 : 0)));
    goto Start;
};

script void MonsterMoneyDrainHandler()
{
    int Luck;
    fixed Radius;
    
    Start:
    if (ClassifyActor(0) & ACTOR_DEAD)
        return;
    
    if (CheckActorInventory(0, "PowerTimeFreezer"))
    {
        Delay(35);
        return;
    };
    
    Luck = CheckInventory("DRPGMonsterLuck");
    Radius = GetActorPropertyFixed(0, APROP_Radius);
    
    for (int i = 0; i < MAX_PLAYERS; i++)
    {
        if (!PlayerInGame(i)) continue;
        
        if (Distance(0, Players(i).TID) < Radius * GameSkill() * 4 && CheckSight(0, Players(i).TID, 0))
        {
            int Previous = CheckActorInventory(Players(i).TID, "DRPGCredits");
            
            TakeActorInventory(Players(i).TID, "DRPGCredits", Luck);
            GiveInventory("DRPGCredits", Luck);
            
            if (CheckActorInventory(Players(i).TID, "DRPGCredits") < Previous)
                Popoff(0, Previous - CheckActorInventory(Players(i).TID, "DRPGCredits"), DNUM_CREDGAIN);
        };
    };
    
    Delay(35);
    
    goto Start;
};

script void MonsterDeathCheck()
{
    Start:
    
    // Death
    if (ClassifyActor(0) & ACTOR_ALIVE)
    {
        Delay(1);
        goto Start;
    };
    
    MonsterDeath();
};

script void MonsterDeath()
{
    int Killer = WhoKilledMe();
    int MonsterLevel = CheckInventory("DRPGMonsterLevel");
    int MonsterThreat = CheckInventory("DRPGMonsterThreat");
    int MonsterFlags = CheckInventory("DRPGMonsterFlags");
    int MonsterCapacity = CheckInventory("DRPGMonsterCapacity");
    int MonsterLuck = CheckInventory("DRPGMonsterLuck");
    int AuraType = CheckInventory("DRPGMonsterAura");
    fixed XPAmount = Random(GetActorProperty(0, APROP_SpawnHealth) / 2.0, GetActorProperty(0, APROP_SpawnHealth)) * (1.0 + (fixed)MonsterThreat * 0.3);
    int RankAmount = GetActorProperty(0, APROP_SpawnHealth) * (1 + (MonsterThreat - (MonsterThreat > 0 ? 1 : 0)));
    int DropMod = MonsterLuck * 0.256;
    int DropAmount = (int)(1.0 + ((fixed)MonsterCapacity / 100.0));
    int DropTID = (Players(Killer).Stim.PowerupTimer[STIM_MAGNETIC] > 0 ? Players(Killer).TID : 0);
    
    // Don't scale drops to skill level
    if (GetCVar("drpg_scale_drops"))
        DropAmount /= GameSkill();
    
    // Always drop at least one item
    if (DropAmount < 1)
        DropAmount = 1;
    
    // Prevent Drop Mod from overflowing
    if (DropMod > 256)
        DropMod = 256;
    
    // Aura-Based XP/Rank Modifiers
    if (AuraType > 0 && AuraType < 10) // All Auras except Black
        RankAmount *= 2;
    if (AuraType == 3) // White
        XPAmount *= 2;
    if (AuraType == 10) // Black
    {
        XPAmount *= 2;
        RankAmount *= 4;
    };
    
    // Double Drop Amount
    if (AuraType == 9 || AuraType == 10)
        DropAmount *= 2;
    
    // Drops
    if (!(MonsterFlags & MF_NODROPS) && !GetActorProperty(0, APROP_Friendly))
    {
        // Aura Drops
        for (int i = 1; i <= DropAmount; i++)
        {
            switch (AuraType)
            {
                case 1: // Red Aura - Strength
                    DropItem(DropTID, "DRPGWeaponDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialStrength", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 2: // Green Aura - Defense
                    DropItem(DropTID, "DRPGUsedArmorDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialDefense", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 3: // White Aura - XP
                    DropItem(DropTID, "DRPGSmallTokenDropper", 1, 96 + DropMod);
                    DropItem(DropTID, "DRPGVialPotency", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 4: // Pink Aura - Vitality
                    DropItem(DropTID, "DRPGHealthDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialVitality", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 5: // Blue Aura - Energy
                    DropItem(DropTID, "DRPGEPCapsule", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGBigEPCapsule", 1, 64 + DropMod);
                    DropItem(DropTID, "DRPGVialEnergy", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 6: // Purple Aura - Regeneration
                    DropItem(DropTID, "DRPGRegenSphere", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialRegeneration", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 7: // Orange Aura - Agility
                    DropItem(DropTID, "DRPGTimeSphere", 1, 64 + (DropMod / 2));
                    DropItem(DropTID, "DRPGVialAgility", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 8: // Dark Blue Aura - Capacity
                    DropItem(DropTID, "DRPGAmmoDropper", 1, 128 + DropMod);
                    if (GetCVar("drpg_ext_doomrl"))
                        DropItem(DropTID, "RLModpackSpawner", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialCapacity", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 9: // Yellow Aura - Luck
                    DropItem(DropTID, "DRPGVialLuck", 1, 255);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 32);
                    break;
                case 10: // Black Aura - Miniboss
                    DropItem(DropTID, "DRPGWeaponDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGUsedArmorDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGSmallTokenDropper", 1, 96 + DropMod);
                    DropItem(DropTID, "DRPGHealthDropper", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGEPCapsule", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGBigEPCapsule", 1, 64 + DropMod);
                    DropItem(DropTID, "DRPGRegenSphere", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGTimeSphere", 1, 64 + (DropMod / 2));
                    DropItem(DropTID, "DRPGAmmoDropper", 1, 128 + DropMod);
                    if (GetCVar("drpg_ext_doomrl"))
                        DropItem(DropTID, "RLModpackSpawner", 1, 128 + DropMod);
                    DropItem(DropTID, "DRPGVialStrength", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialDefense", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialAgility", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialVitality", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialEnergy", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialRegeneration", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialCapacity", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialLuck", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialPurifier", 1, 255);
                    DropItem(DropTID, "DRPGVialPotency", 1, 192 + DropMod);
                    DropItem(DropTID, "DRPGVialDropperRare", 1, 255);
                    break;
            };
        
            // Luck-based Drops
            if (Killer > -1)
            {
                    if (RandomFixed(0.0, 100.0) < Players(Killer).CreditChance) DropItem(DropTID, "DRPGMoneyDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).HealthChance) DropItem(DropTID, "DRPGHealthDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).EPChance)     DropItem(DropTID, "DRPGEPCapsule", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).ArmorChance)  DropItem(DropTID, "DRPGArmorDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).PowerupChance)DropItem(DropTID, "DRPGPowerupDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).WeaponChance) DropItem(DropTID, "DRPGWeaponDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).WeaponChance) DropItem(DropTID, "RLModpackSpawner", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).TokenChance)  DropItem(DropTID, "DRPGSmallTokenDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).AugChance)    DropItem(DropTID, "DRPGAugDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).ShieldChance) DropItem(DropTID, "DRPGShieldDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).StimChance)   DropItem(DropTID, "DRPGStimDropper", 1, 255);
                    if (RandomFixed(0.0, 100.0) < Players(Killer).StimChance)   DropItem(DropTID, "DRPGVialDropper", 1, 255);
            };
        };
            
        // Boss Drops
        if (MonsterFlags & MF_BOSS)
            for (int i = 1; i <= (DropAmount / 2) + 1; i++)
            {
                DropItem(DropTID, "DRPGCredits1000", 1, 255);
                DropItem(DropTID, "DRPGSoulsphereRandomizer", 1, 255);
                DropItem(DropTID, "DRPGContinue", 1, 128 + DropMod);
                DropItem(DropTID, "DRPGTokenDropper", 1, 255 + DropMod);
                DropItem(DropTID, "DRPGAugDropper", 1, 128 + DropMod);
                DropItem(DropTID, "DRPGShieldDropper", 1, 128 + DropMod);
                DropItem(DropTID, "DRPGUACCard", 1, DropMod / 8);
                if (GetCVar("drpg_ext_doomrl"))
                    DropItem(DropTID, "DRPGImmunityCrystal", 1, DropMod / 8);
            };
    };
    
    // Drop Credits
    if (CheckInventory("DRPGCredits") > 0)
        DropMoney(0, CheckInventory("DRPGCredits"));
    
    // Add XP/Rank
    if (!(MonsterFlags & MF_NOXP) && !GetActorProperty(0, APROP_Friendly))
    {
        // XP modifications based on level and skill
        if (GetCVar("drpg_xp_monsterlevel")) XPAmount *= 1.0 + ((fixed)MonsterLevel / 100.0);
        if (GetCVar("drpg_xp_skilllevel")) XPAmount *= 1.0 - (0.15 * (fixed)GameSkill());
        
        for (int i = 0; i < MAX_PLAYERS; i++)
        {
            if (MonsterFlags & MF_NEGATIVEXP)
                AddXP(i, (int)XPAmount, -RankAmount, false)
            else
            {
                if (Killer == i)
                    AddXP(i, (int)XPAmount, RankAmount, false)
                else
                    AddXP(i, (int)XPAmount, RankAmount, true);
            };
        };
    };
    
    // Add a kill to the pay kills
    if (!(MonsterFlags & MF_NOPAYKILL) && Killer > -1)
    {
        if (Players(Killer).Shield.Accessory == SHIELD_ACC_REKT50 && Players(Killer).Shield.Active)
            Players(Killer).PayKills += 3
        else
            Players(Killer).PayKills++;
    };
    
    // Mission Handling
    if (Killer > -1 && Players(Killer).Mission.Active && !InBase && !GetActorProperty(0, APROP_Friendly))
    {
        // Collection Mission
        if (Players(Killer).Mission.Type == MT_COLLECT)
            if (Random(1, 1 << ((Players(Killer).Mission.Difficulty + 1))) == 1)
                DropItem(0, Players(Killer).Mission.Item->Actor, 1, 255);
        
        // Kill Mission
        if (Players(Killer).Mission.Type == MT_KILL)
        {
            str Actor = GetActorClass(0);
            str NeededActor = StrParam("DRPG%s\n", Players(Killer).Mission.Monster->Actor);
            int Match = StrCmp(Actor, NeededActor);
            
            // DoomRL Compatibility
            if (GetCVar("drpg_ext_doomrl"))
            {
                NeededActor = StrParam("%sRPG\n", Players(Killer).Mission.Monster->Actor);
                
                // Special handling for Humans
                for (int i = 0; i < MAX_HUMANS; i++)
                    if (StrCmp(StrLeft(Actor, StrLen(Actor) - 3), MonsterHumanData[i][0]) == 0)
                    {
                        Actor = StrParam("%sRPG\n", MonsterHumanData[i][1]);
                        break;
                    };
                
                // Re-check the match
                Match = StrCmp(Actor, NeededActor);
            }
            // Extras Compatibility
            else if (GetCVar("drpg_ext_extras"))
            {
                NeededActor = StrParam("DRPG%sExtras\n", Players(Killer).Mission.Monster->Actor);
                
                Match = StrCmp(Actor, NeededActor);
            };
            
            if (GetUserCVar(Killer, "drpg_debug"))
                Log("\cdDEBUG: \caKilled %s, need %s, match was %d\n", Actor, NeededActor, Match);
            
            if (Match == 0)
                Players(Killer).Mission.Current++;
        };
        
        // Kill Reinforcements Mission
        if (Players(Killer).Mission.Type == MT_REINFORCEMENTS)
            if (CheckInventory("DRPGMonsterReinforcement"))
                Players(Killer).Mission.Current++;
    };
    
    // SHOTGUNS!
    if (GetCVar("drpg_shotguns"))
        DropItem(0, "DRPGShotSpawn", 1, 255);
};

acscript void MonsterTransport(int Difficulty, int Time, int Radius)
{
    while (true)
    {
        Delay((Time > 0 ? (35 * Time) : -Time));

        bool Complete = true;
        bool DRLA = GetCVar("drpg_ext_doomrl");
        fixed X = GetActorX(0);
        fixed Y = GetActorY(0);
        fixed Z = GetActorZ(0);
        int Min = Difficulty;
        int Max = Difficulty + ((DRLA ? MAX_MONSTERS_DRLA : MAX_MONSTERS) - MAX_DIFFICULTIES);
        fixed SpawnX;
        fixed SpawnY;
        int TID;
        bool Success;
        int SpawnTries;
        int CurrentRadius;
        MonsterInfo Monster;
        
        // Check to see if the mission is complete
        for (int i = 0; i < MAX_PLAYERS; i++)
            if (Players(i).Mission.Active && Players(i).Mission.Type == MT_REINFORCEMENTS)
                Complete = false;
        
        // If the mission is complete, terminate
        if (Complete) return;
        
        // Cap the maximum
        // TODO: DRLA
        if (Max > MAX_MONSTERS - 3)
            Max = MAX_MONSTERS - 3;
        
        for (int i = Min; i < Max; i++)
        {
            if (GetCVar("drpg_ext_doomrl"))
                Monster = MonsterDataDRLA[i]
            else
                Monster = MonsterData[i];
            
            if (!Random(0, 9 - Difficulty))
            {
                TID = UniqueTID();
                Success = false;
                SpawnTries = 0;
                CurrentRadius = Radius;
                
                while (!Success && SpawnTries < 3)
                {
                    SpawnX = RandomFixed(-(fixed)CurrentRadius, (fixed)CurrentRadius);
                    SpawnY = RandomFixed(-(fixed)CurrentRadius, (fixed)CurrentRadius);
                    
                    // Get the floor Z position at this spot
                    SpawnForced("MapSpot", X + SpawnX, Y + SpawnY, Z, TID, 0);
                    Z = GetActorFloorZ(TID);
                    Thing_Remove(TID);
                    
                    Success = Spawn(Monster.Actor, X + SpawnX, Y + SpawnY, Z, TID, 0);
                    if (Success)
                        Success = CheckSight(0, TID, 0);
                    
                    if (!Success)
                    {
                        // Try again, closer to the player each time, up to 3 times, before giving up.
                        Thing_Remove(TID);
                        CurrentRadius /= 2;
                    };
                    
                    SpawnTries++;
                };
                if (Success)
                {
                    SetActorState(TID, "See", false);
                    GiveActorInventory(TID, "DRPGMonsterReinforcement", 1);
                    Thing_ChangeTID(TID, 0); // Get rid of the ID
                    Spawn("TeleportFog", X + SpawnX, Y + SpawnY, Z, 0, 0);
                    if (GetCVar("drpg_debug"))
                        Log("\cdDEBUG: \c-Reinforcements Spawning Index %d (\ca%s\c-)\n", i, Monster.Name);
                    break;
                };
            };
        };
    };
};

script int WhoKilledMe()
{
    SetActivatorToTarget(0);
    return PlayerNumber();
};

function int CalculateMonsterThreatLevel(int TID)
{
    int Aura = CheckInventoryTID(TID, "DRPGMonsterAura");
    int Strength = CheckInventoryTID(TID, "DRPGMonsterStrength");
    int Defense = CheckInventoryTID(TID, "DRPGMonsterDefense");
    int Vitality = CheckInventoryTID(TID, "DRPGMonsterVitality");
    int Energy = CheckInventoryTID(TID, "DRPGMonsterEnergy");
    int Regeneration = CheckInventoryTID(TID, "DRPGMonsterRegeneration");
    int Agility = CheckInventoryTID(TID, "DRPGMonsterAgility");
    int Capacity = CheckInventoryTID(TID, "DRPGMonsterCapacity");
    int Luck = CheckInventoryTID(TID, "DRPGMonsterLuck");
    int Threat;
    
    // Stats
    Threat += Strength;
    Threat += Defense;
    Threat += Vitality;
    Threat += Energy;
    Threat += Regeneration;
    Threat += Agility;
    Threat += Capacity;
    Threat += Luck;
    Threat /= 800;
    
    // Auras
    if (Aura > 0 && Aura < 9) // Normal Auras
        Threat++
    else if (Aura == 10) // Black Aura
        Threat += 2;
    
    // Bosses
    if (CheckFlag(TID, "BOSS"))
        Threat++;
    
    // DRLA
    if (GetCVar("drpg_ext_doomrl"))
    {
        str Actor = GetActorClass(TID);
        
        for (int i = 0; i < MAX_MONSTERS_DRLA; i++)
        {
            MonsterInfoPtr MonsterIterPtr = &MonsterDataDRLA[i];
            
            if (StartsWith(Actor, MonsterIterPtr->Actor))
            {
                Threat += MonsterIterPtr->ThreatLevel;
                break;
            };
        };
    };
    
    // Cap
    if (Threat > 10)
        Threat = 10;
    
    return Threat;
};

function int CalculateMonsterMaxHealth(int TID)
{
    int Vitality = CheckInventoryTID(TID, "DRPGMonsterVitality");
    int HealthMax = GetActorProperty(TID, APROP_SpawnHealth);
    long int Health = HealthMax;
    long int HealthAddition = HealthMax * 9;
    
    HealthAddition *= Vitality;
    HealthAddition /= 1000;
    
    Health += HealthAddition;
    
    return Health;
};

function fixed CalculateMonsterSpeed(int TID)
{
    int Agility = CheckInventoryTID(TID, "DRPGMonsterAgility");
    int BaseSpeed = CheckInventoryTID(TID, "DRPGMonsterSpeed");
    fixed Speed = (fixed)BaseSpeed;
    
    Speed *= 1.0 + ((fixed)Agility / 1000.0);
    
    return Speed;
};
