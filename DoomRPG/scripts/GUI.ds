#include "Globals.dh"
#include "GUI.dh"
#include "Utils.dh"

function void BuildTestWindow()
{
	GUIWindow *Window = GUICreateWindow();
	GUILabel *Label = GUICreateLabel(Window);
	
	Window->Title = "Super Awesome Test Window";
	Window->X = 40;
	Window->Y = 40;
	Window->Width = 300;
	Window->Height = 300;
	Window->Texture = "GUIBack";
	
	Label->Text = "Mega Label";
	Label->X = 24;
	Label->Y = 24;
	Label->Color = CR_GOLD;
	
	Player.Window = Window;
};

acscript ToggleGUI()
{
	Player.GUIOpen = !Player.GUIOpen;
	BuildTestWindow();
	
	if (Player.GUIOpen)
		SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN)
	else
		SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
};

script CheckCursor()
{
	Start:
	
	while (Player.GUIOpen)
	{
		int Width = GetCVar("drpg_menu_width");
		int Height = GetCVar("drpg_menu_height");
		
		// Set the Resolution/HUD Size
		SetHudSize(Width, Height, true);
		
		// Get X/Y input
		Player.Mouse.XAdd = GetPlayerInput(PlayerNumber(), INPUT_YAW);
		Player.Mouse.YAdd = GetPlayerInput(PlayerNumber(), INPUT_PITCH);
		
		// Get Buttons
		Player.Mouse.Buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
		
		// Add X/Y input to current position
		Player.Mouse.X -= Player.Mouse.XAdd / 32;
		Player.Mouse.Y -= Player.Mouse.YAdd / 24;
		
		// Check resolution boundaries
		if (Player.Mouse.X < 0)
			Player.Mouse.X = 0;
		if (Player.Mouse.Y < 0)
			Player.Mouse.Y = 0;
		if (Player.Mouse.X > Width)
			Player.Mouse.X = Width;
		if (Player.Mouse.Y > Height)
			Player.Mouse.Y = Height;
		
		// Draw Cursor
		PrintSprite("Cursor", 0, Player.Mouse.X, Player.Mouse.Y, 0.05);
		
		// Mouse Input
		if (Player.Mouse.Buttons & BT_ATTACK)
			Player.Mouse.LeftButton = true
		else
			Player.Mouse.LeftButton = false;
		if (Player.Mouse.Buttons & BT_ALTATTACK)
			Player.Mouse.RightButton = true
		else
			Player.Mouse.RightButton = false;
		
		Delay(1);
	};
	
	Delay(1);
	goto Start;
};

script CheckGUI()
{
	// Center Mouse
	Player.Mouse.X = GetCVar("drpg_menu_width") / 2;
	Player.Mouse.Y = GetCVar("drpg_menu_height") / 2;
	
	while (true)
	{
		if (Player.GUIOpen)
		{
			// Handle Drawing and Input for the Window
			HandleWindow(Player.Window);
			
			// TODO: These should probably eventually be moved into their own Handle*() functions
			
			// Labels
			for (int i = 0; Player.Window->Labels[i] != nullptr; i++)
			{
				str Text = Player.Window->Labels[i]->Text;
				int X = Player.Window->X + Player.Window->Labels[i]->X;
				int Y + Player.Window->Y + Player.Window->Labels[i]->Y;
				int Color = Player.Window->Labels[i]->Color;
				
				HudMessage("%s\n", Text, HUDMSG_PLAIN, 0, Color, X + 0.1, Y, 0.05);
			};
		};
		
		Delay(1);
	};
};

function void HandleWindow(GUIWindow *Window)
{
	// Set the Resolution/HUD Size
	int Width = GetCVar("drpg_menu_width");
	int Height = GetCVar("drpg_menu_height");
	SetHudSize(Width, Height, true);
	
	// Title
	SetFont("SMALLFONT");
	HudMessage("%s\n", Window->Title, HUDMSG_PLAIN, 0, ((InTitleBar(Window) || Window->Dragging) ? CR_GREEN : CR_WHITE), Window->X + 16.1, Window->Y + 8.0, 0.05);
	
	// Window Background
	SetHudClipRect(Window->X, Window->Y, Window->Width, Window->Height);
	PrintSprite(Window->Texture, 0, Window->X, Window->Y, 0.05);
	SetHudClipRect(0, 0, 0, 0);
	
	// Drag Checking
	if (InTitleBar(Window) && Player.Mouse.LeftButton)
		Window->Dragging = true;
	if (Window->Dragging && !Player.Mouse.LeftButton)
		Window->Dragging = false;
	
	// Dragging
	if (Window->Dragging)
	{
		Window->X -= Player.Mouse.XAdd / 32;
		Window->Y -= Player.Mouse.YAdd / 24;
		
		// Bounding
		if (Window->X < 0)
			Window->X = 0;
		if (Window->X > Width - Window->Width)
			Window->X = Width - Window->Width;
		if (Window->Y < 0)
			Window->Y = 0;
		if (Window->Y > Height - Window->Height)
			Window->Y = Height - Window->Height;
	};
};

function GUIWindow *GUICreateWindow()
{
	GUIWindow *Window = malloc(sizeof(GUIWindow));
	return Window;
};

function GUILabel *GUICreateLabel(GUIWindow *Window)
{
	GUILabel *Label = malloc(sizeof(GUILabel));
	
	for (int i = 0; i < MAX_CONTROLS; i++)
		if (Window->Labels[i] == nullptr)
		{
			Window->Labels[i] = Label;
			break;
		};
	
	return Label;
};

function GUIIcon *GUICreateIcon(GUIWindow *Window)
{
	GUIIcon *Icon = malloc(sizeof(GUIIcon));
	
	for (int i = 0; i < MAX_CONTROLS; i++)
		if (Window->Icons[i] == nullptr)
		{
			Window->Icons[i] = Icon;
			break;
		};
	
	return Icon;
};

function GUIButton *GUICreateButton(GUIWindow *Window)
{
	GUIButton *Button = malloc(sizeof(GUIButton));
	
	for (int i = 0; i < MAX_CONTROLS; i++)
		if (Window->Buttons[i] == nullptr)
		{
			Window->Buttons[i] = Button;
			break;
		};
	
	return Button;
};

function GUIBar *GUICreateBar(GUIWindow *Window)
{
	GUIBar *Bar = malloc(sizeof(GUIBar));
	
	for (int i = 0; i < MAX_CONTROLS; i++)
		if (Window->Bars[i] == nullptr)
		{
			Window->Bars[i] = Bar;
			break;
		};
	
	return Bar;
};

function GUITooltip *GUICreateTooltip()
{
	GUITooltip *Tooltip = malloc(sizeof(GUITooltip));
	return Tooltip;
};

function bool InTitleBar(GUIWindow *Window)
{
	if (Player.Mouse.X > Window->X && Player.Mouse.X < Window->X + Window->Width &&
		Player.Mouse.Y > Window->Y + 10 && Player.Mouse.Y < Window->Y + 20)
		return true
	else
		return false;
};
