#library "Shop"

#include "zcommon.acs"

#import "Stats.acs"
#import "Skills.acs"
#import "Augs.acs"
#import "Menu.acs"
#import "Utils.acs"

#libdefine SHOP_CATEGORIES			16

str ShopNames[SHOP_CATEGORIES] =
{
	"Weapons",
	"Ammo",
	"Health",
	"Armor",
	"Powerups",
	"Tokens",
	"Shield",
	"Augmentations",
	"Brutal Doom",
};

// Shop Array
int Shop[SHOP_CATEGORIES][20][2] =
{
    // Weapon
	{
		{ "Pistol", "Pistol" },
		{ "Shotgun", "Shotgun" },
		{ "SuperShotgun", "Super Shotgun" },
		{ "ChainGun", "Chaingun" },
		{ "RocketLauncher", "Rocket Launcher" },
		{ "PlasmaRifle", "Plasma Rifle" },
		{ "BFG9000", "BFG 9000" },
		{ "Chainsaw", "Chainsaw" }
//		{ "GrenadeFrag", "Frag Grenade" }
	},
    
    // Ammo
	{
		{ "Clip", "Clip" },
		{ "ClipBox", "Box of Bullets" },
		{ "Shell", "Shells" },
		{ "ShellBox", "Box of Shells" },
		{ "RocketAmmo", "Rockets" },
		{ "RocketBox", "Box of Rockets" },
		{ "Cell", "Cell" },
		{ "CellPack", "Cell Charge Pack" },
		{ "Backpack", "Backpack" },
		{ "BigBackpack", "Big Backpack" }
	},
    
    // Health
	{
		{ "HealthBonus", "Health Bonus" },
		{ "Stimpack", "Stimpack" },
		{ "SmallMedikit", "Small Medikit" },
		{ "Medikit", "Medikit" },
		{ "LargeMedikit", "Large Medikit" },
		{ "XLMedikit", "Extra-Large Medikit" },
		{ "MedPack", "Med Pack" },
		{ "SurgeryKit", "Surgery Kit" },
		{ "Soulsphere", "Soulsphere" },
		{ "AtomicHealth", "Atomic Health" },
		{ "Continue", "Continue" },
		{ "EPCapsule", "EP Capsule" },
		{ "BigEPCapsule", "Big EP Capsule" }
	},
    
    // Armor
	{
		{ "ArmorBonus", "Armor Bonus" },
		{ "GreenArmor", "Green Armor" },
		{ "UsedGreenArmor", "Green Armor (Used)" },
		{ "BlueArmor", "Blue Armor" },
		{ "UsedBlueArmor", "Blue Armor (Used)" },
		{ "YellowArmor", "Yellow Armor" },
		{ "UsedYellowArmor", "Yellow Armor (Used)" },
		{ "RedArmor", "Red Armor" },
		{ "UsedRedArmor", "Red Armor (Used)" },
		{ "WhiteArmor", "White Armor" },
		{ "UsedWhiteArmor", "White Armor (Used)" }
	},
	
	// Powerups
	{
		{ "InvulnerabilitySphere", "Invulnerability Sphere" },
		{ "InvulnerabilityCharge", "Invulnerability Charge" },
		{ "BlurSphere", "Invisibility Sphere" },
		{ "InvisibilityCharge", "Invisibility Charge" },
		{ "TimeSphere", "Time Sphere" },
		{ "RegenSphere", "Regeneration Sphere" },
		{ "RadSuit", "Radiation Suit" },
		{ "Infrared", "IR Goggles" },
		{ "Berserk", "Berserk Pack" },
		{ "AllMap", "Computer Area Map" },
		{ "Megasphere", "Megasphere" },
		{ "StrengthRune", "Strength Rune" },
		{ "DrainRune", "Drain Rune" },
		{ "EndlessRune", "Endless Rune" },
		{ "ResistanceRune", "Resistance Rune" },
		{ "RegenerationRune", "Regeneration Rune" },
		{ "FrightRune", "Fright Rune" },
		{ "GhostRune", "Ghost Rune" },
		{ "HighJumpRune", "High Jump Rune" },
		{ "HasteRune", "Haste Rune" }
	},
	
	// Tokens
	{
		{ "StatToken", "Stat Token" },
		{ "BigStatToken", "Big Stat Token" },
		{ "StatCapToken", "Stat Cap Token" },
		{ "BigStatCapToken", "Big Stat Cap Token" },
		{ "SkillToken", "Skill Token" },
		{ "BigSkillToken", "Big Skill Token" },
		{ "MegaToken", "Mega Token" },
		{ "BigMegaToken", "Big Mega Token" }
	},
	
	// Shield
	{
		{ "ShieldBody1", "Shield Body - Pluggy" },
		{ "ShieldBattery1", "Shield Battery - Double-A" },
		{ "ShieldCapacitor1", "Shield Capacitor - Standard" },
		{ "ShieldCapacitor2", "Shield Capacitor - Cooled" },
		{ "ShieldCapacitor3", "Shield Capacitor - Energized" },
		{ "ShieldCapacitor4", "Shield Capacitor - Infused" },
		{ "ShieldCapacitor5", "Shield Capacitor - Fiery" },
		{ "ShieldAccessory1", "Shield Accessory - Amplified" }
	},
	
	// Augmentations
	{
		{ "StrengthAug", "Muscle Replacer" },
		{ "DefenseAug", "Dermal Armor" },
		{ "VitalityAug", "Pain Inhibitor" },
		{ "EnergyAug", "Psi Projector" },
		{ "RegenerationAug", "Adrenaline Booster" },
		{ "AgilityAug", "Wired Reflexes" },
		{ "CapacityAug", "Weight Distributor" },
		{ "LuckAug", "Precognition Unit" },
		{ "BlackMarketAug", "Black Market Module" },
		{ "AugSlotUpgrade", "Augmentation Slot Upgrade" }
	},
	
	// Brutal Doom
	{
		{ "BrutalPistol", "Pistol" },
		{ "Grenade_Launcher", "Grenade Launcher" },
		{ "Rail_Gun", "Railgun" },
		{ "BFG10000", "BFG10000" },
		{ "HellishMissileLauncher", "Hellish Missile Launcher" },
		{ "FlameCannon", "Flame Cannon" },
		{ "SecretWeapon_MP40", "MP40" }
	},
};

int ShopCosts[SHOP_CATEGORIES][20] = {
    // Weapon
	{
		20, 75, 200, 300, 750, 1500, 3000, 300, 100
	},
    // Ammo
	{
		10, 50, 25, 125, 50, 250, 200, 1000, 285, 6325
	},
    // Health
	{
		5, 10, 50, 100, 250, 500, 750, 1000, 2000, 4000, 50000, 500, 1000
	},
    // Armor
	{
		5, 100, 50, 500, 250, 1000, 500, 5000, 2500, 10000, 5000
	},
	// Artifacts
	{
		1000, 2000, 500, 1000, 200, 1000, 250, 500, 1000, 1000, 5000, 	// Powerups
		1000, 1000, 1000, 1000, 1000, 1000, 1000, 1000, 1000			// Runes
	},
	// Tokens
	{
		25000, 125000, 25000, 125000, 25000, 125000, 375000, 750000
	},
	// Shields
	{
		1000, 							// Bodies
		1000, 							// Batteries
		1000, 2000, 4000, 8000, 16000,	// Capacitors
		10000							// Accessories
	},
	// Augmentations
	{
		25000, 25000, 25000, 25000, 25000, 25000, 25000, 25000, 250000,	// Augs
		500000
	},
	// Brutal Doom
	{
		100, 1000, 2500, 10000, 5000, 5000, 500
	},
};

int ShopMax[SHOP_CATEGORIES] =
{
	8, 10, 13, 11, 20, 8, 8, 10, 7
};

bool InShop;
int ShopIndex;
int ShopOffset;
int ShopPage;
int SellTimer;
int ShopSpotID = 1000;

// Open the Shop
script "OpenShop" (void)
{
	// If you're dead, terminate
	if (GetActorProperty(0, APROP_HEALTH) <= 0) terminate;
	
	InMenu = false;
	
	if (InShop)
	{
		// Sanity Check to prevent the shop closing itself after buying an item in the Outpost Shop
		if (InBase) terminate;
		
		ActivatorSound("menu/leave", 127);
		SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN);
		InShop = False;
	}
	else
	{
		// Don't let you Shop Anywhere if it's disabled
		if (!InBase && !GetCVAR("drpg_shoptype")) terminate;
		
		ActivatorSound("menu/shop", 127);
		SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN);
		InShop = true;
	}
}

// Sell items in bulk
script "SellAll" (int SellShopPage, int Silent)
{
	int Total;
	int ItemMax = 1000;
	
	// Set font
	SetFont("BIGFONT");
	
	// Invalid ShopPages
	if (SellShopPage < 1)
	{
		print(s:"\cgInvalid Shop ShopPage");
		ActivatorSound("menu/error", 127);
		terminate;
	}
	
	// Sell Items
	for (int i = 0; i < ShopMax[SellShopPage]; i++)
		for (int j = 0; j < ItemMax; j++)
			if (CheckInventory(Shop[SellShopPage][i][0]) > 0)
			{
					// Oh god...
					if (Shop[SellShopPage][i][0] == "HealthBonus" || Shop[SellShopPage][i][0] == "ArmorBonus") continue;
					
					TakeInventory(Shop[SellShopPage][i][0], 1);
					GiveInventory("Credits", ShopCosts[SellShopPage][i] / 2);
					Total += ShopCosts[SellShopPage][i] / 2;
			}
			else continue;
	
	// Augmentation Handling
	if (SellShopPage == 7)
		for (int k = 0; k < 8; k++)
		{
			Augs[k] = false;
			AugSlotsUsed = 0;
		}

	// Display Message
	if (!Silent)
	{
		HudMessage(s:"Sold all ", s:ShopNames[SellShopPage], s:" items for ", d:Total, s:" Credits";
				   HUDMSG_FADEOUT, 0, CR_GOLD, 0.5, 0.5, 5.0, 5.0);
		ActivatorSound("credits/payout", 127);
	}
}

function void ShopLoop(void)
{
	int Categories = 8; // 8 vanilla Categories, 8 reserved for compatibility
	int Buttons = GetPlayerInput(0, INPUT_BUTTONS);
	int OldButtons = GetPlayerInput(0, INPUT_OLDBUTTONS);
	int Discount = FixedMul(RankLevel, 2.1);
	int Cost = ShopCosts[ShopPage][ShopIndex] - ShopCosts[ShopPage][ShopIndex] * Discount / 100;
	int SellPrice = ShopCosts[ShopPage][ShopIndex] / 2;
	
	// Ammo is sold for 1/4th of normal sell price
	if (ShopPage == 1) SellPrice /= 4;
	
	// Compatibility handling - Brutal Doom
	if (GetCVAR("drpg_ext_brutal"))
	{
		Categories = 9;
		Shop[0][0][1] = "Assault Rifle";
	}

	if (InShop)
    {
		// Freeze the game if the option is active
		if (GetCVAR("drpg_menufreeze"))
			GiveInventory("MenuFreezer", 1);
		
		// Set the HUD Size
		SetHUDSize(GetCVAR("drpg_menu_width"), GetCVAR("drpg_menu_height"), true);

		// Draw the background
		if (GetCVAR("drpg_menudim"))
			FadeRange(0, 0, 0, 0.5, 0, 0, 0, 0.0, 0.25);
        
		// Draw Shop
        SetFont("BIGFONT");
        HudMessage(s:"Shop - ", s:ShopNames[ShopPage],
				   s:" (", d:ShopPage + 1, s:"/", d:Categories, s:")";
                   HUDMSG_PLAIN, 3000, CR_GREEN,
                   24.1, 24.0, 0.1);
        
        // Draw Price
        HudMessage(s:"\cj", d:Cost, s:" C \ci(", d:SellPrice, s:" C) \ck(Discount: ", d:Discount, s:"%)";
                   HUDMSG_PLAIN, 3021, CR_GOLD,
                   24.1, 38.0, 0.1);
        
        /* Draw Help
        SetFont("SMALLFONT");
		HudMessage(s:"Up/Down to move\nLeft/Right to change ShopPages\nUse to buy\nRun to sell\nMenu to exit";
				   HUDMSG_PLAIN, 3022, CR_RED,
				   0.1, 240.1, 0.1); */
        
		// Draw Item List
        for (int i = 0; i < ShopMax[ShopPage]; i++)
        {
			SetFont("SMALLFONT");
            if (i == ShopIndex)
				if (CheckInventory(Shop[ShopPage][i][0]) > 0)
					HudMessage(s:Shop[ShopPage][i][1], s:" [", d:CheckInventory(Shop[ShopPage][i][0]), s:"]";
							   HUDMSG_PLAIN, 3001 + i, CR_GREEN,
							   24.1, 50.0 + 8.0 * i, 0.1);
				else
					HudMessage(s:Shop[ShopPage][i][1];
							   HUDMSG_PLAIN, 3001 + i, CR_GREEN,
							   24.1, 50.0 + 8.0 * i, 0.1);
            else
                if (CheckInventory("Credits") >= ShopCosts[ShopPage][i] - ShopCosts[ShopPage][i] * Discount / 100)
					if (CheckInventory(Shop[ShopPage][i][0]) > 0)
						HudMessage(s:Shop[ShopPage][i][1], s:" [", d:CheckInventory(Shop[ShopPage][i][0]), s:"]";
								   HUDMSG_PLAIN, 3001 + i, CR_WHITE,
								   24.1, 50.0 + 8.0 * i, 0.1);
					 else
						HudMessage(s:Shop[ShopPage][i][1];
								   HUDMSG_PLAIN, 3001 + i, CR_WHITE,
								   24.1, 50.0 + 8.0 * i, 0.1);
                else
					if (CheckInventory(Shop[ShopPage][i][0]) > 0)
						HudMessage(s:Shop[ShopPage][i][1], s:" [", d:CheckInventory(Shop[ShopPage][i][0]), s:"]";
								   HUDMSG_PLAIN, 3001 + i, CR_RED,
								   24.1, 50.0 + 8.0 * i, 0.1);
					else
						HudMessage(s:Shop[ShopPage][i][1];
								   HUDMSG_PLAIN, 3001 + i, CR_RED,
								   24.1, 50.0 + 8.0 * i, 0.1);
        }
        
        // Check Input
        if (Buttons == BT_FORWARD && OldButtons != BT_FORWARD && ShopIndex >= 0)
        {
			if (ShopIndex <= 0) ShopIndex = ShopMax[ShopPage];
            ActivatorSound("menu/move", 127);
            ShopIndex--;
        }
        if (Buttons == BT_BACK && OldButtons != BT_BACK && ShopIndex <= ShopMax[ShopPage])
        {
			if (ShopIndex >= ShopMax[ShopPage] - 1) ShopIndex = -1;
            ActivatorSound("menu/move", 127);
            ShopIndex++;
        }
        if ((Buttons == BT_LEFT && OldButtons != BT_LEFT && ShopPage >= 0) ||
			(Buttons == BT_MOVELEFT && OldButtons != BT_MOVELEFT && ShopPage >= 0))
        {
			if (ShopPage <= 0) ShopPage = Categories;
            ActivatorSound("menu/move", 127);
			ShopIndex = 0;
            ShopPage--;
        }
        if ((Buttons == BT_RIGHT && OldButtons != BT_RIGHT && ShopPage < Categories) ||
			(Buttons == BT_MOVERIGHT && OldButtons != BT_MOVERIGHT && ShopPage < Categories))
        {
			if (ShopPage >= Categories - 1) ShopPage = -1;
            ActivatorSound("menu/move", 127);
			ShopIndex = 0;
            ShopPage++;
        }
        if (Buttons == BT_USE && OldButtons != BT_USE)
		{
			if (CheckInventory("Credits") >= Cost)
				BuyItem(Shop[ShopPage][ShopIndex][0], Cost);
			else
				ActivatorSound("menu/error", 127);
		}
		if (Buttons == BT_SPEED && OldButtons != BT_SPEED)
			if (CheckInventory(Shop[ShopPage][ShopIndex][0]) > 0)
				SellItem(Shop[ShopPage][ShopIndex][0], false);
			else
				ActivatorSound("menu/error", 127);
		if (Buttons & BT_SPEED)
		{
			SellTimer++;
			
			if (SellTimer > 35 * 3 && CheckInventory(Shop[ShopPage][ShopIndex][0]) > 0)
			{
				SellItem(Shop[ShopPage][ShopIndex][0], true);
				SellTimer = 0;
			}
		}
		else
			SellTimer = 0;
    }
}

function void BuyItem(str Item, int Cost)
{
	// If it's a Token, use alternate handling due to bullshit
	if (Item == "StatToken") 	Item = "SmallStatToken";
	if (Item == "StatCapToken") Item = "SmallStatCapToken";
	if (Item == "SkillToken")	Item = "SmallSkillToken";
	
	// Buy Type
	if (GetCVAR("drpg_shopbuytype"))
	{
		int Amount = 1;
		
		// If it's ammo, we need to get the real amounts to give if you're using the Give buy mode
		if (Item == "Clip")			Amount = 10;
		if (Item == "ClipBox")		Amount = 50;
		if (Item == "Shell")		Amount = 4;
		if (Item == "ShellBox")		Amount = 20;
		if (Item == "RocketAmmo")	Amount = 1;
		if (Item == "RocketBox")	Amount = 5;
		if (Item == "Cell")			Amount = 20;
		if (Item == "CellPack")		Amount = 100;
		
		// If it's ammo and it's the large version, just change it down to the base type
		if (Item == "ClipBox")		Item = "Clip";
		if (Item == "ShellBox")		Item = "Shell";
		if (Item == "RocketBox")	Item = "RocketAmmo";
		if (Item == "CellPack")		Item = "Cell";
		
		// If it's an instant use health item, give it the proper amount
		if (Item == "Soulsphere")	Amount = 100;
		if (Item == "AtomicHealth")	Amount = 200;
		
		// Now apply difficulty-based multipliers to the amount
		if (Item == "Clip" || Item == "Shell" || Item == "RocketAmmo" || Item == "Cell")
			switch (GameSkill())
			{
				case 1:	FixedMul(Amount, 4.0); break;
				case 2:	FixedMul(Amount, 2.0); break;
				case 7: FixedMul(Amount, 2.0); break;
			}
		
		if (CheckInventory(Item) == 0 || CheckInventory(Item) < CheckInventoryMax(Item))
			GiveInventory(Item, Amount);
		else
		{
			ActivatorSound("menu/error", 127);
			return;
		}
	}
	else
	{
		if (InBase && !GetCVAR("drpg_shoptype"))
			SpawnSpotForced(Item, ShopSpotID, 0, 0);
		else
			SpawnForced(Item, GetActorX(0), GetActorY(0), GetActorZ(0), 0, 0);
	}
	
	ActivatorSound("menu/buy", 127);
	TakeInventory("Credits", Cost);
}

function void SellItem(str Item, int SellAll)
{
	int SellCost = ShopCosts[ShopPage][ShopIndex] / 2;
	int Amount = 1;
	
	if (SellAll)
		Amount = CheckInventory(Item);
	
	// If it's ammo, 1/4th the price to prevent abuse
	if (Item == "Clip" || Item == "Shell" || Item == "RocketAmmo" || Item == "Cell")
		SellCost /= 4;

	// If it's an Augmentation, unequip it too
	if (ShopPage == 7 && Augs[ShopIndex])
	{
		Augs[ShopIndex] = false;
		AugSlotsUsed--;
	}
	
	// Sell the Item
	ActivatorSound("credits/payout", 127);
	TakeInventory(Item, Amount);
	GiveInventory("Credits", SellCost * Amount);
}
