#library "Utils"

#include "zcommon.acs"

#import "RPG.acs"
#import "Stats.acs"
#import "Skills.acs"
#import "Augs.acs"
#import "Shop.acs"
#import "Menu.acs"
#import "Shield.acs"

// Level and Flags
global int 5 : LevelNum;				// Used to keep track of the level number for the transport system and monster difficulty scaling
global int 62 : InBase;					// Whether you are at the UAC Outpost or not
global int 63 : Transported;			// True if you are using the Transportation System

// For Dropped Items
int ItemID = 3000;
int MaxItems;

// Skill Level Names
str SkillLevels[7] =
{
	"Very Easy", "Easy",
	"Normal",
	"Hard", "Very Hard",
	"Nightmare", "Unfair",
};

// Utility Scripts
// --------------------------------------------------

// Luck-based Item Drops
script "DropItem" (int X, int Y, int Z)
{
	Delay(1);
	
	// Shift the floats for the Spawn function
	X = X << 16;
	Y = Y << 16;
	Z = Z << 16;
	
	// If the CVAR is set, only ever drop one type of luck-based item instead of continuing checks if one is already dropped
	if (GetCVAR("drpg_luckdrop_once"))
	{
		switch (Random(1, 21))
		{
			// Credits
			case 1: if (Random(1, 100) < MoneyChance[0])	SpawnForced("Credits1", X, Y, Z, (ItemID + ++MaxItems), 0);			break;
			case 2: if (Random(1, 100) < MoneyChance[1])	SpawnForced("Credits5", X, Y, Z, (ItemID + ++MaxItems), 0);			break;
			case 3: if (Random(1, 100) < MoneyChance[2])	SpawnForced("Credits10", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 4: if (Random(1, 100) < MoneyChance[3])	SpawnForced("Credits20", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 5: if (Random(1, 100) < MoneyChance[4])	SpawnForced("Credits50", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 6: if (Random(1, 100) < MoneyChance[5])	SpawnForced("Credits100", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 7: if (Random(1, 100) < MoneyChance[6])	SpawnForced("Credits1000", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			
			// Health
			case 8: if (Random(1, 100) < HealthChance[0])	SpawnForced("HealthDropper1", X, Y, Z, (ItemID + ++MaxItems), 0);	break;
			case 9: if (Random(1, 100) < HealthChance[1])	SpawnForced("HealthDropper2", X, Y, Z, (ItemID + ++MaxItems), 0);	break;
			case 10: if (Random(1, 100) < HealthChance[2])	SpawnForced("HealthDropper3", X, Y, Z, (ItemID + ++MaxItems), 0);	break;

			// Armor
			case 11: if (Random(1, 100) < ArmorChance[0])	SpawnForced("GreenArmor", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 12: if (Random(1, 100) < ArmorChance[1])	SpawnForced("BlueArmor", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 13: if (Random(1, 100) < ArmorChance[2])	SpawnForced("YellowArmor", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			case 14: if (Random(1, 100) < ArmorChance[3])	SpawnForced("RedArmor", X, Y, Z, (ItemID + ++MaxItems), 0);			break;
			case 15: if (Random(1, 100) < ArmorChance[4])	SpawnForced("WhiteArmor", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			
			// Powerup
			case 16: if (Random(1, 100) < PowerupChance)	SpawnForced("PowerupDropper", X, Y, Z, (ItemID + ++MaxItems), 0);	break;

			// Rune
			case 17: if (Random(1, 100) < RuneChance)		SpawnForced("RuneDropper", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			
			// Token
			case 18: if (Random(1, 100) < TokenChance)		SpawnForced("SmallTokenDropper", X, Y, Z, (ItemID + ++MaxItems), 0);break;
			
			// Augs
			case 19: if (Random(1, 100) < AugChance)		SpawnForced("AugDropper", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
			
			// Shield Part
			case 20: if (Random(1, 100) < ShieldChance)		SpawnForced("ShieldDropper", X, Y, Z, (ItemID + ++MaxItems), 0);	break;

			// EP Capsule
			case 21: if (Random(1, 100) < EPChance)			SpawnForced("EPCapsule", X, Y, Z, (ItemID + ++MaxItems), 0);		break;
		}
	}
	else
	{
		// Credits
		if (Random(1, 100) < MoneyChance[0])		{ SpawnForced("Credits1", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[1])		{ SpawnForced("Credits5", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[2])		{ SpawnForced("Credits10", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[3])		{ SpawnForced("Credits20", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[4])		{ SpawnForced("Credits50", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[5])		{ SpawnForced("Credits100", X, Y, Z, (ItemID + ++MaxItems), 0); 		SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < MoneyChance[6])		{ SpawnForced("Credits1000", X, Y, Z, (ItemID + ++MaxItems), 0); 		SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		
		// Health
		if (Random(1, 100) < HealthChance[0])		{ SpawnForced("HealthDropper1", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < HealthChance[1])		{ SpawnForced("HealthDropper2", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < HealthChance[2])		{ SpawnForced("HealthDropper3", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }

		// Armor
		if (Random(1, 100) < ArmorChance[0])		{ SpawnForced("GreenArmor", X, Y, Z, (ItemID + ++MaxItems), 0); 		SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		if (Random(1, 100) < ArmorChance[1])		{ SpawnForced("BlueArmor", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		
		// Powerup
		if (Random(1, 100) < PowerupChance)			{ SpawnForced("PowerupDropper", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }

		// Rune
		if (Random(1, 100) < RuneChance)			{ SpawnForced("RuneDropper", X, Y, Z, (ItemID + ++MaxItems), 0); 		SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		
		// Token
		if (Random(1, 100) < TokenChance)			{ SpawnForced("SmallTokenDropper", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		
		// Augs
		if (Random(1, 100) < AugChance)				{ SpawnForced("AccessoryDropper", X, Y, Z, (ItemID + ++MaxItems), 0); 	SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
		
		// Shield Part
		if (Random(1, 100) < ShieldChance)			{ SpawnForced("ShieldDropper", X, Y, Z, (ItemID + ++MaxItems), 0); 		SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }

		// EP Capsule
		if (Random(1, 100) < EPChance)				{ SpawnForced("EPCapsule", X, Y, Z, (ItemID + ++MaxItems), 0); 			SetActorVelocity(ItemID, Random(-4.0, 4.0), Random(-4.0, 4.0), Random(-4.0, 4.0), true, false); }
	}
}

// Deletes all drops that are on the ground
script "ClearDrops" (void)
{
	for (int i = ItemID; i <= ItemID + MaxItems; i++)
	{
		SpawnForced("TeleportFog", GetActorX(i), GetActorY(i), GetActorZ(i), 0, 0);
		Thing_Remove(i);
	}
	
	MaxItems = 0;
}

// Set Skill Level during the game
script "SetSkill" (int NewSkill)
{
	SetFont("BIGFONT");
	
	if (NewSkill < 0 || NewSkill > 6)
	{
		Print(s:"\cgInvalid Skill Level");
		terminate;
	}
	
	FadeRange(255, 255, 255, 0.5, 255, 255, 255, 0.0, 0.5);
	ChangeSkill(NewSkill);
	ActivatorSound("misc/secret", 127);
	Print(s:"\cjSkill Level has been changed to\n\cg", d:NewSkill + 1, s:" - ", s:SkillLevels[NewSkill]);
}

// Respec - Respecialize your Player
script "Respec" (int Stats, int Skills, int Inventory, int Levels)
{
	// Vars
	int StatPoints;
	int SkillPoints;
	int OldCredits;
	
	// Respec Stats
	if (Stats)
	{
		// Add stats into a pool of tokens to give back to the player
		StatPoints += Strength;
		StatPoints += Defense;
		StatPoints += Vitality - 10;
		StatPoints += Energy - 10;
		StatPoints += Regeneration;
		StatPoints += Agility;
		StatPoints += Capacity - 10;
		StatPoints += Luck;
		
		// Reset Stats
		Strength = 0;
		Defense = 0;
		Vitality = 10;
		Energy = 10;
		Regeneration = 0;
		Agility = 0;
		Capacity = 10;
		Luck = 0;

		// Give Respecced Stat Tokens back
		GiveInventory("StatToken", StatPoints);

		// Reset HP and EP
		SetActorProperty(0, APROP_HEALTH, 100);
		EP = 100;
		EPMax = 100;
		
		// Reset Shield
		Shield = 0;
		ShieldTimer = 375;
	}
	
	// Respec Skills
	if (Skills)
	{
		// Reset skills, add skill levels together into a pool of tokens to give back to the player
		for (int i = 0; i < MAX_CATEGORIES; i++)
			for (int j = 0; j < MAX_SKILLS; j++)
			{
				SkillPoints += SkillLevel[(i * MAX_SKILLS) + j];
				SkillLevel[(i * MAX_SKILLS) + j] = 0;
			}

		// Give Respecced Skill Tokens back
		GiveInventory("StatToken", SkillPoints);

		// Remove Auras
		Aura = 0;
		AuraTimer = 0;
	}
	
	// Clear and Sell Inventory
	if (Inventory)
	{
		// Run through the shop catagories and sell everything using the utility SellAll script
		for (int k = 1; k < SHOP_CATEGORIES - 1; k++)
		{
			ACS_NamedExecuteAlways("SellAll", 0, k, 1);
			Delay(1);
		}
		OldCredits = CheckInventory("Credits");
		
		// Remove Augs
		for (int l = 0; l < MAX_AUGS; l++)
			Augs[l] = false;
		AugSlotsUsed = 0;

		// Clear out everything else that isn't sellable or can't be sold
		ClearInventory();
		
		// Restore the default item loadout
		DefaultLoadout();
		
		// Restore default Shield Parts
		ShieldBody = 1;
		ShieldBattery = 1;
		ShieldCapacitor = 1;
		ShieldAccessory = 1;
	}
	
	// Reset Levels
	if (Levels)
	{
		Level = 0;
		XP = 0;
		XPNext = 0;
		RankLevel = 0;
		Rank = 0;
		RankNext = 0;
	}
		
	// Take 1/2 Credits
	GiveInventory("Credits", OldCredits);
	TakeInventory("Credits", CheckInventory("Credits") / 2);
	
	// FX
	FadeRange(255, 255, 255, 0.75, 0, 0, 0, 0.0, 2.5);
	HudMessage(s:"Respec Complete"; HUDMSG_FADEOUT, 0, CR_WHITE, 0.5, 0.5, 2.5, 2.5);
	ActivatorSound("misc/secret", 127);
}

// For items or other things to add EP
script "AddEP" (int Amount)
{
	FadeRange(0, 255, 255, 0.25, 0, 255, 255, 0, 0.25);
	
	EP += amount;
	
	if (EP > EPMax)
		EP = EPMax;
}

// Returns your current EP (for DECORATE)
script "GetEP" (void)
{
	SetResultValue(EP);
}

// Returns your max EP (for DECORATE)
script "GetEPMax" (void)
{
	SetResultValue(EPMax);
}

// Returns the amount to heal given the percentage out of max HP (for DECORATE)
script "GetHealPercent" (int Percent)
{
	int Amount = HealthMax * Percent / 100;
	SetResultValue(Amount);
}

// Returns your max Health (for DECORATE)
script "GetHealthMax" (void)
{
	SetResultValue(HealthMax);
}

// Used by the RegenSphere to temporarily increase regen rates
script "RegenBoost" (void)
{
	RegenBoostTimer += Random(1000, 3000);
}

// HAAAAAAAAAAAAAAAX
script "Cheat" (int StatBoost)
{
	int ItemMax = 1000;
	
	// Max Level/Rank
	XP = 1000000000;
	Rank = 1000000000;
	
	// Stats
	Strength += statboost;
	Defense += statboost;
	Vitality += statboost;
	Energy += statboost;
	Regeneration += statboost;
	Agility += statboost;
	Capacity += statboost;
	Luck += statboost;
	
	// Max out all Skills
	for (int i = 0; i < MAX_CATEGORIES; i++)
		for (int j = 0; j < MAX_SKILLS; j++)
		{
			SkillLevel[(i * MAX_SKILLS) + j] = SkillLevelMax[i][j];
			CurrentSkillLevel[(i * MAX_SKILLS) + j] = SkillLevelMax[i][j];
		}
	
	// Items - Tokens
	// GiveInventory("StatToken", 10000);
	GiveInventory("StatCapToken", 100);
	// GiveInventory("SkillToken", 10000);
	
	// Items - Health
	GiveInventory("StimPack", ItemMax);
	GiveInventory("SmallMedikit", ItemMax);
	GiveInventory("Medikit", ItemMax);
	GiveInventory("LargeMedikit", ItemMax);
	GiveInventory("XLMedikit", ItemMax);
	GiveInventory("MedPack", ItemMax);
	GiveInventory("SurgeryKit", ItemMax);
	GiveInventory("Continue", 1000);
	
	// Items - Armor
	TakeInventory("BasicArmor", 10000000);
	GiveInventory("WhiteArmor", 1);
	Delay(1);
	for (int k = 0; k < ItemMax; k++)
	{
		GiveInventory("UsedGreenArmor", ItemMax);
		GiveInventory("GreenArmor", ItemMax);
		GiveInventory("UsedBlueArmor", ItemMax);
		GiveInventory("BlueArmor", ItemMax);
		GiveInventory("UsedYellowArmor", ItemMax);
		GiveInventory("YellowArmor", ItemMax);
		GiveInventory("UsedRedArmor", ItemMax);
		GiveInventory("RedArmor", ItemMax);
		GiveInventory("UsedWhiteArmor", ItemMax);
		GiveInventory("WhiteArmor", ItemMax);
	}
	
	// Items - Powerups
	GiveInventory("InvulnerabilitySphere", ItemMax);
	GiveInventory("InvulnerabilityCharge", ItemMax);
	GiveInventory("BlurSphere", ItemMax);
	GiveInventory("InvisibilityCharge", ItemMax);
	GiveInventory("RadSuit", ItemMax);
	GiveInventory("Infrared", ItemMax);
	GiveInventory("Berserk", ItemMax);
	GiveInventory("AllMap", ItemMax);
	
	// Items - Weapons/Ammo
	GiveInventory("Pistol", 1);
	GiveInventory("Shotgun", 1);
	GiveInventory("SuperShotgun", 1);
	GiveInventory("ChainGun", 1);
	GiveInventory("RocketLauncher", 1);
	GiveInventory("PlasmaRifle", 1);
	GiveInventory("BFG9000", 1);
	GiveInventory("ChainSaw", 1);
	GiveInventory("Pistol1", 1);
	GiveInventory("Shotgun1", 1);
	GiveInventory("SSG", 1);
	GiveInventory("ChainGun1", 1);
	GiveInventory("RocketLauncher1", 1);
	GiveInventory("PlasmaGun", 1);
	GiveInventory("BFG9001", 1);
	GiveInventory("ChainSaw1", 1);
	GiveInventory("Clip", GetAmmoCapacity("Clip"));
	GiveInventory("Shell", GetAmmoCapacity("Shell"));
	GiveInventory("RocketAmmo", GetAmmoCapacity("RocketAmmo"));
	GiveInventory("Cell", GetAmmoCapacity("Cell"));
	// GiveInventory("GrenadeFrag", 100);
	
	// Items - Misc
	ACS_NamedExecute("AllRunes", 0);
	ACS_NamedExecute("GiveAugs", 0);
	ACS_NamedExecute("FullShield", 0);
	GiveInventory("AugSlotUpgrade", 30);
	GiveInventory("RedCard", 1);
	GiveInventory("YellowCard", 1);
	GiveInventory("BlueCard", 1);
	GiveInventory("RedSkull", 1);
	GiveInventory("YellowSkull", 1);
	GiveInventory("BlueSkull", 1);
	GiveInventory("PowerInvulnerable", 1);
	GiveInventory("PowerShadow", 1);
	GiveInventory("PowerIronFeet", 1);
	GiveInventory("PowerLightAmp", 1);
	GiveInventory("Credits", 1000000000);
	
	// Restore HP/EP/Shields
	HealThing(1000000);
	EP = EPMax;
	Shield = ShieldCapacity;
	ShieldTimer = 0;
}

// For testing different stats and their effects
script "ModStat" (int stat, int value)
{
	switch (stat)
	{
		case 1:	strength = value;		break;
		case 2:	Defense = value;		break;
		case 3:	Vitality = value;		break;
		case 4:	Energy = value;			break;
		case 5:	Regeneration = value;	break;
		case 6:	Agility = value;		break;
		case 7:	Capacity = value;		break;
		case 8:	Luck = value;			break;
	}
}

// Give Credits
script "GiveCredits" (int Amount)
{
	if (Amount == 0) Amount = 1000000000;
	GiveInventory("Credits", Amount);
	ActivatorSound("credits/payout", 127);
}

// Instant payout
script "Payout" (int Time)
{
	if (Time > 0)
		PayTimer = Time;
	else
		PayTimer = 35 * 60 * GetCVAR("drpg_pay_interval");
}

// Give all Runes
script "AllRunes" (void)
{
	ActivatorSound("misc/secret", 127);
	GiveInventory("StrengthRune", 1);
	GiveInventory("DrainRune", 1);
	GiveInventory("EndlessRune", 1);
	GiveInventory("ResistanceRune", 1);
	GiveInventory("RegenerationRune", 1);
	GiveInventory("FrightRune", 1);
	GiveInventory("GhostRune", 1);
	GiveInventory("HighJumpRune", 1);
	GiveInventory("HAsteRune", 1);
}

// Give all Augs
script "GiveAugs" (void)
{
	GiveInventory("StrengthAug", 1);
	GiveInventory("DefenseAug", 1);
	GiveInventory("AgilityAug", 1);
	GiveInventory("VitalityAug", 1);
	GiveInventory("EnergyAug", 1);
	GiveInventory("RegenerationAug", 1);
	GiveInventory("CapacityAug", 1);
	GiveInventory("LuckAug", 1);
	GiveInventory("BlackMarketAug", 1);
}

// Give all Shield Components
script "FullShield" (void)
{
	// Bodies
	GiveInventory("ShieldBody2", 1);
	GiveInventory("ShieldBody3", 1);
	GiveInventory("ShieldBody4", 1);
	GiveInventory("ShieldBody5", 1);
	GiveInventory("ShieldBody6", 1);
	GiveInventory("ShieldBody7", 1);
	
	// Batteries
	GiveInventory("ShieldBattery2", 1);
	GiveInventory("ShieldBattery3", 1);
	GiveInventory("ShieldBattery4", 1);
	GiveInventory("ShieldBattery5", 1);
	GiveInventory("ShieldBattery6", 1);
	GiveInventory("ShieldBattery7", 1);

	// Capacitors
	GiveInventory("ShieldCapacitor2", 1);
	GiveInventory("ShieldCapacitor3", 1);
	GiveInventory("ShieldCapacitor4", 1);
	GiveInventory("ShieldCapacitor5", 1);
	
	// Accessories
	GiveInventory("ShieldAccessory1", 1);
	GiveInventory("ShieldAccessory2", 1);
	GiveInventory("ShieldAccessory3", 1);
	GiveInventory("ShieldAccessory4", 1);
	GiveInventory("ShieldAccessory5", 1);
	GiveInventory("ShieldAccessory6", 1);
	GiveInventory("ShieldAccessory7", 1);
}

// Handles Damage Numbers
script "DamageNumber" (void)
{
	// Translations
	CreateTranslation(1, 0 : 255 = 176 : 176);	// Overkill
	CreateTranslation(2, 0 : 255 = 112 : 112);	// Healed
	CreateTranslation(3, 0 : 255 = 100 : 100);	// Scratch
	
	GiveInventory("MonsterHeightCheck", GetActorProperty(0, APROP_HEIGHT));

	while (GetActorProperty(0, APROP_HEALTH) > 0)
	{
		SetInventory("MonsterCurrentHealth", GetActorProperty(0, APROP_HEALTH));
		SetInventory("MonsterHasTicked", 0);
		
		// Lag handling
		int DelayRate = GetLevelInfo(LEVELINFO_TOTAL_MONSTERS) / 100;
		Delay(1 + DelayRate);
		
		if (GetActorProperty(0, APROP_HEALTH) != CheckInventory("MonsterCurrentHealth"))
			if (CheckInventory("MonsterHasTicked") == 0)
			{
				int NewHealth = CheckInventory("MonsterCurrentHealth") - GetActorProperty(0, APROP_HEALTH);
				
				if (NewHealth > 9999)
					NewHealth = 9999;
				
				if (NewHealth != 0)
				{
					int Digit1 = GetDigit(Abs(NewHealth), 0);
					int Digit2 = GetDigit(Abs(NewHealth), 1);
					int Digit3 = GetDigit(Abs(NewHealth), 2);
					int Digit4 = GetDigit(Abs(NewHealth), 3);
					
					int X = GetActorX(0) + 1.0;
					int Y = GetActorY(0) + Random(-10.0, 10.0);
					int Z;
					int ActorHeight = CheckInventory("MonsterHeightCheck");
					int DigitID = Random(4000, 5000);
					bool Healed = (NewHealth < 0);
					
					if (Healed)
						Log(s:"\cd", d:NewHealth);
					
					if (ActorHeight > 0)
						Z = GetActorZ(0) + Random(ActorHeight, ActorHeight + 6);
					else
						Z = GetActorZ(0) + GetActorProperty(0, APROP_HEIGHT) + Random(0.0, 6.0);
					
					switch (Digit1)
					{
						case 0:	Spawn("Digit1Num0", X, Y, Z, DigitID, 0);	break;
						case 1:	Spawn("Digit1Num1", X, Y, Z, DigitID, 0);	break;
						case 2:	Spawn("Digit1Num2", X, Y, Z, DigitID, 0);	break;
						case 3:	Spawn("Digit1Num3", X, Y, Z, DigitID, 0);	break;
						case 4:	Spawn("Digit1Num4", X, Y, Z, DigitID, 0);	break;
						case 5:	Spawn("Digit1Num5", X, Y, Z, DigitID, 0);	break;
						case 6:	Spawn("Digit1Num6", X, Y, Z, DigitID, 0);	break;
						case 7:	Spawn("Digit1Num7", X, Y, Z, DigitID, 0);	break;
						case 8:	Spawn("Digit1Num8", X, Y, Z, DigitID, 0);	break;
						case 9:	Spawn("Digit1Num9", X, Y, Z, DigitID, 0);	break;
					}
					
					if (NewHealth >= 10 || NewHealth <= -10)
						switch (Digit2)
						{
							case 0:	Spawn("Digit2Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit2Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit2Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit2Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit2Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit2Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit2Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit2Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit2Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit2Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					if (NewHealth >= 100 || NewHealth <= -100)
						switch (Digit3)
						{
							case 0:	Spawn("Digit3Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit3Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit3Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit3Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit3Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit3Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit3Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit3Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit3Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit3Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					if (NewHealth >= 1000 || NewHealth <= -1000)
						switch (Digit4)
						{
							case 0:	Spawn("Digit4Num0", X, Y, Z, DigitID, 0);	break;
							case 1:	Spawn("Digit4Num1", X, Y, Z, DigitID, 0);	break;
							case 2:	Spawn("Digit4Num2", X, Y, Z, DigitID, 0);	break;
							case 3:	Spawn("Digit4Num3", X, Y, Z, DigitID, 0);	break;
							case 4:	Spawn("Digit4Num4", X, Y, Z, DigitID, 0);	break;
							case 5:	Spawn("Digit4Num5", X, Y, Z, DigitID, 0);	break;
							case 6:	Spawn("Digit4Num6", X, Y, Z, DigitID, 0);	break;
							case 7:	Spawn("Digit4Num7", X, Y, Z, DigitID, 0);	break;
							case 8:	Spawn("Digit4Num8", X, Y, Z, DigitID, 0);	break;
							case 9:	Spawn("Digit4Num9", X, Y, Z, DigitID, 0);	break;
						}
					
					// Overkill
					if (NewHealth >= GetActorProperty(0, APROP_SPAWNHEALTH))
						Thing_SetTranslation(DigitID, 1);

					// Healed
					if (Healed)
						Thing_SetTranslation(DigitID, 2);

					// Scratch
					if (NewHealth == 1)
						Thing_SetTranslation(DigitID, 3);
						
					SetActorVelocity(DigitID, 0, Random(-1.0, 1.0), 0.5, 0, 0);
					SetInventory("MonsterHasTicked", 1);
				}
			}
	}
}

// Handles HUD Health Bars
script "HealthBars" (void)
{
	while (GetCVAR("drpg_healthbars"))
	{
		Thing_ChangeTID(0, 1000);
		int Activator = ActivatorTID();
		SetActivatorToTarget(1000);
		
		Delay(1);
		
		int CurrentHealth = GetActorProperty(0, APROP_HEALTH);
		int MaxHealth = GetActorProperty(0, APROP_SPAWNHEALTH);
		int HealthPercent;
		str FillType;
		
		if (ActivatorTID() != Activator)
		{
			SetActivator(Activator);
			SetActivatorToTarget(1000);
			
			SetActivator(Activator);
			
			HealthPercent = (CurrentHealth * 100) / MaxHealth;
			
			if (HealthPercent > 100)
				FillType = "Fill5";
			if (HealthPercent >= 75 && HealthPercent <= 100)
				FillType = "Fill1";
			if (HealthPercent >= 50 && HealthPercent <= 74)
				FillType = "Fill2";
			if (HealthPercent >= 25 && HealthPercent <= 49)
				FillType = "Fill3";
			if (HealthPercent <= 24)
				FillType = "Fill4";
			
			if (CurrentHealth > 0 && (!InMenu && !InShop))
			{
				SetHudSize(800, 600, false);
				PrintSprite("HPBar", 4000, 400.0, 100.0, 0.1);
				SetFont("SmallFont");
				HudMessage(d:CurrentHealth, s:"/", d:MaxHealth; HUDMSG_PLAIN, 4001, CR_GREEN, 400.0, 100.0, 0.1);
				
				// Prevent bar overflow
				if (HealthPercent > 100)
					HealthPercent = 100;
				
				for (int i = 0; i < HealthPercent; i++)
					PrintSprite(FillType, 4002 + i, 303.0 + (i * 2.0), 100.0, 0.1);
			}
		}
	}
	
	Delay(1);
	restart;
}

// Utility Functions
// --------------------------------------------------

// Gets Max EP after all bonuses are applied to it
function int GetEPMax(void)
{
	int MaxEP = EPMax;
	
	// Max EP Checks for Augs
	if (Augs[AUG_ENERGY] || Augs[AUG_BLACKMARKET])
		MaxEP *= 2;
	if (Augs[AUG_ENERGY] && Augs[AUG_BLACKMARKET])
		MaxEP *= 2;
	
	return MaxEP;
}

// PrintSprite Utility Function
function void PrintSprite(str sprite, int id, int x, int y, int d)
{
    SetFont(sprite);
    HudMessage(s:"A"; HUDMSG_PLAIN, id, CR_UNTRANSLATED, x, y, d);
}

function void PrintSpriteFade(str sprite, int id, int x, int y, int d, int h)
{
    SetFont(sprite);
    HudMessage(s:"A"; HUDMSG_FADEOUT, id, CR_UNTRANSLATED, x, y, d, h);
}

function int SetInventory(str Item, int Count)
{
	int n = Count - CheckInventory(Item);
	
	if (n > 0)
		GiveInventory(Item, n);
	else if (n < 0)
		TakeInventory(Item, -n);
	
	return n;
}

// For Damage Digits
function int GetDigit(int Num, int Digit)
{
	return (Num / Pow(10, Digit)) % 10;
}

// Returns integer value
function int Round(int fixedNumber)
{
	return (fixedNumber + 0.5) >> 16;
}

// Returns fixed point value
function int Floor(int fixedNumber)
{
	return fixedNumber & 0xFFFF0000;
}

// This function computes n-th power of x.
function int Pow(int x, int n)
{
	int y = 1;
	while (n-- > 0) y *= x;
	return y;
}

// This function can be used to return the absolute value of a negative integer. 
function int Abs(int x)
{
    if (x < 0)
        return -x;

    return x;
}
