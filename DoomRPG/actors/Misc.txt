// Icons
actor MenuIcon
{
	+NOTIMEFREEZE
	+NOINTERACTION
	
    RenderStyle Add
	
	Scale 0.75
	
	States
	{
	Spawn:
		MNIC A 2 Bright
		Stop
	}
}

actor LevelUpArrow
{
    +NOTIMEFREEZE
    +NOINTERACTION
    
    RenderStyle Add
    
    States
    {
    Spawn:
        ARRW A 0 Bright NoDelay A_ChangeVelocity(FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), 1, CVF_REPLACE)
    Fade:
        ARRW A 1 Bright A_FadeOut(0.01, true)
        Loop
    }
}

actor RankUpArrow
{
    +NOTIMEFREEZE
    +NOINTERACTION
    
    RenderStyle Add
    
    States
    {
    Spawn:
        ARRW B 0 Bright NoDelay A_ChangeVelocity(FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), 1, CVF_REPLACE)
    Fade:
        ARRW B 1 Bright A_FadeOut(0.01, true)
        Loop
    }
}

// Bouncy Balls!
actor BouncyBall 19998
{
	var int user_type;
	
	Radius 8
	Height 32
	Mass 1
	Gravity 0.5
	Speed 0
	
	BounceType "Doom"
	BounceFactor 1.0
	
	+SOLID
	+MISSILE
	+PUSHABLE
	+SHOOTABLE
	+NOBLOOD
	+BOUNCEONACTORS
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ThrustThing(Angle, Random(4, 8), false, 0)
		TNT1 A 0 A_Jump(256, "Ball1", "Ball2", "Ball3", "Ball4", "Ball5", "Ball6")
		Stop
	Ball1:
		TNT1 A 0 A_SetUserVar("user_type", 1)
		SOUL ABCDCB 6 Bright
		Loop
	Ball2:
		TNT1 A 0 A_SetUserVar("user_type", 2)
		MEGA ABCD 6 Bright
		Loop
	Ball3:
		TNT1 A 0 A_SetUserVar("user_type", 3)
		PINV ABCD 6 Bright
		Loop
	Ball4:
		TNT1 A 0 A_SetUserVar("user_type", 4)
		PINS ABCD 6 Bright
		Loop
	Ball5:
		TNT1 A 0 A_SetUserVar("user_type", 5)
		TIME ABCDCB 4 Bright
		Loop
	Ball6:
		TNT1 A 0 A_SetUserVar("user_type", 6)
		REGN ABCDCB 4 Bright
		Loop
	Death:
		TNT1 A 0 ThrustThing(Angle, Random(4, 8), false, 0)
		TNT1 A 0 A_JumpIf(user_type == 1, "Ball1")
		TNT1 A 0 A_JumpIf(user_type == 2, "Ball2")
		TNT1 A 0 A_JumpIf(user_type == 3, "Ball3")
		TNT1 A 0 A_JumpIf(user_type == 4, "Ball4")
		TNT1 A 0 A_JumpIf(user_type == 5, "Ball5")
		TNT1 A 0 A_JumpIf(user_type == 6, "Ball6")
		Stop
	}
}

// Misc hacky crap

// Time Freeze for Menus
actor MenuFreezer : PowerTimeFreezer
{
	Powerup.Duration 2
}

actor TimeFreezerSound
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("slomo/in", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
		TNT1 A 0 
		TNT1 A 0 A_PlaySound("slomo/loop", CHAN_AUTO | CHAN_NOPAUSE, 1.0, true, ATTN_NONE)
		TNT1 A 7
		TNT1 A 2
		TNT1 A 0 A_PlaySound("slomo/out", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
		TNT1 A 35
		Stop
	}
}

actor TimeFreezerQuickSound
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("slomo/in", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
		TNT1 A 0
		TNT1 A 0 A_PlaySound("slomo/loop", CHAN_AUTO | CHAN_NOPAUSE, 1.0, true, ATTN_NONE)
		TNT1 A 7
		TNT1 A 180
		TNT1 A 0 A_PlaySound("slomo/out", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
		TNT1 A 35
		Stop
	}
}

actor Speed : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE
	
	Powerup.Type DoubleFiringSpeed
	Powerup.Duration 2
}

actor ShotSpawn
{
	Mass 5
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("DumbShotgun", Random(0, 80), 0, 0, 0, Random(-30, 30))
		TNT1 A 1 A_Jump(254, "Spawn")
		Stop
	}
}

actor DumbShotgun
{
	Mass 10
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("weapons/shotgr")
		SHOT A 50 A_SetTics(Random(40, 70))
		TNT1 A 0 A_PlaySound("weapons/shotgf")
		TNT1 A 0 A_SetAngle(Random(0, 256))
		TNT1 A 0 A_ChangeFlag("VISIBILITYPULSE", true)
		TNT1 A 0 A_SetTranslucent(1.0, 1)
		SHOT A 10 Bright A_CustomBulletAttack(30, 45, Random(5, 25), 1, "BulletPuff", 9999.0, CBAF_AIMFACING)
		SHOT A 105
		TNT1 A 0 A_SetScale(4.0, 4.0)
		TNT1 A 0 A_SetTranslucent(1.0, 0)
		TNT1 A 0 A_PlaySound("weapons/rocklx")
		MISL B 3 Bright A_Explode(3, 64, XF_NOTMISSILE, 256, 1)
		MISL C 3 Bright 
		MISL D 3 Bright 
		Stop
	}
}

actor DumbPistol
{
	States
	{
	Spawn:
		PIST A 0 NoDelay A_ChangeVelocity(FRandom(-4.0, 4.0), FRandom(-4.0, 4.0), FRandom(0.0, 32.0))
		PIST A -1
		Stop
	}
}
