ACTOR DRPGMarine
{
  Health 100
  GibHealth -50
  Radius 20
  Height 56
  Speed 8
  PainChance 256
  DamageFactor "Crush", 10
  Monster
  +FLOORCLIP
  +AVOIDMELEE
  +FRIENDLY
  +MISSILEMORE
  -COUNTKILL
  +AMBUSH
  +NEVERRESPAWN
  +QUICKTORETALIATE
  +NOBLOCKMONST
  +NOINFIGHTING
  +DONTHARMSPECIES
  Obituary "The UAC decided to terminate %o's employment."
  Species "Human"
  Tag "Marine"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NEGATIVEXP | MF_NOPAYKILL | MF_NODROPS)
    TNT1 A 0
	Goto Idle
  Idle:
    TNT1 A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_DEFAULT)
	Goto FuriousIdle
    NMA2 A 5
    Goto Idle+1
  FuriousIdle:
    NMA2 ABBABBAA 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarinePistolShotCounter", 6, "RetreatReload")
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_JumpIfHealthLower (30, "SeeScared")
    NMA2 A 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 A 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 B 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 B 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 C 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 C 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 D 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    NMA2 D 3 A_Chase
    TNT1 A 0 A_JumpIfTargetInLOS (3, 0, JLOSF_CLOSENOSIGHT, 0, 512)
    TNT1 A 0 A_GiveInventory ("DRPGMarineGuardChaseCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGMarineGuardChaseCounter", 100, "TargetReset")
    TNT1 A 0
    Loop
  SeeScared:
    TNT1 A 0 A_JumpIfHealthLower (30, 1)
	Goto See
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    NMA2 A 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 A 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 B 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 C 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, 1)
	Goto TargetReset
    NMA2 D 2 A_Chase
    TNT1 A 0 A_JumpIfCloser (1024, "SeeScared")
	Goto TargetReset
  TargetReset:
    TNT1 A 0 A_TakeInventory ("DRPGMarineGuardChaseCounter", 100)
    TNT1 A 0 A_ClearTarget
    Goto FuriousIdle
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    NMA2 AA 3 A_Chase ("", "")
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    NMA2 BB 3 A_Chase ("", "")
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    NMA2 CC 3 A_Chase ("", "")
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    NMA2 DD 3 A_Chase ("", "")
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGMarinePistolShotCounter", 6, "RetreatReload")
    NMA2 E 8 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("DRPGMarinePistolShotCounter", 1)
    TNT1 A 0 A_PlaySound("drpgmarines/pistol", CHAN_WEAPON)
    NMA2 F 4 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    NMA2 E 6
    TNT1 A 0 A_JumpIfInventory ("DRPGMarinePistolShotCounter", 6, "RetreatReload")
    NMA2 E 5 A_MonsterRefire (0, "Overwatch")
    Goto Missile
  Overwatch:
    TNT1 A 0 A_JumpIfTargetInLOS (1, 0, JLOSF_DEADNOJUMP)
    Goto See
    NMA2 E 1 A_MonsterRefire (250, "See")
    Goto Overwatch
  Reload:
    NMA2 EE 4
    TNT1 A 0 A_PlaySound("drpgmarines/pistolreloadout", 7)
    NMA2 EE 4
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("drpgmarines/pistolreloadin", 7)
    TNT1 A 0 A_TakeInventory ("DRPGMarinePistolShotCounter", 6)
    NMA2 EEEE 4
    NMA2 E 3
    TNT1 A 0 A_PlaySound("drpgmarines/pistolcock", 7)
    NMA2 E 6
    Goto See
  Pain:
    TNT1 A 0 A_CheckFlag("FRIENDLY", "Enraged", AAPTR_DEFAULT)
	Goto PainContinue
  PainContinue:
    NMA2 G 3
    NMA2 G 3 A_Pain
    Goto See
  Enraged:
    TNT1 A 0 A_ChangeFlag ("FRIENDLY", 0)
    Goto PainContinue
  Death:
    NMA2 H 3
    NMA2 I 3 A_Scream
    NMA2 J 3 A_NoBlocking
    NMA2 K 3
    TNT1 A 0 A_SpawnItem ("DRPGBodyThud", 0, 0, 0, 0)
    NMA2 LM 3
    NMA2 N -1
    Stop
  XDeath:
    NMA2 O 3
    NMA2 P 3 A_XScream
    NMA2 Q 3 A_NoBlocking
    NMA2 PQ 3
    TNT1 A 0 A_SpawnItem ("DRPGGibThud", 0, 0, 0, 0)
    NMA2 RSTUV 3
    NMA2 W -1
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("DRPGCrushedMarine",0,0,0,0)
    Stop
  }
}

//-------------------------------------------------------------------
//		BULLET PUFFS

ACTOR DRPGMarineBulletPuff
{ 
   ActiveSound ""
   PainSound ""
   AttackSound ""
   Decal "BulletChip"
   +NOGRAVITY
   +NOEXTREMEDEATH
   +NOBLOCKMAP
   +ALLOWPARTICLES
   +RANDOMIZE
   +THRUGHOST
   DamageType "Bullet"
   RenderStyle Translucent
   Alpha 0.5
   VSpeed 1
   Mass 5
   States
   {
   Spawn:
      PUFF A 4 Bright
      PUFF BCD 4
      Stop
   }
}

ACTOR DRPGMarineMeleeWeaponPuff : DRPGMarineBulletPuff
{ 
   PainSound ""
   AttackSound ""
   Decal "None"
   DamageType "Melee"
}

ACTOR DRPGMarineBulletPuffGib : DRPGMarineBulletPuff
{ 
   -NOEXTREMEDEATH
}

ACTOR DRPGMarineMinigunBulletPuff : DRPGMarineBulletPuff
{ 
   -NOEXTREMEDEATH
   +EXTREMEDEATH
}

//-------------------------------------------------------------------
//		CRUSHED BODIES
// Nashgore's squishy leftovers from ceiling-based mayhem.

// Small
actor DRPGCrushedMarine
{
states
	{
	Spawn:
	 TNT1 A 0 A_PlaySound("misc/gibs")
	 CRS1 A -1
	 loop
	}
}

//-------------------------------------------------------------------
//		MISCELLANEOUS

Actor DRPGMarineGuardChaseCounter : Inventory
{
  inventory.maxamount 100
}

Actor DRPGMarineSummonAngerCounter : Inventory
{
  inventory.maxamount 5
}

ACTOR DRPGBodyThud
{
   Radius  32
   Height  6
     +CORPSE
     +NOCLIP
     -DONTSPLASH
   States
   {
   Spawn:
      TNT1 A 8
      Stop
   Crash:
      TNT1 A 1 A_PlaySound("drpgmarines/bodythud")
      Stop
   }
}

ACTOR DRPGGibThud : DRPGBodyThud
{
   States
   {
   Spawn:
      TNT1 A 8
      Stop
   Crash:
      TNT1 A 1 A_PlaySound("drpgmarines/gibthud")
      Stop
   }
}
