// Powerups
actor SkillInvulnerability1 : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ADDITIVETIME
	
	Powerup.Type Invulnerable
	Powerup.Color GoldMap
	Powerup.Duration -30
}

actor SkillInvulnerability2 : SkillInvulnerability1
{
	Powerup.Mode Reflective
}

actor SkillInvisibility1 : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ADDITIVETIME
	
	Powerup.Type Shadow
	Powerup.Strength 100
	Powerup.Duration -30
}

actor SkillInvisibility2 : SkillInvisibility1
{
	Powerup.Type Ghost
}

actor TimeFreezerQuick : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE

	Powerup.Type "TimeFreezer"
	Powerup.Color GrayMap
	Powerup.Duration 3
}

// Used for SBARINFO Aura HUD checking, horrible hacks
actor Aura : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
}

// Aura Skills
actor RedAura2x : PowerDamage 					{ Powerup.Duration 2 DamageFactor 2.0 }
actor RedAura4x : PowerDamage 					{ Powerup.Duration 2 DamageFactor 2.0 }
actor RedAuraInfiniteAmmo : PowerInfiniteAmmo 	{ Powerup.Duration 2 }
actor GreenAuraIronFeet : PowerIronFeet			{ Powerup.Duration 2 }
actor PinkAuraDrain : PowerDrain				{ Powerup.Duration 2 }

// Bullet-Time Skill
actor SkillBulletTime : PowerupGiver
{
	-INVBAR
	
	+INVENTORY.AUTOACTIVATE
	-INVENTORY.ADDITIVETIME
	
 	Powerup.Type TimeFreezer
	Powerup.Duration 60
}

// Repulsion Skill
actor SkillBlast1 : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 A_Blast(0, 255, 255, 20, "BlastEffect", "skills/blast")
        Stop
    }
}

actor SkillBlast2 : SkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 512, 50, "BlastEffect", "skills/blast")
        Stop
    }
}

actor SkillBlast3 : SkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 1024, 100, "BlastEffect", "skills/blast")
        Stop
    }
}

actor SkillBlast4 : SkillBlast1
{
    States
    {
    Use:
        TNT1 A 0 A_Blast(0, 255, 2048, 200, "BlastEffect", "skills/blast")
        Stop
    }
}

// Force Field
actor ForceField
{
	Radius 32
	Height 64
	
	+SOLID
	+BRIGHT
	+NOCLIP
	+REFLECTIVE
	+DEFLECT
	
	RenderStyle Add
	Alpha 0.5
	
	States
	{
	Spawn:
		FFLD A 0
	FadeOut:
		FFLD A 3 A_FadeOut(0.001, true)
		Loop
	}
}

actor ForceField2 : ForceField
{
	Radius 64
	Height 128
	Scale 2.0
}

// Player Teleport Setters
actor PlayerTeleportGhost : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	
	States
	{
	Use:
		TNT1 A 0 A_ChangeFlag("SOLID", false)
		TNT1 A 0 A_ChangeFlag("GHOST", true)
		TNT1 A 0 A_SetTranslucent(0.5, 1)
		Stop
	}
}

actor PlayerTeleportNormal : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	
	States
	{
	Use:
		TNT1 A 0 A_ChangeFlag("SOLID", true)
		TNT1 A 0 A_ChangeFlag("GHOST", false)
		TNT1 A 0 A_SetTranslucent(1.0, 0)
		Stop
	}
}

actor TranslocateBall : TelOtherFX1
{
	Radius 6
	Height 1
	Speed 20
	
	SeeSound "weapons/bfgf"
	DeathSound "weapons/bfgx"
	
	Scale 0.5
	
	States
	{
	Spawn:
		BFS1 AB 4 Bright
		Loop
	Death:
		BFE1 ABCDEF 8 Bright
		Stop
	}
}

// Mean Things
actor EPDrain : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, -1)
        Stop
    }
}

actor AuraRemover : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0
    Use:
        TNT1 A 0 ACS_NamedExecuteAlways("RemoveAura", 0)
        Stop
    }
}
