// Powerups
actor SkillInvulnerability1 : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ADDITIVETIME
	
	Powerup.Type Invulnerable
	Powerup.Color GoldMap
	Powerup.Duration -30
}

actor SkillInvulnerability2 : SkillInvulnerability1
{
	Powerup.Mode Reflective
}

actor SkillInvisibility1 : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ADDITIVETIME
	
	Powerup.Type Shadow
	Powerup.Strength 100
	Powerup.Duration -30
}

actor SkillInvisibility2 : SkillInvisibility1
{
	Powerup.Type Ghost
}

actor TimeFreezerQuick : PowerupGiver
{
	-INVBAR
	+INVENTORY.AUTOACTIVATE

	Powerup.Type "TimeFreezer"
	Powerup.Color GrayMap
	Powerup.Duration 3
}

// Used for SBARINFO Aura HUD checking, horrible hacks
actor Aura : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
}

// --------------------------------------------------
// AURA FX

// Red Aura - Strength
Actor RedAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 2 NoDelay A_SpawnItemEx ("RedAuraParticle", 0,0,0, FRandom (0.1, 2.5),0,FRandom (-2.5, 1.25), Random (0, 360))
		Stop
	}
}

Actor RedAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 0.5
	Scale 0.08
	States
	{
	Spawn:
		AUEF A 1 Bright
		TNT1 A 0 A_FadeOut (0.5 / 35.0)
		TNT1 A 0 A_ScaleVelocity (0.97)
		TNT1 A 0 A_ChangeVelocity (0, 0, 0.125)
		Loop
	}
}

// White Aura - XP
Actor WhiteAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 4, "DoNothing")
		TNT1 A 0 A_SpawnItemEx ("WhiteAuraParticle",
			Cos ((-CallACS ("GetTimer") / 60.0) * 360.0 + 180.0) * 32.0,
			Sin ((-CallACS ("GetTimer") / 60.0) * 360.0 + 180.0) * 32.0,
			Sin ((-CallACS ("GetTimer") / 50.0) * 360.0 + 180.0) * 16.0,
			0,0,0, 0)
		TNT1 A 2 A_SpawnItemEx ("WhiteAuraParticle",
			Cos ((CallACS ("GetTimer") / 50.0) * 360.0) * 32.0,
			Sin ((CallACS ("GetTimer") / 50.0) * 360.0) * 32.0,
			Sin ((CallACS ("GetTimer") / 60.0) * 360.0) * 16.0,
			0,0,0, 0)
		Stop
	DoNothing:
		TNT1 A 2
		Stop
	}
}

Actor WhiteAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 1.0
	Scale 0.075
	States
	{
	Spawn:
		AUEF B 1 Bright
		TNT1 A 0 A_FadeOut (1.0 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX - 0.0021)
		Loop
	}
}

// Purple Aura - Regeneration
Actor PurpleAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 6, "DoNothing")
		TNT1 A 2 A_SpawnItemEx ("PurpleAuraParticle",
			Random (-16, 16),
			Random (-16, 16),
			Random (-32, 32),
			0,0,1.0, 0)
		Stop
	DoNothing:
		TNT1 A 2
		Stop
	}
}

// Same effect as Purple Aura but without the light
actor RegenSphereEffect : PurpleAura {}

Actor PurpleAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 1.0
	Scale 0.0375
	States
	{
	Spawn:
		AUEF C 1 Bright
		TNT1 A 0 A_FadeOut (1.0 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX - 0.001, ScaleY + 0.001)
		Loop
	}
}

// Orange Aura - Agility
Actor OrangeAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 2 NoDelay A_SpawnItemEx ("OrangeAuraParticle", 0,0,0, 0,0,0, 0)
		Stop
	}
}

Actor OrangeAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 0.01
	Scale 0.2
	States
	{
	Spawn:
		AUEF DDDDDDDDDDDDDDD 1 NoDelay Bright A_FadeIn (0.1 / 15.0)
		AUEF D 1 Bright A_FadeOut (0.1 / 15.0)
		Wait
	}
}

// Green Aura - Defense
Actor GreenAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	Var Int user_cycle;
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf (user_cycle >= 360, "Done")
		TNT1 A 0 A_SpawnItemEx ("GreenAuraParticle",
			Cos (user_cycle) * 32.0,
			Sin (user_cycle) * 32.0,
			Sin (CallACS ("GetTimer") * 3 + user_cycle) * 10.0,
			0,0,0, 0)
		TNT1 A 0 A_SetUserVar ("user_cycle", user_cycle + 20)
		Loop
	Done:
		TNT1 A 2
		Stop
	}
}

Actor GreenAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 1.0
	Scale 0.025
	States
	{
	Spawn:
		AUEF E 1 Bright
		TNT1 A 0 A_FadeOut (1.0 / 4.0)
		Loop
	}
}

// Pink Aura - Vitality
Actor PinkAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 8, "DoNothing")
		TNT1 A 2 A_SpawnItemEx ("PinkAuraParticle",
			Random (-16, 16),
			Random (-16, 16),
			Random (-32, 32),
			0,0,1.0, 0)
		Stop
	DoNothing:
		TNT1 A 2
		Stop
	}
}

Actor PinkAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 1.0
	Scale 0.0375
	States
	{
	Spawn:
		AUEF F 1 Bright
		TNT1 A 0 A_FadeOut (1.0 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX - 0.001)
		Loop
	}
}

// Blue Aura - Energy
Actor CyanAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 8, "DoNothing")
		TNT1 A 2 A_SpawnItemEx ("CyanAuraParticle",
			Random (-16, 16),
			Random (-16, 16),
			Random (-32, 32),
			0,0,0, 0)
		Stop
	DoNothing:
		TNT1 A 2
		Stop
	}
}

Actor CyanAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 0.66
	Scale 0.025
	States
	{
	Spawn:
		AUEF G 1 Bright
		TNT1 A 0 A_FadeOut (0.66 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX + 0.001)
		Loop
	}
}

// Dark Blue Aura - Capacity
Actor BlueAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 20, "DoNothing")
		TNT1 A 2 A_SpawnItemEx ("BlueAuraParticle", 0,0,0, 0,0,0, 0)
		Stop
	DoNothing:
		TNT1 A 2
		Stop
	}
}

Actor BlueAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 0.33
	Scale 0.15
	States
	{
	Spawn:
		AUEF H 1 Bright
		TNT1 A 0 A_FadeOut (0.33 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX + 0.0014)
		Loop
	}
}

// Yellow Aura - Luck
Actor YellowAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf (CallACS ("GetTimer") % 8, "DoNothing")
		TNT1 A 2 A_SpawnItemEx ("YellowAuraParticle",
			Random (-16, 16),
			Random (-16, 16),
			Random (-32, 32),
			0,0,0, 0)
		TNT1 A 0 A_JumpIf (!(CallACS ("GetTimer") % 35) && !Random (0, 2), "DropCoin")
		Stop
	DropCoin:
		TNT1 A 0 A_SpawnItemEx ("LuckAuraCoin", 0,0,0, FRandom (-3,3),FRandom (-3,3),FRandom (-3,3), 0)
	DoNothing:
		TNT1 A 2
		Stop
	}
}

Actor YellowAuraParticle
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle Add
	Alpha 1.0
	Scale 0.075
	States
	{
	Spawn:
		AUEF I 1 Bright
		TNT1 A 0 A_FadeOut (1.0 / 35.0)
		TNT1 A 0 A_SetScale (ScaleX - 0.0021)
		Loop
	}
}

Actor LuckAuraCoin
{
	Projectile
	+ThruActors
	-NoGravity
	Gravity 0.33
	Damage 0
	Speed 4
	BounceCount 2
	BounceType Doom
	BounceSound "skills/coinbounce"
	DeathSound "skills/coinbounce"
	
	States
	{
	Spawn:
		AUEF J -1 Bright
		Stop
	Death:
		AUEF J 70 Bright
		AUEF J 1 Bright A_FadeOut (1.0 / 20.0)
		Wait
	}
}

// Black Aura - Miniboss
Actor BlackAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 2
		Stop
	}
}

// Friendly Aura - Applied to friendly summons/monsters
Actor FriendlyAura
{
	+NoTimeFreeze
	+NoInteraction
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 2 NoDelay
		Stop
	}
}

Actor FriendlyIndicator
{
	+NoTimeFreeze
	+NoInteraction
	Scale 0.05
	States
	{
	Spawn:
		GHRT A 1 Bright A_FadeOut (0.25)
		Wait
	}
}

// Aura Skills
actor AuraPower : PowerupGiver
{
	-INVBAR
	
	+INVENTORY.AUTOACTIVATE

	Powerup.Duration 2
}
actor RedAura1 : AuraPower 		{ Powerup.Type Damage DamageFactor 2.0	}
actor RedAura2 : AuraPower 		{ Powerup.Type Damage DamageFactor 2.0	}
actor RedAura3 : AuraPower 		{ Powerup.Type InfiniteAmmo				}
actor GreenAura1 : AuraPower	{ Powerup.Type IronFeet 				}
actor PinkAura1 : AuraPower		{ Powerup.Type Drain					}

// Bullet-Time Skill
actor SkillBulletTime : PowerupGiver
{
	-INVBAR
	
	+INVENTORY.AUTOACTIVATE
	-INVENTORY.ADDITIVETIME
	
 	Powerup.Type TimeFreezer
	Powerup.Duration 60
}

// Force Field
actor ForceField
{
	Radius 32
	Height 64
	
	+SOLID
	+BRIGHT
	+NOCLIP
	+REFLECTIVE
	+DEFLECT
	
	RenderStyle Add
	Alpha 0.5
	
	States
	{
	Spawn:
		FFLD A 0
	FadeOut:
		FFLD A 1 A_FadeOut(0.001, true)
		Loop
	}
}

actor ForceField2 : ForceField
{
	Radius 64
	Height 128
	Scale 2.0
}
