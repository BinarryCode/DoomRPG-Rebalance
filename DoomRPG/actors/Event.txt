// Map Event - Termonuclear Bomb
// --------------------------------------------------

actor DRPGThermonuclearBomb
{
    Radius 16
    Height 32
    
    Scale 0.75
    
    +SOLID
    
    States
    {
    Spawn:
    Active:
        TBOM AB 6 Bright
        Loop
    Inactive:
        TNT1 A 0 A_ChangeFlag("SOLID", false)
        TBOM C -1
        Stop
    }
}

// Map Event - Low Power
// --------------------------------------------------

actor DRPGGenerator
{
    Radius 24
    Height 80
    
    Scale 0.5
    
    +SOLID
    
    States
    {
    Spawn:
    Inactive:
        GENR B 1
        Loop
    Active:
        GENR A 1 Bright
        Loop
    }
}

actor DRPGGeneratorCell : Inventory
{
    Scale 0.5
    
    -INVBAR
    
    Inventory.MaxAmount 1
    Inventory.PickupMessage "You got a Generator Power Cell."
    Inventory.PickupSound "items/vial"
    
    States
    {
    Spawn:
        GCEL ABCDEFGFEDCB 6 Bright
        Loop
    }
}

actor DRPGGeneratorCellDead : Inventory
{
    Scale 0.5
    
    -INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a dead Generator Power Cell."
    Inventory.PickupSound "items/vial"
    
    States
    {
    Spawn:
        GCEL H -1
        Stop
    }
}

// Map Event - Hell Unleashed
// --------------------------------------------------

actor DRPGPandoraBox : SwitchingDecoration
{
    Radius 16
    
    -SOLID
    +USESPECIAL
    
    Activation THINGSPEC_Activate
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SetTranslucent(0.0)
        PBOX A 1 Bright
        TNT1 A 0 A_FadeIn(0.01, FTF_CLAMP)
        TNT1 A 0 A_SpawnItemEx("DRPGBlingParticle", Random(-Radius, Radius), Random(-Radius, Radius), Random(-Height + 16, Height + 16))
        TNT1 A 0 A_Jump(Alpha >= 1.0, "Spawn2")
        Goto Spawn+1
    Spawn2:
        PBOX A -1 Bright
        Stop
    Active:
        TNT1 A 0 A_SetTranslucent(1.0)
        TNT1 A 0 A_PlaySound("pandorabox/whoosh", CHAN_BODY, 1.0, false, ATTN_NORM)
        PBOX BCDEFG 6 Bright
        TNT1 A 0 A_PlaySound("pandorabox/open1", CHAN_5, 1.0, false, ATTN_NORM)
        PBOX HI 6 Bright
        TNT1 A 0 A_PlaySound("pandorabox/open2", CHAN_7, 1.0, false, ATTN_NORM)
        PBOX JKL 6 Bright
        PBOX L 15 Bright
        TNT1 A 0 ACS_NamedExecuteAlways("HellUnleashedStart", 0)
        PBOX R 280 Bright A_PlaySound("pandorabox/doom", CHAN_5, 1.0, false, ATTN_NONE)
        TNT1 A 0 A_PlaySound("pandorabox/loop", CHAN_6, 1.0, true, 2.0)
    ActiveLoop:
        PBOX MNOPQPON 4 Bright
        Loop
    }
}
