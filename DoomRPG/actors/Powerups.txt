// Invulnerability Sphere
ACTOR DRPGInvulnerabilitySphere : InvulnerabilitySphere Replaces InvulnerabilitySphere
{
    Tag "Invulnerability Sphere - Invulnerability for 30 seconds"
    
    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "$GOTINVUL"
    Inventory.PickupSound "powerups/protect"
    Inventory.UseSound "powerups/protect"
    Inventory.Icon "PINV"
    
    Powerup.Type Invulnerable
    Powerup.Color GoldMap

    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    }
}

// Invulnerability Charge
actor DRPGInvulnerabilityCharge : PowerupGiver 20050
{
    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Tag "Invulnerability Charge - Reflective Invulnerability for 30 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invulnerability Charge!"
    Inventory.PickupSound "powerups/protect"
    Inventory.UseSound "powerups/protect"
    Inventory.Icon "CRG2"

    Powerup.Type Invulnerable
    Powerup.Mode Reflective
    Powerup.Color GoldMap

    States
    {
    Spawn:
        CRG2 ABC 3 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    } 
} 

// Invisibility Sphere
ACTOR DRPGBlurSphere : BlurSphere
{
    Tag "Blur Sphere - Partial Invisibility for 60 seconds"
    
    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    +VISIBILITYPULSE
    +FLOATBOB
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invisibility Sphere!"
    Inventory.PickupSound "powerups/invis"
    Inventory.UseSound "powerups/invis"
    Inventory.Icon "PINS"

    Powerup.Type Invisibility

    RenderStyle Translucent

    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    }
}   

// Invisibility Charge
actor DRPGInvisibilityCharge : PowerupGiver 20051
{
    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Tag "Invisibility Charge - Complete Invisibility for 30 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invisibility Charge!"
    Inventory.PickupSound "powerups/invis"
    Inventory.UseSound "powerups/invis"
    Inventory.Icon "CRG1"

    Powerup.Type Shadow
//  Powerup.Mode Stencil
    Powerup.Duration -30
    Powerup.Strength 100
    
    States
    {
    Spawn:
        CRG1 ABC 3 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    }
} 

// Time Sphere
actor DRPGTimeFreezer : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    
    Powerup.Type "TimeFreezer"
    Powerup.Color GrayMap
    Powerup.Duration -10
}

actor DRPGTimeSphere : CustomInventory 20052
{
    Tag "Time Sphere - Stops Time for 10 Seconds"
    
    Scale 0.75  
    
    +INVBAR
    +FLOATBOB
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.Pickupmessage "You got a Time Sphere!"
    Inventory.PickupSound "powerups/time"
    Inventory.Icon "TIME"
    
    States
    {
    Spawn:
        TIME ABCDCB 4 bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 2 A_SpawnItemEx("DRPGTimeFreezerSound")
        TNT1 A 2 A_GiveInventory("DRPGTimeFreezer", 1)
        Stop
    }
}

// Regeneration Sphere
actor DRPGRegenSphere : CustomInventory 20053
{
    Tag "Regeneration Sphere - Quickly regenerates HP and EP"
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Regeneration Sphere!"
    Inventory.PickupSound "powerups/regen"
    Inventory.UseSound "powerups/regen"
    Inventory.Icon "REGN"

    +INVBAR
    +FLOATBOB
    
    States
    {
    Spawn:
        REGN ABCDCB 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("HPEPMax"), "Spawn")
        TNT1 A 0 ACS_NamedExecute("RegenBoost", 0)
        Stop
    }
}

// Radiation Suit
ACTOR DRPGRadSuit : RadSuit Replaces Radsuit
{
    Tag "Radiation Suit - Protects against Damage Floors"

    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Radiation Suit!"
    Inventory.PickupSound "powerups/suit"
    Inventory.UseSound "powerups/suit"
    Inventory.Icon "SUIT"
    
    Powerup.Type IronFeet
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    }
}   

// IR Goggles
ACTOR DRPGInfrared : Infrared Replaces Infrared
{
    Tag "Night-Vision Goggles - See in the Dark"
    
    +INVBAR
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a pair of Infrared Goggles!"
    Inventory.PickupSound "powerups/light"
    Inventory.UseSound "powerups/light" 
    Inventory.Icon "PVIS"
    
    Powerup.Type LightAmp
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    }
}

// Computer Area Map and Scanner
actor DRPGAllMap : AllMap Replaces AllMap
{
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.FANCYPICKUPSOUND
    +INVENTORY.ALWAYSPICKUP
    
    Inventory.PickupMessage "You got a Computer Area Map!"
    Inventory.PickupSound "powerups/map"
    
    States
    {
    Use:
        TNT1 A 1 A_GiveInventory("DRPGAllMapRevealer", 1)
//      TNT1 A 1 A_GiveInventory("DRPGAllMapScanner", 1)
        Stop
    }
}

actor DRPGAllMapRevealer : MapRevealer {}

actor DRPGAllMapScanner : PowerupGiver
{
    +INVENTORY.AUTOACTIVATE

    Powerup.Type Scanner
    Powerup.Duration -0x7FFFFFFF
}

// Berserk Pack
actor DRPGBerserk : Berserk Replaces Berserk
{
    Tag "Berserk Pack - +100 HP and Increased Melee Damage"
    
    +INVBAR
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Berserk Pack!"
    Inventory.PickupSound "powerups/berserk"
    Inventory.UseSound "powerups/berserk"
    Inventory.Icon "PSTR"
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 2)
        TNT1 A 0 ACS_NamedExecuteAlways("AddToxicity", 0, 10)
        TNT1 A 0 A_GiveInventory("PowerStrength")
        TNT1 A 0 HealThing(100, 0)
        Stop
    }
}

// Wings
actor DRPGWings : CustomInventory 20054
{
    Tag "Wings - Allows Flight"
    
    +INVBAR
    +FLOATBOB

    Inventory.DefMaxAmount
    Inventory.PickupMessage "You got a pair of Wings!"
    Inventory.PickupSound "powerups/wings"
    Inventory.UseSound "powerups/wings"
    Inventory.Icon "WINGS"
    
    States
    {
    Spawn:
        WING A -1 Bright
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGFlightPower", 1)
        Stop
    }
}

actor DRPGFlightPower : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    
    Powerup.Type Flight
    Powerup.Duration -0x7FFFFFFF
}

// Megasphere
ACTOR DRPGMegasphere : CustomInventory Replaces MegaSphere
{
    RenderStyle Add
    
    Inventory.PickupMessage "You got a Megasphere!"
    Inventory.PickupSound "powerups/megasphere"
    
    States
    {
    Spawn:
        MEGA ABCD 6 BRIGHT
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("SoulSphere", Random(1, 3))
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, 1000000)
        TNT1 A 0 ACS_NamedExecuteAlways("AddShield", 0, 1000000)
        Stop
    }
}   

// Immunity Crystal
actor DRPGImmunityCrystal : CustomInventory
{
    Inventory.Amount 1
    Inventory.MaxAmount 1
	Inventory.PickupMessage "Got an Immunity Crystal!"
	Inventory.PickupSound "powerups/crystal"
	Inventory.Icon "CRYS"
    
    +INVBAR
    +FLOATBOB
    +BRIGHT
    
    RenderStyle Add
    
    Tag "Immunity Crystal"
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(256, "BulletMode", "MeleeMode", "FireMode", "PlasmaMode")
    BulletMode:
        CRYS AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 35 A_RadiusGive("DRPGPowerImmunityBullet", 512.0, RGF_PLAYERS)
        TNT1 A 0 A_Jump(240, "MeleeMode", "FireMode", "PlasmaMode")
        Goto BulletMode
    MeleeMode:
        CRYS BBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 35 A_RadiusGive("DRPGPowerImmunityMelee", 512.0, RGF_PLAYERS)
        TNT1 A 0 A_Jump(240, "BulletMode", "FireMode", "PlasmaMode")
        Goto MeleeMode
    FireMode:
        CRYS CCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 35 A_RadiusGive("DRPGPowerImmunityFire", 512.0, RGF_PLAYERS)
        TNT1 A 0 A_Jump(240, "MeleeMode", "BulletMode", "PlasmaMode")
        Goto FireMode
    PlasmaMode:
        CRYS DDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 35 A_RadiusGive("DRPGPowerImmunityPlasma", 512.0, RGF_PLAYERS)
        TNT1 A 0 A_Jump(240, "MeleeMode", "FireMode", "BulletMode")
        Goto PlasmaMode
    Use:
        TNT1 A 0 A_FireCustomMissile("DRPGImmunityCrystalDropped", 0, 0, 0)
        Stop
    }
}

actor DRPGImmunityCrystalDropped
{
    Damage 0
    Radius 16
    Height 8
    Speed 10
    
    Projectile
    +BRIGHT
    -NOGRAVITY
    
    States
    {
    Spawn:
        CRYS ABCD 1
        Loop
    Death:
        CRYS A 1 A_SpawnItem("DRPGImmunityCrystal", 0, 0, 0, 0)
        Stop
    }
}

actor DRPGPowerImmunityBullet : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Bullet", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityMelee : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Melee", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityFire : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Fire", 0
	Powerup.Duration 35
}

actor DRPGPowerImmunityPlasma : PowerProtection
{
    +INVENTORY.AUTOACTIVATE
    
    DamageFactor "Plasma", 0
	Powerup.Duration 35
}
