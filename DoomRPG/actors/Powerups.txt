// Invulnerability Sphere
Actor DRPGInvulnerabilityPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invulnerable
    Powerup.Color GoldMap
}

ACTOR DRPGInvulnerabilitySphere : CustomInventory Replaces InvulnerabilitySphere
{
    Tag "Invulnerability Sphere - Invulnerability for 30 seconds"
    
    Inventory.PickupSound "powerups/protect"
    Inventory.Icon "PINV"
    
    States
    {
    Spawn:
        PINV ABCD 6 Bright
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGInvulnerabilityPower", 1)
        Stop
    }
}

// Invulnerability Charge
Actor DRPGInvulnerabilityChargePower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invulnerable
    Powerup.Mode Reflective
    Powerup.Color GoldMap
}

actor DRPGInvulnerabilityCharge : CustomInventory 20050
{
    +INVBAR

    Tag "Invulnerability Charge - Reflective Invulnerability for 30 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invulnerability Charge!"
    Inventory.PickupSound "powerups/protect"
    Inventory.UseSound "powerups/protect"
    Inventory.Icon "CRG2"

    States
    {
    Spawn:
        CRG2 ABC 3 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGInvulnerabilityChargePower", 1)
        Stop
    } 
}

// Invisibility Sphere
Actor DRPGInvisibilityPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invisibility
}

ACTOR DRPGBlurSphere : CustomInventory
{
    Tag "Blur Sphere - Partial Invisibility for 60 seconds"
    
    +VISIBILITYPULSE
    +FLOATBOB

    Inventory.PickupMessage "You got an Invisibility Sphere!"
    Inventory.PickupSound "powerups/invis"
    Inventory.Icon "PINS"

    RenderStyle Translucent

    States
    {
    Spawn:
        PINS ABCD 6 Bright
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGInvisibilityPower", 1)
        Stop
    }
}   

// Invisibility Charge
Actor DRPGInvisibilityChargePower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type Invisibility
    Powerup.Mode Translucent
    Powerup.Duration -60
    Powerup.Strength 100
    +GHOST
}

actor DRPGInvisibilityCharge : CustomInventory 20051
{
    +INVBAR

    Tag "Invisibility Charge - Complete Invisibility for 60 seconds"

    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got an Invisibility Charge!"
    Inventory.PickupSound "powerups/invis"
    Inventory.UseSound "powerups/invis"
    Inventory.Icon "CRG1"

    States
    {
    Spawn:
        CRG1 ABC 3 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGInvisibilityChargePower", 1)
        Stop
    }
} 

// Time Sphere
actor DRPGTimeFreezer : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER
    
    Powerup.Type "TimeFreezer"
    Powerup.Color Black
    Powerup.Duration -10
}

actor DRPGTimeSphere : CustomInventory 20052
{
    Tag "Time Sphere - Stops Time for 10 Seconds"
    
    Scale 0.75  
    
    +INVBAR
    +FLOATBOB
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.Pickupmessage "You got a Time Sphere!"
    Inventory.PickupSound "powerups/time"
    Inventory.Icon "TIME"
    
    States
    {
    Spawn:
        TIME ABCDCB 4 bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 2 A_SpawnItemEx("DRPGTimeFreezerSound")
        TNT1 A 2 A_GiveInventory("DRPGTimeFreezer", 1)
        Stop
    }
}

// Regeneration Sphere
actor DRPGRegenSphere : CustomInventory 20053
{
    Tag "Regeneration Sphere - Quickly regenerates HP and EP"
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Regeneration Sphere!"
    Inventory.PickupSound "powerups/regen"
    Inventory.UseSound "powerups/regen"
    Inventory.Icon "REGN"

    +INVBAR
    +FLOATBOB
    
    States
    {
    Spawn:
        REGN ABCDCB 4 Bright
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("HPEPMax"), "UseFail")
        TNT1 A 0 ACS_NamedExecute("RegenBoost", 0)
        Stop
    UseFail:
        TNT1 A 0
        Fail
    }
}

// Radiation Suit
Actor DRPGRadSuitPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type IronFeet
}

ACTOR DRPGRadSuit : CustomInventory Replaces Radsuit
{
    Tag "Radiation Suit - Protects against Damaging Environments"

    +INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Radiation Suit!"
    Inventory.PickupSound "powerups/suit"
    Inventory.UseSound "powerups/suit"
    Inventory.Icon "SUIT"
    
    States
    {
    Spawn:
        SUIT A -1 Bright
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGRadSuitPower", 1)
        Stop
    }
}   

// IR Goggles
Actor DRPGInfraredPower : PowerupGiver
{
    Inventory.MaxAmount 0
    
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ADDITIVETIME
    +INVENTORY.PERSISTENTPOWER

    Powerup.Type LightAmp
}

ACTOR DRPGInfrared : CustomInventory Replaces Infrared
{
    Tag "Night-Vision Goggles - See in the Dark"
    
    +INVBAR
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a pair of Infrared Goggles!"
    Inventory.PickupSound "powerups/light"
    Inventory.UseSound "powerups/light" 
    Inventory.Icon "PVIS"
    
    States
    {
    Spawn:
        PVIS A 6 Bright
        PVIS B 6
        Loop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGInfraredPower", 1)
        Stop
    }
}

// Computer Area Map and Scanner
actor DRPGAllMap : AllMap Replaces AllMap
{
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.FANCYPICKUPSOUND
    +INVENTORY.ALWAYSPICKUP
    
    Inventory.PickupMessage "You got a Computer Area Map!"
    Inventory.PickupSound "powerups/map"
    
    States
    {
    Use:
        TNT1 A 1 A_GiveInventory("DRPGAllMapRevealer", 1)
//      TNT1 A 1 A_GiveInventory("DRPGAllMapScanner", 1)
        Stop
    }
}

actor DRPGAllMapRevealer : MapRevealer {}

actor DRPGAllMapScanner : PowerupGiver
{
    +INVENTORY.AUTOACTIVATE

    Powerup.Type Scanner
    Powerup.Duration -0x7FFFFFFF
}

// Berserk Pack
actor DRPGBerserk : Berserk Replaces Berserk
{
    Tag "Berserk Pack - +100 HP and Increased Melee Damage"
    
    +INVBAR
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    
    Inventory.DefMaxAmount
    Inventory.InterHubAmount 1000
    Inventory.PickupMessage "You got a Berserk Pack!"
    Inventory.PickupSound "powerups/berserk"
    Inventory.UseSound "powerups/berserk"
    Inventory.Icon "PSTR"
    
    States
    {
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 2)
        TNT1 A 0 ACS_NamedExecuteAlways("AddToxicity", 0, 10)
        TNT1 A 0 A_GiveInventory("PowerStrength")
        TNT1 A 0 HealThing(100, 0)
        Stop
    }
}

// Wings
actor DRPGWings : CustomInventory 20054
{
    Tag "Wings - Allows Flight"
    
    +INVBAR
    +FLOATBOB

    Inventory.DefMaxAmount
    Inventory.PickupMessage "You got a pair of Wings!"
    Inventory.PickupSound "powerups/wings"
    Inventory.UseSound "powerups/wings"
    Inventory.Icon "WINGS"
    
    States
    {
    Spawn:
        WING A -1 Bright
        Stop
    Pickup:
        TNT1 A 0 ACS_NamedExecuteWithResult("CheckCapacity")
        Stop
    Use:
        TNT1 A 0 A_GiveInventory("DRPGFlightPower", 1)
        Stop
    }
}

actor DRPGFlightPower : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    
    Powerup.Type Flight
    Powerup.Duration -0x7FFFFFFF
}

// Megasphere
ACTOR DRPGMegasphere : CustomInventory Replaces MegaSphere
{
    RenderStyle Add
    
    Inventory.PickupMessage "You got a Megasphere!"
    Inventory.PickupSound "powerups/megasphere"
    
    States
    {
    Spawn:
        MEGA ABCD 6 BRIGHT
        Loop
    Pickup:
        TNT1 A 0 A_GiveInventory("DRPGSoulSphere3")
        TNT1 A 0 ACS_NamedExecuteAlways("AddEP", 0, 1000000)
        TNT1 A 0 ACS_NamedExecuteAlways("AddShield", 0, 1000000)
        Stop
    }
}   
