
//==============================================================================
//		DEAD BODIES
//==============================================================================

ACTOR RLDeadBodySpawner replaces DeadMarine
{
	Radius 4
	Height 4
	+NOBLOCKMAP
	+NOCLIP
	+NOTONAUTOMAP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 0, "VanillaSpawn")
		TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") >= 1, "DRLASpawn")
	DRLASpawn:
		TNT1 A 0 A_SpawnItemEx ("RLDRLADeadBody", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		Stop
	VanillaSpawn:
		TNT1 A 0 A_SpawnItemEx ("RLDRLADeadBodyVanilla", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR RLDRLADeadBody : DeadMarine
{
  +CORPSE
  var int user_bodytype;
  States
  {
  Spawn:
    TNT1 A 0
// preference is given to scientists and generic marines
    TNT1 A 0 A_Jump(128, "SpawnScientistCorpse", "SpawnGenericMarineCorpse")
// randomly select a corpse type
    TNT1 A 0 A_Jump(256, "SpawnMarineCorpse", "SpawnScoutCorpse", "SpawnTechnicianCorpse", "SpawnRenegadeCorpse", "SpawnDemolitionistCorpse", "SpawnCommandoCorpse")
	Goto SpawnGenericMarineCorpse
  SpawnScientistCorpse:
// Dead scientist corpse
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLRandomPhaseDeviceSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 100)
    TNT1 A 0 A_Jump(256, 1,2,3,4,5,6,7,8) // randomly select an appearance for the scientist corpse
    FSZK N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    FSZW N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    FSZC N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    FSZS N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    SCZA N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    SCZC N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    SCZH N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    SCFE N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
  SpawnGenericMarineCorpse:
// Generic marine corpse
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 4)
    TNT1 AAA 0 A_SpawnItemEx ("RLRandomAmmoSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 70)
    FSZK N 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
	
  SpawnMarineCorpse:
// Marine corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 1)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLMarineGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 100)
    PMAR N 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
  SpawnScoutCorpse:
// Scout corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 2)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLScoutGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 130)
    PSCO N 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
  SpawnTechnicianCorpse:
// Technician corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 3)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLTechnicianGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 100)
    PTEC L 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
  SpawnRenegadeCorpse:
// Renegade corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 4)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLRenegadeGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 180)
    PREN N 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
  SpawnDemolitionistCorpse:
// Demolitionist corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 5)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLDemolitionistGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 120) 
    PDMO N 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
  SpawnCommandoCorpse:
// Commando corpse
    TNT1 A 0 A_SetUserVar("user_bodytype", 6)
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 2, 2)
    TNT1 A 0 A_SpawnItemEx ("RLCommandoNotGunSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, SXF_NOPOINTERS, 120)
    PCOM N 0 A_Jump(256, "CorpseLoop")
    Goto CorpseLoop
	
  CorpseLoop:
    "####" "#" random(5,14)
	"####" "#" 0 A_JumpIfInventory ("PowerRLCorruptionContextEffect", 1, "ResurrectCorrupt")
    Loop
  PlayerCorpseStart:
	"####" "#" 0// A_QueueCorpse // These are generated dynamically, so I wouldn't want them to spawn in their hundreds, heh.
  PlayerCorpseLoop:
    "####" "#" random(9,25)
	"####" "#" 0 A_JumpIfInventory ("PowerRLCorruptionContextEffect", 1, "ResurrectCorrupt")
    Loop
  UncrimeableCorpseLoop:
    "####" "#" -1
    Loop
	
  ResurrectCorrupt:
// Revive corrupted players
    "####" "#" 0 A_JumpIf (user_bodytype == 1, "ResurrectCorruptMarine")
    "####" "#" 0 A_JumpIf (user_bodytype == 2, "ResurrectCorruptScout")
    "####" "#" 0 A_JumpIf (user_bodytype == 3, "ResurrectCorruptTechnician")
    "####" "#" 0 A_JumpIf (user_bodytype == 4, "ResurrectCorruptRenegade")
    "####" "#" 0 A_JumpIf (user_bodytype == 5, "ResurrectCorruptDemolitionist")
//    "####" "#" 0 A_JumpIf (user_bodytype == 6, "ResurrectCorruptCommando")
	Goto UncrimeableCorpseLoop
  ResurrectCorruptMarine:
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedMarineRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    Stop
  ResurrectCorruptScout:
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedScoutRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    Stop
  ResurrectCorruptTechnician:
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedTechnicianRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    Stop
  ResurrectCorruptRenegade:
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedRenegadeRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    Stop
  ResurrectCorruptDemolitionist:
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedDemolitionistRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    Stop
  ResurrectCorruptCommando:
    TNT1 A 0// A_SpawnItemEx ("RLCorruptedCommandoRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION)
    TNT1 A 0 A_SpawnItemEx ("RLCorruptedMarineRevived", 0,0,0, 0,0,0, 0, SXF_NOPOINTERS | SXF_NOCHECKPOSITION) // Temporary
    Stop
  }
}

// These are left behind by corrupted players when killed.
ACTOR RLPlayerMarineDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Marine corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 1)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

ACTOR RLPlayerScoutDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Scout corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 2)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

ACTOR RLPlayerTechnicianDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Technician corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 3)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

ACTOR RLPlayerRenegadeDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Renegade corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 4)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

ACTOR RLPlayerDemolitionistDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Demolitionist corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 5)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

ACTOR RLPlayerCommandoDeadBody : RLDRLADeadBody
{
  States
  {
  Spawn:
    "####" "#" 0
// Commando corpse
    "####" "#" 0 A_SetUserVar("user_bodytype", 6)
    "####" "#" 0 A_Jump(256, "PlayerCorpseStart")
    Goto PlayerCorpseStart
  }
}

//==============================================================================

ACTOR RLDRLADeadBodyVanilla : DeadMarine
// Generic marine corpse
{
  States
  {
  Spawn:
    PLAY N -1
    Stop
  }
}

ACTOR RLGibbedBody1 : RLDRLADeadBody replaces GibbedMarine
// Gibbed corpse
{
  States
  {
  Spawn:
    TNT1 A 0 
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 1, 2)
    PLAY W 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    TNT1 A 0 A_SpawnItemEx ("RLRandomPhaseDeviceSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, 0, 180)
    PLAY W 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
  }
}

ACTOR RLGibbedBody2 : RLDRLADeadBody replaces GibbedMarineExtra
// Gibbed corpse
{
  States
  {
  Spawn:
    TNT1 A 0 
	TNT1 A 0 A_JumpIf(CallACS("DRLA_mapcorpsetype") == 1, 2)
    PLAY W 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
    TNT1 A 0 A_SpawnItemEx ("RLRandomPhaseDeviceSpawner", Random(-24,24),Random(-24,24),0, 0,0,0, 0, 0, 180)
    PLAY W 0 A_Jump(256, "UncrimeableCorpseLoop")
    Goto UncrimeableCorpseLoop
  }
}

//==============================================================================

actor RLScoutGunSpawner : RandomSpawner
{
	DropItem "RLClip2"                  255 40
	DropItem "RLClipBox2" 		        255 15
    DropItem "RLPistolPickup"           255 25
    DropItem "RLCombatPistolPickup"     255 10
    DropItem "RLMarksmanPistolPickup"   255 4
    DropItem "RLHandcannonPickup"       255 1
	DropItem "DRPGTurretPart"           255 5	
}

actor RLMarineGunSpawner : RandomSpawner
{
	DropItem "RLClip2"                  255 40
	DropItem "RLClipBox2" 		        255 20
    DropItem "RLChaingunPickup"         255 10
    DropItem "RLBattleRiflePickup"      255 20
    DropItem "RLUziPickup"              255 5
	DropItem "DRPGTurretPart"           255 5
}

actor RLTechnicianGunSpawner : RandomSpawner
{
	DropItem "RLClip2"                  255 40
	DropItem "RLClipBox2" 		        255 10
	DropItem "RLShellBox2" 		        255 10
    DropItem "RLCellPack2" 		        255 20
	DropItem "DRPGTurretPart"           255 20
}

actor RLRenegadeGunSpawner : RandomSpawner
{
    DropItem "RLShellBox2" 		        255 40
	DropItem "RLShotgunPickup"          255 30
    DropItem "RLDoubleShotgunPickup"    255 20
	DropItem "DRPGTurretPart"           255 9
    DropItem "RLAaaghSphere"            255 1
}

actor RLDemolitionistGunSpawner : RandomSpawner
{
    DropItem "RLRocketBox"              255 50
    DropItem "RLRocketLauncherPickup"   255 30
	DropItem "DRPGTurretPart"           255 20
}

actor RLCommandoNotGunSpawner : RandomSpawner
{
//    DropItem "RLCombatKnifePickup" 255 11
    DropItem "RLChainsawPickup" 255 9
//    DropItem "RLAxePickup" 255 3
//    DropItem "RLMachetePickup" 255 3
//    DropItem "RLMacePickup" 255 3
    DropItem "RLBerserk" 255 18
}
