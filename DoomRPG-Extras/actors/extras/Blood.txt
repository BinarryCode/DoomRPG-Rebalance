// Blood Effects from NASHGORE.WAD - Thanks to Nash Muhandes.

// Modify the internal Blood actor to spawn flying blood
actor NashGore_Blood : Blood replaces Blood
{
    game Doom
    states
    {
    Spawn:
        TNT1 AAA 0
        BLUD C 8 A_SpawnItemEx("NashGore_FlyingBlood", 0,0,0,
            VelX+FRandom (-4,4),VelY+FRandom (-4,4),VelZ+FRandom (0,8),
            0, SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION)
        BLUD B 8
        BLUD A 8
        stop
    }
}

// Blood sprite that flies outwards and leaves trails behind
actor NashGore_FlyingBlood
{
    +NotOnAutomap
    game Doom
    scale 0.75
    health 1
    radius 8
    height 1
    mass 1
    +CORPSE
    +NOTELEPORT
    +NOBLOCKMAP
    states
    {
    Spawn:
        TNT1 A 0 A_CheckSight("Cheap")
        FBLD A 1 A_SpawnItemEx("NashGore_FlyingBloodTrail", 0,0,0, 0,0,0, 0,
            SXF_TRANSFERTRANSLATION)
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        loop
    Cheap:
        FBLD A 8 A_SpawnItemEx("NashGore_FlyingBloodTrail", 0,0,0, 0,0,0, 0,
            SXF_TRANSFERTRANSLATION)
        FBLD A 1 A_JumpIf(waterlevel > 1, "Remove")
        loop
    Crash:
        TNT1 A 1 A_SpawnItem("NashGore_BloodSpot",0,0,0,1)
        stop
    Remove: // Anti-Lag - Removes blood in water.
        TNT1 A 0 
        stop
    }
}

actor NashGore_BloodSpot
{
    +NotOnAutomap
    game Doom
    radius 12
    height 2
    mass 1
    +NOTELEPORT
    +NOBLOCKMAP
    +MOVEWITHSECTOR
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("gibbage/blood",0)
        TNT1 A 0 A_Jump(64,4)
        TNT1 A 0 A_Jump(128,4)
        TNT1 A 0 A_Jump(192,4)
        TNT1 A 0 A_Jump(255,4)
        // make the blood spots stay on the ground. 1500 is the default duration.
        BSPT A 500 // A_JumpIf(waterlevel > 1, "Remove")
        Goto FadeOutA
        BSPT B 500 // A_JumpIf(waterlevel > 1, "Remove")
        Goto FadeOutB
        BSPT C 500 // A_JumpIf(waterlevel > 1, "Remove")
        Goto FadeOutC
        BSPT D 500 // A_JumpIf(waterlevel > 1, "Remove")
        Goto FadeOutD
    Remove: // Anti-Lag - Removes blood in water.
        TNT1 A 0 
        stop
    FadeOutA:
        BSPT A 1 A_Fadeout(0.0025)
        loop
    FadeOutB:
        BSPT B 1 A_Fadeout(0.0025)
        loop
    FadeOutC:
        BSPT C 1 A_Fadeout(0.0025)
        loop
    FadeOutD:
        BSPT D 1 A_Fadeout(0.0025)
        loop
    }
}

// Blood trails spawned by FlyingBlood
actor NashGore_FlyingBloodTrail
{
    +NotOnAutomap
    game Doom
    scale 0.70
    mass 1
    +LOWGRAVITY
    +NOTELEPORT
    +NOBLOCKMAP
    states
    {
    Spawn:
        BTRL A 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL B 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL C 4 A_JumpIf(waterlevel > 1, "Remove")
        BTRL D 4 A_JumpIf(waterlevel > 1, "Remove")
        stop
    Remove: // Anti-Lag - Removes blood in water.
        TNT1 A 0 
        stop
    }
}
