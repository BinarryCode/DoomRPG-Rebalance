//******
// FIST
//******

Actor DRPGFistExtras : Fist replaces Fist
{
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
	Weapon.SelectionOrder 370
	Weapon.UpSound "weapons/draw"
	Inventory.Icon "PUNPA0"
	
	+NOALERT
	
	States
	{
	Select:
		TNT1 A 0 A_Raise
		FIST A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_Lower
		FIST A 1 A_Lower
		loop
	Ready:
		FIST A 1 A_WeaponReady
		loop
	Fire:
		FIST A 1 offset(0, 35)
		FIST A 1 offset(0, 50)
	Hold:
		TNT1 A 0
		TNT1 A 0 A_playsound("weapons/fist/swing")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"HoldStrong")
		FIST CDEFG 1 offset(0, 32)
		FIST I 3 A_Custompunch(4, 0, 1, "FistPuff")
		FIST HGFEDCB 1 offset(0, 32)
		TNT1 A 0 A_Refire
		FIST A 1 offset(0, 65)
		FIST A 1 offset(0, 50)
		FIST A 1 offset(0, 35)
 		Goto Ready
	HoldStrong:
		FIST DFG 1 offset(0, 32)
		FIST I 3 A_Custompunch(40, 0, 1, "FistPuffStrong")
		FIST I 1 offset(0, 32)
		FIST I 1 offset(-1, 33)
		FIST I 1 offset(1, 31)
		FIST I 1 offset(2, 30)
		FIST GFEDC 1 offset(0, 32)
		TNT1 A 0 A_Refire
		FIST A 1 offset(0, 65)
		FIST A 1 offset(0, 50)
		FIST A 1 offset(0, 35)
 		Goto Ready
 	}
}

Actor FistPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+NOEXTREMEDEATH
        +MTHRUSPECIES
	-ALLOWPARTICLES
	
	AttackSound "weapons/fist/hitwall"
	SeeSound "weapons/fist/punch"
}

Actor FistPuffStrong : FistPuff
{
	Decal FistScorch
	
	-NOEXTREMEDEATH
	
	AttackSound "weapons/fist/strong"
	SeeSound "weapons/fist/strong"
	
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("ShotSmoke")
		Stop
	}
}
