//******
// FIST
//******

Actor DRPGFistExtras : Fist replaces Fist
{
    Inventory.Icon "PUNPA0"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Weapon.SelectionOrder 370
    Weapon.UpSound "weapons/draw"
    Weapon.BobStyle Alpha
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.7
    Weapon.BobRangeY 0.75
    
    +NOALERT
    
    States
    {
    Select:
        TNT1 A 0 A_Raise
        TNT1 A 0 A_Raise
        FIST A 1 A_Raise
        loop
    Deselect:
        TNT1 A 0 A_Lower
        TNT1 A 0 A_Lower
        FIST A 1 A_Lower
        loop
    Ready:
        FIST A 1 A_WeaponReady
        loop
    Fire:
        FIST A 1 Offset(0, 35)
        FIST A 1 Offset(0, 50)
    Hold:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("weapons/fist/swing")
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"HoldStrong")
        FIST CDEFG 1 Offset(0, 32)
        FIST I 3 A_CustomPunch(4, 0, 1, "FistPuff")
        FIST HGFEDCB 1 Offset(0, 32)
        TNT1 A 0 A_Refire
        FIST A 1 Offset(0, 65)
        FIST A 1 Offset(0, 50)
        FIST A 1 Offset(0, 35)
        Goto Ready
    HoldStrong:
        FIST DFG 1 Offset(0, 32)
        FIST I 3 A_CustomPunch(40, 0, 1, "FistPuffStrong")
        FIST I 1 Offset(0, 32)
        FIST I 1 Offset(-1, 33)
        FIST I 1 Offset(1, 31)
        FIST I 1 Offset(2, 30)
        FIST GFEDC 1 Offset(0, 32)
        TNT1 A 0 A_Refire
        FIST A 1 Offset(0, 65)
        FIST A 1 Offset(0, 50)
        FIST A 1 Offset(0, 35)
        Goto Ready
    }
}

Actor FistPuff
{
    AttackSound "weapons/fist/hitwall"
    SeeSound "weapons/fist/punch"
    
    +NOBLOCKMAP
    +NOGRAVITY
    +PUFFONACTORS
    +NOEXTREMEDEATH
    +MTHRUSPECIES
    -ALLOWPARTICLES
}

Actor FistPuffStrong : FistPuff
{
    Decal "FistScorch"
    AttackSound "weapons/fist/strong"
    SeeSound "weapons/fist/strong"
    
    -NOEXTREMEDEATH
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnItem("ShotSmoke")
        Stop
    }
}
