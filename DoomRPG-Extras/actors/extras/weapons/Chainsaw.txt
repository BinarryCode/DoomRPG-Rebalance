//**********
// CHAINSAW
//**********

Actor DRPGChainsawExtras : DRPGChainsaw replaces Chainsaw
{
    Inventory.PickupSound "weapons/csawpkup"
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.SelectionOrder 220
    Weapon.UpSound "weapons/chainsaw/draw"
    Weapon.ReadySound "blank"
    
    States
    {
    Select:
        SAWI A 1 A_Raise
        Loop
    Deselect:
        TNT1 A 0 A_PlaySound("weapons/csawpkup", CHAN_WEAPON)
        SAWI A 1 A_Lower
        Goto Deselect+1
    Ready:
        SAWI ABCDEF 1 A_WeaponReady
        SAWI ABCDEF 0 A_PlaySound("weapons/chainsaw/idle", CHAN_WEAPON)
        Goto Ready
    Fire:
        SAWG A 1 A_PlaySound("weapons/chainsaw/start", CHAN_WEAPON)
        SAWG BCD 1
    Hold:
        SAWG E 1 Offset(1, 32) A_Saw("", "weapons/sawhit", 2, "ChainsawPuffSpawner")
        SAWG F 1 Offset(-2, 33)
        SAWG G 1 Offset(2, 31)
        SAWG F 1 Offset(3, 30)
        SAWG E 1 Offset(2, 31) A_Saw("", "weapons/sawhit", 2, "ChainsawPuffSpawner")
        SAWG D 1 Offset(-1, 32)
        SAWG D 2 Offset(-2, 33)
        SAWG D 0 A_PlaySound("weapons/chainsaw/loop", CHAN_WEAPON, 1)
        SAWG D 1 A_ReFire
        SAWG B 1 A_PlaySound("weapons/chainsaw/stop", CHAN_WEAPON)
        SAWI ABCDEFABCDEFABCDEFABCDEFABCDEF 1 A_WeaponReady
        Goto Ready
    }
}

Actor ChainsawPuffSpawner
{
    +DONTSPLASH
    +NOGRAVITY
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnItem("ChainsawPuff",0,0)
        Stop
    }
}

Actor ChainsawPuff : BulletPuffed
{
    Scale 0.15
    Alpha 0.6
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_CustomMissile("WallPart", 0, 0, Random(0, 360), 2, Random(-20, 80))
        TNT1 A 0 A_CustomMissile("WallPart", 0, 0, Random(0, 360), 2, Random(-20, 80))
        TNT1 A 0 A_CustomMissile("WallPart", 0, 0, Random(0, 360), 2, Random(-20, 80))
        TNT1 A 0 A_CustomMissile("WallPart", 0, 0, Random(0, 360), 2, Random(-20, 80))
        TNT1 A 0 A_SpawnItem("ShotSmoke")
        TNT1 A 0 A_Jump(100, 2)
        TNT1 A 0 A_SpawnItem("RicochetSaw")
        BPUF ABCD 1 bright
        TNT1 A 0
        Stop
    }
}

Actor RicochetSaw : BulletSpark
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("weapons/chainsaw/hitwall")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BulletSpark", 0, 0, Random(0, 360), 2, Random(10, 60))
        Stop
    }
}
