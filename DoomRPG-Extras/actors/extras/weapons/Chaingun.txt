//**********
// CHAINGUN
//**********

Actor DRPGChaingunExtrasGiver : WeaponGiver replaces DRPGChaingun { DropItem "DRPGChaingunExtras" }

Actor DRPGChaingunExtras : DRPGChaingun replaces Chaingun
{
    Decal "BulletChip"
    Scale 0.8
    
    AttackSound "weapons/chaingun/fire"
    Inventory.pickupsound "weapons/chaingun/pickup"
    Weapon.SlotNumber 4
    Weapon.SelectionOrder 70
    Weapon.AmmoType2 "Clip"
    Weapon.AmmoUse2 1
    Weapon.UpSound "weapons/chgndraw"
    Weapon.BobStyle Alpha
    Weapon.BobSpeed 2.5
    Weapon.BobRangeX 0.5
    Weapon.BobRangeY 0.5
    
    States
    {
    Select:
        CHGG A 1 A_Raise
        Loop
    Deselect:
        CHGG A 0 A_StopSound(CHAN_BODY)
        CHGG A 1 A_Lower
        Wait
    Ready:
        CHGG A 1 A_WeaponReady
        Loop
    Fire:
        CHGG A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, -12, -18)
        CHGG A 0 A_GunFlash
        CHGF A 1 Bright Offset(0,35) A_FireBullets (5.6, 0, 1, 5, "BulletPuffed") //Exact equivalent of A_FireCGun
        CHGG B 1 Bright Offset(0,35)
        CHGG CD 1 Offset(0,35)
        CHGG A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, -12, -18)
        CHGG A 0 A_GunFlash
        CHGF A 1 Bright Offset(0,35) A_FireBullets (5.6, 0, 1, 5, "BulletPuffed") //Exact equivalent of A_FireCGun
        CHGG B 1 Bright Offset(0,35)
        CHGG CD 1 Offset(0,35)
        CHGG A 0 A_ReFire
        CHGG A 0 A_FireCustomMissile("SmokeSpawner", 0, 0, 0, -2)
        CHGG ABC 1
        CHGG D 1 Offset(0,32) A_WeaponReady
        CHGG ABCD 2 Offset(0,32) A_WeaponReady
        CHGG ABCD 3 Offset(0,32) A_WeaponReady
        Goto Ready
    AltFire:
        CHGG A 0 A_PlaySound("weapons/chaingun/start")
        CHGG ABCDABCDABCDABCD 1
        CHGG A 0 A_PlaySound("weapons/chaingun/spin", CHAN_BODY, 1)
        CHGG ACACACACAC 1

        CHGG A 0 A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON)
        CHGG A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, -12, -18)
        CHGG A 0 A_GunFlash
        CHGF A 1 Bright Offset(0,35) A_firebullets(7, 2, 1, 5, "BulletPuffed") //Equivalent of A_FireCGun but with larger spread
        CHGG C 1 Bright Offset(0,35)

        CHGG A 0 A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON)
        CHGG A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, -12, -18)
        CHGG A 0 A_GunFlash
        CHGF A 1 Bright Offset(0,35) A_firebullets(7, 2, 1, 5, "BulletPuffed")
        CHGG C 1 Bright Offset(0,35)
    
    AltHold:
        CHGG A 0 A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON)
        CHGG A 0 A_FireCustomMissile("ChaingunCasingSpawner", 0, 0, -12, -18)
        CHGG A 0 A_GunFlash
        CHGF A 1 Bright Offset(0,35) A_firebullets(7, 2, 1, 5, "BulletPuffed")
        CHGG C 1 Bright Offset(0,35)

        CHGG A 0 A_ReFire
        CHGG A 0 A_StopSound(CHAN_BODY)
        CHGG A 0 A_PlaySound("weapons/chaingun/Stop", CHAN_BODY)
        CHGG ABCD 1
        CHGG A 1 Offset(0,32) A_FireCustomMissile("SmokeSpawner",0,0,random(-3,3),random(-2,-4))
        CHGG BCDAB 2
        CHGG C 2 Offset(0,32) A_FireCustomMissile("SmokeSpawner",0,0,random(-3,3),random(-2,-4))
        CHGG DABC 3
        CHGG D 3 Offset(0,32) A_FireCustomMissile("SmokeSpawner",0,0,random(-3,3),random(-2,-4))
        Goto Ready
    AltFlash:
        TNT1 A 1 A_Light2
        TNT1 A 1 A_Light1
        Goto LightDone
    Flash:
        TNT1 A 2 A_Light2
        TNT1 A 2 A_Light1
        Goto LightDone
    }
}
