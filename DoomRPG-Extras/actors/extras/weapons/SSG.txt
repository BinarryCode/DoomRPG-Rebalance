//***************
// SUPER SHOTGUN
//***************

Actor DRPGSuperShotgunExtrasGiver : WeaponGiver replaces DRPGSuperShotgun { DropItem "DRPGSuperShotgunExtras" }

Actor DRPGSuperShotgunExtras : DRPGSuperShotgun replaces SuperShotgun
{
    Decal "BulletChip"
    
    AttackSound ""
    Inventory.PickupSound "weapons/ssg/close"
    Weapon.SlotNumber 3
    Weapon.SlotPriority 1
    Weapon.Kickback 300
    Weapon.SelectionOrder 40
    Weapon.UpSound "weapons/ssgdraw"
    Weapon.AmmoUse 1
    Weapon.AmmoUse2 1
    Weapon.AmmoType2 "Shell"
    
    States
    {
    Select:
        SH2N A 1 A_Raise
        Loop
    Deselect:
        SH2N A 1 A_Lower
        Loop
    Ready:
        SH2N A 0 A_JumpIfInventory("SSGShot", 1, 3)
        SH2N A 1 A_WeaponReady
        Wait
        SH2N A 0
        SH2N B 1 A_weaponready
        Wait
    Fire:
        SH2N A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, "FireInfinite")
        SH2N A 0 A_JumpIfInventory("DRPGDarkBlueAuraInfiniteAmmo", 1, "FireInfinite")
        SH2N A 0 A_JumpIfInventory("SSGShot", 1, "Reload1")
        SH2N A 0 A_PlaysoundEx("weapons/ssg/fire", "soundslot5")
        SH2N A 0 A_GunFlash
        SH2F X 1 Bright A_FireBullets(11.2, 7.1, 10, 5, "BulletPuffed")
        SH2N A 0 A_JumpIfNoAmmo(8)
        SH2N A 0 A_PlaySoundEx("weapons/ssg/fire","soundslot6")
        SH2N A 0 A_GunFlash
        SH2F Y 1 Bright A_FireBullets(11.2, 7.1, 10, 5, "BulletPuffed")
        SH2F A 2 A_FireCustomMissile("SmokeSpawner", 0, 0, 1, -5)
        SH2F B 2 A_FireCustomMissile("SmokeSpawner", 0, 0, -1, -5)
        SH2N C 2
        Goto Reload
        SH2N A 0
        SH2N A 0 A_Giveinventory("SSGShot", 1)
        SH2N A 0 A_FireCustomMissile("SmokeSpawner", 0, 0, 1, -2)
        SH2F CD 2
        SH2N B 2
        Goto Ready
    FireInfinite:
        SH2N A 0 A_PlaysoundEx("weapons/ssg/fire", "soundslot5")
        SH2N A 0 A_GunFlash
        SH2F X 1 Bright A_FireBullets(11.2, 7.1, 10, 5, "BulletPuffed")
        SH2N A 0 A_GunFlash
        SH2F Y 1 Bright A_FireBullets(11.2, 7.1, 10, 5, "BulletPuffed")
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, 0, -15)
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, 0, -15)
        SH2F A 7
        SH2F A 0 A_Refire
        Goto Ready
    AltFire:
        SH2N A 0 A_JumpIfInventory("PowerInfiniteAmmo", 1, "AltFireInfinite")
        SH2N A 0 A_JumpIfInventory("DRPGDarkBlueAuraInfiniteAmmo", 1, "AltFireInfinite")
        SH2N A 0 A_JumpIfInventory("SSGShot", 1, 9)
        SH2N A 0 A_GiveInventory("SSGShot", 1)
        SH2N A 0 A_PlayWeaponSound("weapons/ssg/fire")
        SH2N A 0 A_GunFlash
        SH2F X 1 Bright A_FireBullets(10, 6.5, 10, 5,"BulletPuffed")
        SH2N A 0 A_FireCustomMissile("SmokeSpawner", 0, 0, 1, -5)
        SH2F CD 2
        SH2N B 5
        Goto Ready
        SH2N A 0 A_TakeInventory("SSGShot", 1)
        SH2N A 0 A_PlayWeaponSound("weapons/ssg/fire")
        SH2N A 0 A_GunFlash
        SH2F Y 1 Bright A_FireBullets(10, 6.5, 10, 5,"BulletPuffed")
        SH2N A 0 A_FireCustomMissile("SmokeSpawner", 0, 0, -1, -5)
        SH2F AB 2
        SH2N C 5
        Goto Reload
    AltFireInfinite:
        SH2N A 0 A_PlayWeaponSound("weapons/ssg/fire")
        SH2N A 0 A_JumpIfInventory("SSGShot", 1, 7)
        SH2N A 0 A_GiveInventory("SSGShot", 1)
        SH2N A 0 A_GunFlash
        SH2F X 1 Bright A_FireBullets(10, 6.5, 10, 5,"BulletPuffed")
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, -4, -15)
        SH2F A 7
        SH2F A 0 A_Refire
        Goto Ready
        SH2N A 0 A_PlayWeaponSound("weapons/ssg/fire")
        SH2N A 0 A_TakeInventory("SSGShot", 1)
        SH2N A 0 A_GunFlash
        SH2F Y 1 Bright A_FireBullets(10, 6.5, 10, 5,"BulletPuffed")
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, -8, -15)
        SH2F A 7
        SH2F A 0 A_Refire
        Goto Ready
    Reload:
        SH2R A 0 A_CheckReload
        SH2R ABCDEFGH 1
        SH2R I 1 A_PlaySound("weapons/ssg/open")
        SH2R JKL 1
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, -4, -15)
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, -8, -15)
        SH2R M 2
        SH2R NOPR 2
        SH2R S 2 A_PlaySound("weapons/ssg/in")
        SH2R TUVWX 2
        SH2R Y 2 A_PlaySound("weapons/ssg/in")
        SH2R Z 1
        SH2S ABCDEF 1
        SH2N A 0 A_PlaySound("weapons/ssg/close")
        SH2N A 0 A_ReFire
        SH2S G 3 
        SH2S H 1
        SH2R A 1
        Goto Ready
    Reload1:
        SH2R A 0 A_JumpIfInventory ("Shell", 2, 1)
        Goto Ready
        SH2N A 0 A_TakeInventory("SSGShot")
        SH2R ABCDEFGH 1
        SH2R I 1 A_PlaySound("weapons/ssg/open")
        SH2R JKL 1
        SH2N A 0 A_FireCustomMissile("SSGCasingSpawner", 0, 0, -4, -15)
        SH2R U 3
        SH2R VWX 2
        SH2R Y 2 A_PlaySound("weapons/ssg/in")
        SH2R Z 1
        SH2S ABCDEF 1
        SH2N A 0 A_PlaySound("weapons/ssg/close")
        SH2N A 0 A_ReFire
        SH2S G 3
        SH2S H 1
        SH2R A 1
        Goto Ready
    Flash:
        TNT1 A 1 A_Light2
        TNT1 A 1 A_Light1
        Goto LightDone
    AltFlash:
        TNT1 A 2 A_Light2
        TNT1 A 2 A_Light1
        Goto LightDone
    }
}

Actor SSGShot : Inventory
{
    Inventory.MaxAmount 1
}

Actor SSGCasingSpawner : ShotgunCasingSpawner
{
    States
    {
    Spawn:
        TNT1 A 1
        Goto Death
    Death:
        TNT1 A 0 A_CustomMissile("ShotgunCasing", 0, 0, Random(80, 100), 2, Random(50, 70))
        Stop
    }
}
