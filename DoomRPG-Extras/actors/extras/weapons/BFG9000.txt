//**********
// BFG-9001
//**********

Actor DRPGBFG9000ExtrasGiver : WeaponGiver replaces DRPGBFG9000 { DropItem "DRPGBFG9000Extras" }

Actor DRPGBFG9000Extras : DRPGBFG9000 replaces BFG9000
{
    Inventory.PickupSound "weapons/bfg/pickup"
    Weapon.SlotNumber 7
    Weapon.Selectionorder 280
    Weapon.UpSound "weapons/bfg/draw"
    
    States
    {
    Select:
        BFGN A 1 A_Raise
        Loop
    Deselect:
        BFGN A 1 A_Lower
        Loop
    Ready:
        BFGN A 1 A_WeaponReady
        Loop
    Fire:
        BFGN A 0 A_PlaySound("weapons/bfg/start")
        BFGN ABCDE 4
        BFGF ABCDE 2
        BFGF F 0 A_QuakeEx (3, 3, 3, 20, 0, 128, "None", QF_SCALEDOWN)
        BFGF F 5 Bright A_FireCustomMissile("DRPGBFGBallExtras",0,1)
        BFGF EBA 2
        BFGN A 0 A_Refire
        BFGN A 0 A_PlaySound("weapons/bfg/cool")
        BFGN EEEEDDDDCCCCBBBBAAAA 1 A_FireCustomMissile("SmokeSpawner",0,0,random(-5,5),random(-4,-6))
        Goto Ready
    }
}


Actor DRPGBFGBallExtras : BFGBall replaces BFGBall
{
    DamageType "Plasma"
    RenderStyle Add
    Alpha 1.0
    Decal "BFGLightning"
    SeeSound "weapons/bfg/fire"
    DeathSound "weapons/bfg/explosion"
    Translation "192:207=112:127"
    Scale 0.8
    
    +FORCEXYBILLBOARD
    +THRUGHOST
    +MTHRUSPECIES
    +DONTREFLECT
    
    States
    {
    Spawn:
        BFGB A 0 NoDelay A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB A 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB B 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB B 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB C 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB C 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB D 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB D 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB E 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB E 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB F 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB F 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB G 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB G 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        BFGB H 0 A_CustomMissile("DRPGBFGTrailExtras",random(-24,24),random(-24,24),180)
        BFGB H 1 Bright A_SpawnItem("DRPGBFGLightTrailExtras")
        Loop
    Death:
        BFE1 A 0 A_QuakeEx (4, 4, 4, 34, 0, 256, "None", QF_SCALEDOWN)
        BFE1 AB 8 Bright
        BFE1 C 8 Bright A_BFGSpray
        BFE1 DEF 8 Bright
        Stop
    }
}

Actor DRPGBFGLightTrailExtras
{
    RenderStyle Add
    Scale 4
    Alpha 0.05
    //Translation "192:207=112:127"
    
    +FORCEXYBILLBOARD
    +NOINTERACTION
    
    States
    {
    Spawn:
        LENG A 7 Bright NoDelay A_FadeOut(0.01)
        Loop
    }
}

Actor DRPGBFGTrailExtras
{
    RenderStyle Add
    Scale 0.25
    Alpha 0.3
    Translation "192:207=112:127"
    
    +FORCEXYBILLBOARD
    +NOINTERACTION
    
    States
    {
    Spawn:
        BFGB ABCDEFGH 2 Bright NoDelay A_FadeOut(0.01)
        Loop
    }
}

actor DRPGBFGExtraExtras : BFGExtra replaces BFGExtra
{
    DamageType "Plasma"
    RenderStyle Add
    Alpha 0.75
    
    +NOINTERACTION
    
    States
    {
    Spawn:
        BFE2 ABCD 8 Bright
        Stop
    }
}
