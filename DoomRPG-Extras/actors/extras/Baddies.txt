//Making Existing Monsters "Gush"

// The Zombieman. He's kind of a dick.
actor SplattyZombieMan : Zombieman replaces Zombieman
{
+slidesonwalls
	decal BulletChip
	States
    {
       Spawn:
		POSS A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       POSS A 1 A_Look
       Goto Spawn+1
       See:
       POSS A 4 A_Chase
       POSS A 4 A_Chase
       POSS B 3 A_Chase
       POSS B 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       POSS C 4 A_Chase
       POSS C 4 A_Chase
       POSS D 3 A_Chase
       POSS D 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       loop
       Missile:
       POSS E 10 A_FaceTarget
       POSS F 0 A_PosAttack
       POSS F 8 A_CustomMissile("9mmCasing",30,0,135+random(-15,15),2,35+random(-15,15))
       POSS E 8 
       goto See
       Pain:
       POSS G 3
       POSS G 3 A_Pain
       goto See
       Death:
       POSS G 3 A_Jump (120, "Death2")
       POSS H 5
       POSS I 5 A_Scream
       POSS J 5 A_NoBlocking
       POSS K 5
       POSS L 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       POSS L -1 
       stop
       Death2:
       POSS V 5
       POSS W 5 A_Scream
       POSS X 5 A_NoBlocking
       POSS Y 5
       POSS Z 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       POSS Z -1 
       stop
       Death.PlasmaDamage:
	SPOS M 5
	SPOS N 5 A_XScream 
    SPOS N 0 A_SpawnItem("GIBBER2")
	SPOS O 5 A_NoBlocking  
	SPOS P 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS Q 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS R 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS S 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS T 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS U -1 
	Stop
       XDeath:
       NULL A 0	A_Jump (160, "XDeath2")
       NULL A 0 A_Fall
       // Unleash The Gibs!
        NULL A 0 A_PlaySound("gibbage/xsplat")
        NULL A 0 A_SpawnItem("GIBBER")
        NULL A 1 A_CustomMissile ("ZOMBIEMANHEAD", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_CustomMissile ("ZOMBIEMANLEG", random(0,360), 0, 33, 2, 45)
         NULL A 1 A_CustomMissile ("ZOMBIEMANLEG", random(0,360), 0, -33, 2, 27)
       Stop
	   XDeath2:
	SPOS M 5 A_SpawnItem("GIBBER3")
	SPOS N 5 A_XScream 
	SPOS O 5 A_NoBlocking  
	SPOS P 5
	SPOS Q 5
	SPOS R 5 
	SPOS S 5
	SPOS T 5 
	SPOS U -1 
	Stop  
    }
}

// The Shotgunner. STOP MAKING HIM OVERPOWERED YOU ASSHATS.
actor SplattyShotgunGuy : ShotgunGuy replaces ShotgunGuy
{
decal BulletChip
	States
    {
       Spawn:
		SPOS A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       SPOS A 1 A_Look
       Goto Spawn+1
       See:
       SPOS A 4 A_Chase
       SPOS A 4 A_Chase
       SPOS B 3 A_Chase
       SPOS B 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       SPOS C 4 A_Chase
       SPOS C 4 A_Chase
       SPOS D 3 A_Chase
       SPOS D 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       loop
       Missile:
       SPOS E 10 A_FaceTarget
       SPOS F 0 bright A_SPosAttack
       SPOS F 10 bright 
       SPOS E 0 A_PlaySound("weapons/shotgun/pump")
       SPOS E 10 A_CustomMissile("ShotgunCasing",30,0,135+random(-15,15),2,35+random(-15,15))
       goto See
       Pain:
       SPOS G 3
       SPOS G 3 A_Pain
       goto See
       Death:
       SPOS G 3 A_Jump (120, "Death2")
       SPOS H 5
       SPOS I 5 A_Scream
       SPOS J 5 A_NoBlocking
       SPOS K 5
       SPOS L 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SPOS L -1 
       stop
       Death2:
       SPOS V 5
       SPOS W 5 A_Scream
       SPOS X 5 A_NoBlocking
       SPOS Y 5
       SPOS Z 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SPOS Z -1 
       stop
       Death.PlasmaDamage:
	SPOS M 5
	SPOS N 5 A_XScream
    SPOS N 0 A_SpawnItem("GIBBER2")
	SPOS O 5 A_NoBlocking
	SPOS P 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SPOS Q 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SPOS R 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SPOS S 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SPOS T 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SPOS U -1
	Stop
       XDeath:
	   NULL A 0	A_Jump (160, "XDeath2")
       NULL A 0 A_Fall
       // Unleash The Gibs! 
        NULL A 0 A_PlaySound("gibbage/xsplat")
        NULL A 1 A_CustomMissile ("SHOTGUYHEAD", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_CustomMissile ("SHOTGUYARM", 25, 0, random(0,360), 2, 50)
        NULL A 1 A_CustomMissile ("SHOTGUYARM", 36, 0, random(0,360), 2, 32)
        NULL A 0 A_SpawnItem("GIBBER")
       Stop
	   XDeath2:
	SPOS M 5 A_SpawnItem("GIBBER3")
	SPOS N 5 A_XScream
	SPOS O 5 A_NoBlocking
	SPOS P 5 
    SPOS Q 5 
    SPOS R 5 
    SPOS T 5 
	SPOS U -1
	Stop
    }
}

// The Imp. Show me on the doll where he touched you.
actor SplattyDoomImp : DoomImp replaces DoomImp
{
	States
    {
       Spawn:
		TROO A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       TROO A 1 A_Look
       Goto Spawn+1
    See:
       TROO A 4 A_Chase
       TROO A 4 A_Chase
       TROO B 3 A_Chase
       TROO B 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       TROO C 4 A_Chase
       TROO C 4 A_Chase
       TROO D 3 A_Chase
       TROO D 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       loop
    Melee:
    Missile:
    TROO EF 8 A_FaceTarget
    TROO G 6 A_TroopAttack
    goto See
    Pain:
    TROO H 2
    TROO H 2 A_Pain
    goto See
    Death:
    TROO I 0 A_Jump(120, "Death2")
    TROO I 8
    TROO J 8 A_Scream
    TROO K 6
    TROO L 6 A_NoBlocking
    TROO M 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    TROO M -1 
    stop
    Death2:
    TROO V 6
    TROO W 6 A_Scream
    TROO X 6 A_NoBlocking
    TROO Y 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    TROO Y -1 
    stop
    Death.PlasmaDamage:
	TROO N 5
	TROO O 5 A_XScream
    TROO O 0 A_SpawnItem("GIBBER2")
	TROO P 5 A_NoBlocking
    TROO Q 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	TROO R 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	TROO S 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    TROO T 5
	TROO U -1
	Stop
       XDeath:
	   NULL A 0	A_Jump (160, "XDeath2")
       NULL A 0 A_Fall
       // Unleash The Gibs! 
        NULL A 0 A_PlaySound("gibbage/xsplat")
        NULL A 1 A_CustomMissile ("IMPHEAD", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_CustomMissile ("IMPCLAW", 11, 0, random(0,360), 2, 45)
        NULL A 1 A_CustomMissile ("IMPCLAW", 11, 0, random(0,360), 2, 27)
        NULL A 0 A_SpawnItem("GIBBER")
       Stop
	XDeath2:  
	TROO N 5 A_SpawnItem("GIBBER3")
	TROO O 5 A_XScream
	TROO P 5 A_NoBlocking
    TROO Q 5 
	TROO R 5 
	TROO S 5 
    TROO T 5
	TROO U -1
	Stop
    }
}

// The chaingunner. Fat, bearded... probably a goon.
actor SplattyChaingunner : ChaingunGuy replaces ChaingunGuy
{
decal BulletChip
	States
    {
       Spawn:
		CPOS A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       CPOS A 1 A_Look
       Goto Spawn+1
       See:
       CPOS A 4 A_Chase
       CPOS A 4 A_Chase
       CPOS B 3 A_Chase
       CPOS B 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       CPOS C 4 A_Chase
       CPOS C 4 A_Chase
       CPOS D 3 A_Chase
       CPOS D 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       loop
       Missile:
       CPOS E 10 A_FaceTarget
       CPOS F 0 bright A_CPosAttack
       CPOS F 4 bright A_CustomMissile("9mmCasing",30,0,135+random(-15,15),2,35+random(-15,15))
       CPOS E 0 bright A_CPosAttack
       CPOS E 4 bright A_CustomMissile("9mmCasing",30,0,135+random(-15,15),2,35+random(-15,15))
       CPOS F 1 A_CPosRefire
       goto Missile+1
       Pain:
       CPOS G 3
       CPOS G 3 A_Pain
       goto See
       Death:
       CPOS H 1 A_Jump(120, "Death2")
       CPOS H 1 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 0 A_SpawnItem("Blood")
       CPOS H 4
       CPOS I 5 A_Scream
       CPOS J 5 A_NoBlocking
       CPOS KLM 5
       CPOS N 1 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       CPOS N -1 
       stop
       Death2:
       CPOD A 1 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 0 A_SpawnItem("Blood")
       CPOD A 6 
       CPOD B 6 A_Scream
       CPOD C 6 A_NoBlocking
       CPOD DE 6
       CPOD F 0 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       CPOD F -1 
       stop
       Death.PlasmaDamage:
	CPOS O 5 
	CPOS P 5 A_XScream
    CPOS P 0 A_SpawnItem("GIBBER2")
	CPOS Q 5 A_NoBlocking
    CPOS Q 0 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    CPOS R 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	CPOS S 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	CPOS T -1
	Stop
       XDeath:
	   NULL A 0	A_Jump (160, "XDeath2")
       NULL A 0 A_Fall 
       // Unleash The Gibs!
        NULL A 0 A_PlaySound("gibbage/xsplat")
        NULL A 1 A_CustomMissile ("CHAINGUNNERHEAD", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_SpawnItem("GIBBER")
       Stop
	XDeath2:
	CPOS O 5 A_SpawnItem("GIBBER3")
	CPOS P 5 A_XScream
	CPOS Q 5 A_NoBlocking
    CPOS Q 0
    CPOS R 5 
	CPOS S 5 
	CPOS T -1
	Stop
    }
}

actor SplattyNazi : WolfensteinSS replaces WolfensteinSS
{
decal BulletChip
	States
    {
       Spawn:
		SSWV A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       SSWV A 1 A_Look
       Goto Spawn+1
	Pain:
	SSWV H 3
	SSWV H 3 A_PlaySound("nazi/pain") 
       goto See
       Death.PlasmaDamage:
	SSWV N 5 
	SSWV O 5 A_XScream
    SSWV O 0 A_SpawnItem("GIBBER2")
	SSWV P 5 A_NoBlocking
	SSWV Q 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SSWV R 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SSWV S 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SSWV T 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
    SSWV U 5 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
	SSWV V -1
	Stop
       XDeath:
	   SSWV N 5
	   NULL A 0	A_Jump (160, "XDeath2")
       NULL A 0 A_Fall 
NULL A 0 A_SpawnItem("GIBBER")
       Stop
	XDeath2:
	SSWV N 5 A_SpawnItem("GIBBER3")
	SSWV O 5 A_XScream
	SSWV P 5 A_NoBlocking
	SSWV Q 5 
    SSWV R 5 
    SSWV S 5 
    SSWV T 5 
    SSWV U 5 
	SSWV V -1
	Stop
    }
}

// "Strange, those didn't gib before..."
actor SplattyPinky : Demon replaces Demon
{
	gibhealth	50
	States
    {
       Spawn:
		SARG A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       SARG A 1 A_Look
       Goto Spawn+1
       See:
       SARG A 2 A_Chase
       SARG A 2 A_Chase
       SARG B 1 A_Chase
       SARG B 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       SARG C 2 A_Chase
       SARG C 2 A_Chase
       SARG D 1 A_Chase
       SARG D 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       loop
       Melee:
       SARG EF 8 A_FaceTarget
       SARG G 8 A_SargAttack
       goto See
       Pain:
       SARG H 2
       SARG H 2 A_Pain
       goto See
       Death:
       SARG H 0 A_Jump (120, "Death2")
       SARG I 8
       SARG J 8 A_Scream
       SARG K 4
       SARG L 4 A_NoBlocking
       SARG M 4 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SARG N -1
       stop
    	Death2:
	SAAR A 2 A_Scream
	SAAR A 0 A_CustomMissile("PINKYARM",32,0,235)
	SAAR A 3
	SAAR B 7
	SAAR C 6
	SAAR D 5 A_NoBlocking
	SAAR E 8
	SAAR F -1
	Stop 
       Death.PlasmaDamage:
       SARG O 4 A_Scream
       SARG P 4 A_SpawnItem("GIBBER2")
       SARG Q 4 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
       SARG R 4 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
       SARG R 0 A_NoBlocking
       SARG S 4 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SARG T -1
       stop
       XDeath:
	   NULL A 0 A_Jump (160, "XDeath2")
       NULL A 0 A_Fall 
       // Unleash The Gibs!
        NULL A 0 A_PlaySound("gibbage/xsplat") 
        NULL A 1 A_CustomMissile ("PINKYHEAD", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_CustomMissile ("PINKYLEG", 40, 0, random(0,360), 2, 24)
         NULL A 1 A_CustomMissile ("PINKYLEG", 34, 0, random(0,360), 2, 64)
        NULL A 0 A_SpawnItem("GIBBER")
       Stop
	   XDeath2: 
	SAR2 O 5 A_SpawnItem("GIBBER3")
    SAR2 P 5 A_XScream
    SAR2 Q 5 A_NoBlocking
    SAR2 RSTUV 5
    SAR2 W -1
    Stop
    }
}

// I hate these things.
actor SplattyFuzzy : SplattyPinky replaces Spectre
  {
  spawnid 9
  hitobituary "%o was surprised by a spectre."
  BloodColor Black
  renderstyle OptFuzzy
  alpha 0.2
  seesound "spectre/sight"
  attacksound "spectre/melee"
  painsound "spectre/pain"
  deathsound "spectre/death"
  activesound "spectre/active"
  +SHADOW
	gibhealth	50
	States
    {
       Spawn:
		SARG A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       SARG A 1 A_Look
       Goto Spawn+1
       See:
       SARG A 2 A_Chase
       SARG A 2 A_Chase
       SARG B 1 A_Chase
       SARG B 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       SARG C 2 A_Chase
       SARG C 2 A_Chase
       SARG D 1 A_Chase
       SARG D 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       loop
       Melee:
       SARG EF 8 A_FaceTarget
       SARG G 8 A_SargAttack
       goto See
       Pain:
       SARG H 2
       SARG H 2 A_Pain
       goto See
       Death:
       SARG H 0 A_Jump (120, "Death2")
       SARG I 8
       SARG J 8 A_Scream
       SARG K 4
       SARG L 4 A_NoBlocking
       SARG M 4 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SARG N -1
       stop
    	Death2:
	SAAR A 2 A_Scream
	SAAR A 0 A_CustomMissile("PINKYARM2",32,0,235)
	SAAR A 3
	SAAR B 7
	SAAR C 6
	SAAR D 5 A_NoBlocking
	SAAR E 8
	SAAR F -1
	Stop 
       Death.PlasmaDamage:
       SARG O 4 A_Scream
       SARG P 4 A_SpawnItem("SPECGIBBER2")
       SARG Q 4 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
       SARG R 4 A_SpawnItem("FlameSmoke",random(-16,16),random(-16,16),random(8,16))
       SARG R 0 A_NoBlocking
       SARG S 4 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
       SARG T -1
       stop
       XDeath:
	   NULL A 0 A_Jump (160, "XDeath2")
       NULL A 0 A_Fall 
       // Unleash The Gibs!
        NULL A 0 A_PlaySound("gibbage/xsplat") 
        NULL A 1 A_CustomMissile ("PINKYHEAD2", 32, 0, random(0,360), 2, 150)
        NULL A 0 A_CustomMissile ("PINKYLEG2", 40, 0, random(0,360), 2, 24)
         NULL A 1 A_CustomMissile ("PINKYLEG2", 34, 0, random(0,360), 2, 64)
        NULL A 0 A_SpawnItem("SPECGIBBER")
       Stop
	   XDeath2: 
	SAR2 O 5 A_SpawnItem("SPECGIBBER3")
    SAR2 P 5 A_XScream
    SAR2 Q 5 A_NoBlocking
    SAR2 RSTUV 5
    SAR2 W -1
    Stop
    }
}


// But I hate these more! Revenants.
ACTOR Skeleton : Revenant replaces Revenant
{
States
 {
   Spawn:
	SKEL A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    SKEL A 1 A_Look
    Goto Spawn+1
       See:
       SKEL A 2 A_Chase
       SKEL A 2 A_Chase
       SKEL B 1 A_Chase
       SKEL B 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       SKEL C 2 A_Chase
       SKEL C 2 A_Chase
       SKEL D 1 A_Chase
       SKEL D 1 A_SpawnItem ("Footstep", 0, 0, 0, 0)
       loop
 Missile:
   SKEL J 0 bright A_Jump(128,4)
   SKEL J 10 bright A_FaceTarget
   SKEL K 10 A_CustomMissile("RevMissile1", 48, 0, 0)
   SKEL K 10 A_FaceTarget
   goto See
   SKEL J 10 bright A_FaceTarget
   SKEL K 10 A_CustomMissile("RevMissile2", 48, 0, 0)
   SKEL K 10 A_FaceTarget
   goto See
  Pain:
    SKEL L 5
    SKEL L 5 A_Pain
    goto See
  Death:
    SKEL L 0 A_jump (120, "Death2")
    SKEL LM 7
    SKEL N 7 A_Scream
    SKEL O 7 A_NoBlocking
    SKEL P 7 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    SKEL Q -1
    stop
  Death2:
    REVH E 5
    REVH F 5 A_Scream
    REVH GH 5 
    REVH I 5 A_NoBlocking
    REVH J 5
    REVH K 5 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    REVH L -1
    stop
 }
}

// Archvile, or grandparent? Depends, is it setting you alight,
// or throwing out your animes?
actor VaguelySplattyArchvile : Archvile replaces Archvile
{
   States
   {
       Spawn:
	VILE A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    VILE A 1 A_Look
    Goto Spawn+1
       See:
       VILE A 2 A_Chase
       VILE A 2 A_Chase
       VILE B 1 A_Chase
       VILE B 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       VILE C 2 A_Chase
       VILE C 2 A_Chase
       VILE D 1 A_Chase
       VILE D 1 A_SpawnItem ("Fleshstep", 0, 0, 0, 0)
       loop
  Death:
        VILE Q 1 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
        VILE Q 0 A_CustomMissile ("GIB2SPLAT", 30, 0, random(0,360), 2, 12)
        VILE Q 0 A_CustomMissile ("GIB2", 34, 0, random(0,360), 2, 64)
        VILE Q 0 A_CustomMissile ("GIB3", 25, 0, random(0,360), 2, 50)
        VILE Q 1 A_CustomMissile ("GIB3SPLAT", 36, 0, random(0,360), 2, 32)
        VILE Q 0 A_CustomMissile ("GIB1", 40, 0, random(0,360), 2, 24)
        VILE Q 0 A_CustomMissile ("GIB1SPLAT", 38, 0, random(0,360), 2, 48)
        VILE Q 0 A_CustomMissile ("GIB3", 32, 0, random(0,360), 2, 32)
        VILE Q 1 A_CustomMissile ("GIB3", 32, 0, random(0,360), 2, 12)
        VILE Q 0 A_CustomMissile ("GIB4", 32, 0, random(0,360), 2, 36)
        VILE Q 0 A_CustomMissile ("GIB4SPLAT", 40, 0, random(0,360), 2, 21)
        VILE Q 1 A_CustomMissile ("GIB4", 20, 0, random(0,360), 2, 55)
        VILE Q 0 A_CustomMissile ("GIB5SPLAT", 32, 0, random(0,360), 2, 66)
        VILE Q 0 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
        VILE Q 0 A_CustomMissile ("GIB3", 30, 0, random(0,360), 2, 12)
        VILE Q 1 A_CustomMissile ("GIB2", 34, 0, random(0,360), 2, 64)
    VILE Q 5 A_Scream
    VILE R 5 
    VILE S 5 A_NoBlocking
    VILE T 5 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    VILE UVWXY 5
    VILE XY 5
    VILE Z -1 
    stop

  Death.BFGDamage:
        VIGI A 1 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
        VIGI A 0 A_CustomMissile ("GIB2SPLAT", 30, 0, random(0,360), 2, 12)
        VIGI A 0 A_CustomMissile ("GIB2", 34, 0, random(0,360), 2, 64)
        VIGI A 0 A_CustomMissile ("GIB3", 25, 0, random(0,360), 2, 50)
        VIGI A 1 A_CustomMissile ("GIB3SPLAT", 36, 0, random(0,360), 2, 32)
        VIGI A 0 A_CustomMissile ("GIB1", 40, 0, random(0,360), 2, 24)
        VIGI A 0 A_CustomMissile ("GIB1SPLAT", 38, 0, random(0,360), 2, 48)
        VIGI A 0 A_CustomMissile ("GIB3", 32, 0, random(0,360), 2, 32)
        VIGI A 1 A_CustomMissile ("GIB3", 32, 0, random(0,360), 2, 12)
        VIGI A 0 A_CustomMissile ("GIB4", 32, 0, random(0,360), 2, 36)
        VIGI A 0 A_CustomMissile ("GIB4SPLAT", 40, 0, random(0,360), 2, 21)
        VIGI A 1 A_CustomMissile ("GIB4", 20, 0, random(0,360), 2, 55)
        VIGI A 0 A_CustomMissile ("GIB5SPLAT", 32, 0, random(0,360), 2, 66)
        VIGI A 0 A_CustomMissile ("GIB2", 32, 0, random(0,360), 2, 32)
        VIGI A 0 A_CustomMissile ("GIB3", 30, 0, random(0,360), 2, 12)
        VIGI A 1 A_CustomMissile ("GIB2", 34, 0, random(0,360), 2, 64)
    VIGI A 5 A_XScream
    VIGI B 5 
    VIGI C 5 A_NoBlocking
    VIGI DEF 5 
    VIGI GHI 5 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    VIGI J -1 
    stop
        }
}

// The mancubus. Reminds me of Wolf3d bosses for some reason.
// Also probably a goon.
actor HalfSplattyFatass : Fatso replaces Fatso
{
 states
  {
  Spawn:
		FATT A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    FATT G 1 A_Look
    Goto Spawn+1
         See:
       FATT A 4 A_Chase
       FATT A 4 A_Chase
       FATT B 4 A_Chase
       FATT B 4 A_Chase
       FATT C 4 A_Chase
       FATT C 3 A_Chase
       FATT D 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       FATT D 4 A_Chase
       FATT E 4 A_Chase
       FATT E 4 A_Chase
       FATT F 3 A_Chase
       FATT F 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       loop
    Missile:
    FATT G 20 A_FatRaise
    FATT H 10 bright A_FatAttack1  // See FatShot
    FATT IG 5 A_FaceTarget
    FATT H 10 bright A_FatAttack2
    FATT IG 5 A_FaceTarget
    FATT H 10 bright A_FatAttack3
    FATT IG 5 A_FaceTarget
    goto See
  Pain:
    FATT J 3
    FATT J 3 A_Pain
    goto See
    Death:
    FATT K 0 A_jump (120, "Death2")
    FATT K 0 A_SpawnItem("Blood")
    FATT K 0 A_SpawnItem("Blood")
    FATT K 6 A_SpawnItem("Blood")
    FATT L 6 A_Scream
    FATT M 6 A_NoBlocking
    FATT N 6
    FATT O 6 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    FATT P 6
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 0 A_SpawnItem("Blood")
    FATT Q 6
    FATT R 6
    FATT S 6
    FATT T -1 A_BossDeath
    stop
    Death2:
    FATK A 0 A_SpawnItem("Blood")
    FATK A 0 A_SpawnItem("Blood")
    FATK A 8 A_SpawnItem("Blood")
    FATK B 4 A_Scream
    FATK B 4 A_NoBlocking
    FATK C 8 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    FATK D 8
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK D 0 A_SpawnItem("Blood")
    FATK E 8
    FATK F -1 A_BossDeath
    stop
    }
}

// Blood Color Fix (thanks AFADoomer!) 

// Bruiser Retarded Second Cousin!
ACTOR DietBaron : HellKnight replaces HellKnight
{
   BloodColor "0 78 0"
      	States
    {
        Spawn:
		BOS2 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       BOS2 A 1 A_Look
       Goto Spawn+1
       See:
       BOS2 A 4 A_Chase
       BOS2 A 4 A_Chase
       BOS2 B 3 A_Chase
       BOS2 B 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       BOS2 C 4 A_Chase
       BOS2 C 4 A_Chase
       BOS2 D 3 A_Chase
       BOS2 D 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       loop
      Melee:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 8 A_BruisAttack  // See BaronBall
    goto See
      Missile:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 0 A_PlaySound("baron/attack")
    BOS2 G 8 A_BruisAttack  // See BaronBall
    goto See
     Death:
    BOS2 I 0 A_Jump (120, "Death2")
    BOS2 I 8
    BOS2 J 8 A_Scream
    BOS2 K 8 A_SpawnItem("BARONGIBBER")
    BOS2 L 8 A_NoBlocking
    BOS2 M 8 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BOS2 N 8
    BOS2 O -1 A_BossDeath
    stop
     Death2:
    BOS2 P 8
    BOS2 Q 8 A_Scream
    BOS2 R 8 A_SpawnItem("BARONGIBBER")
    BOS2 S 8 A_NoBlocking
    BOS2 T 8 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BOS2 U 8
    BOS2 V -1 A_BossDeath
	stop
	  Death.BFGDamage:
    BO22 P 5
    BO22 Q 5 A_XScream
    BO22 R 5 A_SpawnItem("BARONGIBBER")
    BO22 S 5 A_NoBlocking
    BO22 TU 5
    BO22 V 5 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BO22 W 5 
    BO22 X -1 A_BossDeath
    Stop
    }
}

// Insert joke about head here.
ACTOR Caco : Cacodemon replaces Cacodemon
{
   BloodColor Blue
  States
  {
  Spawn:
	HEAD A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
	HEAD A 10 A_Look
	Goto Spawn+1
  Death:
    HEAD G 0 A_Jump (85, "Death2", "Death3")
    HEAD G 8 
    HEAD H 8 A_Scream
    HEAD I 8 A_SpawnItemEx("CacoEye",random(-4,4),random(-4,4),random(8,32),random(3,7),0,random(0,5),random(-10,10))
    HEAD J 8 A_SpawnItem("CACOGIBBER")
    HEAD K 8 A_NoBlocking
    HEAD L -1 A_SetFloorClip
    stop
  Death2:
    HEAD M 8 
    HEAD N 8 A_Scream
    HEAD O 8 A_SpawnItemEx("CacoEye",random(-4,4),random(-4,4),random(8,32),random(3,7),0,random(0,5),random(-10,10))
    HEAD P 8 A_SpawnItem("CACOGIBBER")
    HEAD Q 8 A_NoBlocking
    HEAD R 8
    HEAD S -1 A_SetFloorClip
    stop
  Death3:
    HEAD TUVW 5 
    HEAD X 7 A_NoBlocking
    HEAD Y 8 A_SpawnItemEx("CacoEye",random(-4,4),random(-4,4),random(8,32),random(3,7),0,random(0,5),random(-10,10))
    HEAA A 8 A_Scream
    HEAA B 8 A_SpawnItem("CACOGIBBER")
    HEAA CD 8
    HEAA E -1 A_SetFloorClip
    stop
    }
}

// Rolled up newspaper, anyone?
actor SplattyArachnotron replaces Arachnotron
{
  spawnid 6
  obituary "$OB_BABY"
  health 500
  radius 64
  height 64
  mass 600
  speed 12
  painchance 128
  seesound "baby/sight"
  painsound "baby/pain"
  deathsound "baby/death"
  activesound "baby/active"
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
	BSPI A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    BSPI A 1 A_Look
    Goto Spawn+1
  See:
    BSPI A 20
    BSPI A 3 A_BabyMetal
    BSPI ABBCC 3 A_Chase
    BSPI D 3 A_BabyMetal
    BSPI DEEFF 3 A_Chase
    goto See+1
  Missile:
    BSPI A 20 bright A_FaceTarget
    BSPI G 4 bright A_BspiAttack    // See ArachnotronPlasma
    BSPI H 4 bright
    BSPI H 1 bright A_SpidRefire
    goto Missile+1
  Pain:
    BSPI I 3
    BSPI I 3 A_Pain
    goto See+1
  Death:
    BSPI J 8 A_Jump (120, "Death2")
    BSPI J 0 A_Scream
    BSPI J 5 A_CustomMissile ("GIB1", 32, 0, random(0,360), 2, 32)
    BSPI J 5 A_CustomMissile ("GIB2SPLAT", 30, 0, random(0,360), 2, 12)
    BSPI J 5 A_CustomMissile ("GIB3", 34, 0, random(0,360), 2, 64)
    BSPI J 5 A_CustomMissile ("GIB4", 25, 0, random(0,360), 2, 50)
    BSPI K 7 A_NoBlocking
    BSPI LM 7
    BSPI N 7 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    BSPI O 7
    BSPI P -1 A_BossDeath
    stop
   Death2:
    XBSP A 0 A_Scream
    XBSP A 2 A_SpawnItemEx("Spiderleg", 0, 0, 0, 5, 5, 5, 0, 0, 0)
    XBSP A 2 A_CustomMissile ("GIB1", 32, 0, random(0,360), 2, 32)
    XBSP A 2 A_CustomMissile ("GIB2SPLAT", 30, 0, random(0,360), 2, 12)
    XBSP A 2 A_CustomMissile ("GIB3", 34, 0, random(0,360), 2, 64)
    XBSP A 2 A_CustomMissile ("GIB4", 25, 0, random(0,360), 2, 50)
    XBSP B 4 A_NoBlocking
    XBSP C 4 A_SpawnItem ("VileThud", 0, 0, 0, 0)
    XBSP D -1 A_BossDeath
    stop
  Raise:
    BSPI PONMLKJ 5
    goto See+1
  }
}

// The Little Jerk
actor LostSoul2 : LostSoul replaces LostSoul
{
	+FLOATBOB
  states
  {
  Spawn:
	SKUL A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NOXP + MF_NOAURA + MF_NODROPS)
	SKUL AB 10 BRIGHT A_Look
	Goto Spawn+1
  Death:
    SKUL F 6 bright
    SKUL G 6 bright A_Scream
    SKUL H 6 bright A_NoBlocking
    SKUL I 6 bright A_Explode(5)
    SKUL JK 6
    stop
  }
}

// Bruiser Brother!
ACTOR Baron : BaronOfHell replaces BaronOfHell
{
   BloodColor "0 78 0"
   	States
    {
       Spawn:
		BOSS A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
       BOSS A 1 A_Look
       Goto Spawn+1
       See:
       BOSS A 4 A_Chase
       BOSS A 4 A_Chase
       BOSS B 3 A_Chase
       BOSS B 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       BOSS C 4 A_Chase
       BOSS C 4 A_Chase
       BOSS D 3 A_Chase
       BOSS D 1 A_SpawnItem ("BigStep", 0, 0, 0, 0)
       loop
      Melee:
    BOSS EF 8 A_FaceTarget
    BOSS G 8 A_BruisAttack  // See BaronBall
    goto See
      Missile:
    BOSS EF 8 A_FaceTarget
    BOSS G 0 A_PlaySound("baron/attack")
    BOSS G 8 A_BruisAttack  // See BaronBall
    goto See
     Death:
    BOSS I 0 A_Jump (120, "Death2")
    BOSS I 8
    BOSS J 8 A_Scream
    BOSS K 8 A_SpawnItem("BARONGIBBER")
    BOSS L 8 A_NoBlocking
    BOSS M 8 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BOSS N 8
    BOSS O -1 A_BossDeath
    stop
     Death2:
    BOSS P 8
    BOSS Q 8 A_Scream
    BOSS R 8 A_SpawnItem("BARONGIBBER")
    BOSS S 8 A_NoBlocking
    BOSS T 8 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BOSS U 8
    BOSS V -1 A_BossDeath
    stop
	Death.BFGDamage:
    BOS1 P 5
    BOS1 Q 5 A_XScream
    BOS1 R 5 A_SpawnItem("BARONGIBBER")
    BOS1 S 5 A_NoBlocking
    BOS1 TU 5
    BOS1 V 5 A_SpawnItem ("BodyThud", 0, 0, 0, 0)
    BOS1 W 5
    BOS1 X -1 A_BossDeath
    Stop

    }
} 

// Stealth Monsters. Chances are if you ever use these,
// it will not be in a way that enhances gameplay. >:(


ACTOR StealthyArachnotron : SplattyArachnotron replaces StealthArachnotron
{
	Game Doom
	SpawnID 117
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHBABY"
}

ACTOR StealthyArchvile : VaguelySplattyArchvile replaces StealthArchvile
{
	Game Doom
	SpawnID 118
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHVILE"
}

ACTOR StealthyBaron : Baron replaces StealthBaron
{
	Game Doom
	SpawnID 100
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHBARON"
	HitObituary "$OB_STEALTHBARON"
}

ACTOR StealthyCacodemon : Caco replaces StealthCacodemon
{
	Game Doom
	SpawnID 119
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHCACO"
	HitObituary "$OB_STEALTHCACO"
}

ACTOR StealthyChaingunGuy : SplattyChaingunner replaces StealthChaingunGuy
{
	Game Doom
	SpawnID 120
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHCHAINGUY"
}

ACTOR StealthyDemon : SplattyPinky replaces StealthDemon
{
	Game Doom
	SpawnID 121
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHDEMON"
	HitObituary "$OB_STEALTHDEMON"
}

ACTOR StealthyHellKnight : DietBaron replaces StealthHellKnight
{
	Game Doom
	SpawnID 101
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHKNIGHT"
	HitObituary "$OB_STEALTHKNIGHT"
}

ACTOR StealthyDoomImp : SplattyDoomImp replaces StealthDoomImp
{
	Game Doom
	SpawnID 122
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHIMP"
	HitObituary "$OB_STEALTHIMP"
}

ACTOR StealthyFatso : HalfSplattyFatass replaces StealthFatso
{
	Game Doom
	SpawnID 123
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHFATSO"
}

ACTOR StealthyRevenant : Skeleton replaces StealthRevenant
{
	Game Doom
	SpawnID 124
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHUNDEAD"
	HitObituary "$OB_STEALTHUNDEAD"
}

ACTOR StealthyShotgunGuy : SplattyShotgunGuy replaces StealthShotgunGuy
{
	Game Doom
	SpawnID 103
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHSHOTGUNGUY"
}

ACTOR StealthyZombieMan : SplattyZombieMan replaces StealthZombieMan
{
	Game Doom
	SpawnID 102
	+STEALTH
	RenderStyle Translucent
	Alpha 0
	Obituary "$OB_STEALTHZOMBIE"
}

  
