//******
// FIST
//******

// ADVICE: don't forget to make Berserk effect emulation when you make new Fist for your mod.
// This emulation can be simply made with A_JumpIfInventory checking for PowerStrengh which
// is given anytime you take BerserkPack. But you will still have to remake Berserk item itself
// so game doesn't choose usual Fist when player picks Berserk (player will have Fist if he for
// example used give all to look at all the weapons).

Actor Knuckle : Fist replaces Fist
{
weapon.selectionorder 370
weapon.upsound "weapons/draw"
inventory.icon "PUNPA0"
+NOALERT
states
{
	Select:
		TNT1 A 0 A_Raise
		FIST A 1 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_Lower
		FIST A 1 A_Lower
		loop
	Ready:
		FIST A 1 A_WeaponReady
		loop
	Fire:
		FIST A 1 offset(0,35)
		FIST A 1 offset(0,50)
	Hold:
		TNT1 A 0
		TNT1 A 0 A_playsound("weapons/fist/swing")
		TNT1 A 0 A_jumpifinventory("PowerStrength",1,"HoldStrong")
		FIST CDEFG 1 offset(0,32)
		FIST I 3 A_Custompunch(4,0,1,"FistPuff")
		FIST HGFEDCB 1 offset(0,32)
		TNT1 A 0 A_Refire
		FIST A 1 offset(0,65)
		FIST A 1 offset(0,50)
		FIST A 1 offset(0,35)
 		goto ready
	HoldStrong:
		FIST DFG 1 offset(0,32)
		FIST I 3 A_Custompunch(40,0,1,"FistPuffStrong")
		FIST I 1 offset(0,32)
		FIST I 1 offset(-1,33)
		FIST I 1 offset(1,31)
		FIST I 1 offset(2,30)
		FIST GFEDC 1 offset(0,32)
		TNT1 A 0 A_Refire
		FIST A 1 offset(0,65)
		FIST A 1 offset(0,50)
		FIST A 1 offset(0,35)
 		goto ready
 	}
}

Actor FistPuff
{
+NOBLOCKMAP
+NOGRAVITY
+PUFFONACTORS
-ALLOWPARTICLES
+NOEXTREMEDEATH
attacksound "weapons/fist/hitwall"
seesound "weapons/fist/punch"
}

Actor FistPuffStrong : FistPuff
{
decal FistScorch
attacksound "weapons/fist/strong"
seesound "weapons/fist/strong"
-NOEXTREMEDEATH
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("ShotSmoke")
		stop
	}
}
